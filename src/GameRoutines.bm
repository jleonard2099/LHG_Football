'----------------------------------------
'            INFO Subroutine
'----------------------------------------
'This subroutine displays a simple header
'and footer for the game options menu.
Sub INFO ()

    Cls

    Color , 1
    Locate 1, 1
    Print Tab(80);

    Locate , 33
    Color 14
    Print "GAME OPTIONS MENU"

    Locate 24, 1
    Print Tab(80);
    Locate , 1
    Print "    LANCE HAFFNER 1986-2000"; Tab(70); Left$(Time$, 5);
    Locate 3, 1
    Color , 0

End Sub


'----------------------------------------
'           InitVar Subroutine
'----------------------------------------
' Initializes variables for the game
' Does not process any that will be read
' in from file or assigned elsewhere
Sub InitVar ()

    Shared scheduleFile$
    Shared actualAttendance&, avgAttendance&

    Shared gamePK$(), oppName$(), locIndicator$(), gameSite$()
    Shared teamIndicator$()
    Shared tradeLF$(), tradeLI$(), tradeLK$(), tradeLP$()

    Shared oppScore(), teamScore(), powerRating()

    Shared gameAttendance&()

    If ruleOptType <> 0 Then
        For I = 1 To 10
            If Left$(defFormation$(I), 3) = "5-2" Then defFormation$(I) = "3-4" + Right$(defFormation$(I), Len(defFormation$(I)) - 3)
            If Left$(defPlay$(I), 3) = "5-2" Then defPlay$(I) = "3-4" + Right$(defPlay$(I), Len(defPlay$(I)) - 3)
        Next
    End If

    For J1 = 1 To 15:
        defPlay$(J1) = defFormation$(J1)
    Next

    For J1 = 12 To 15:
        defPlay$(J1) = defPlay$(J1 - 7)
    Next

    defPlay$(5) = defPlay$(9)
    defPlay$(6) = defPlay$(10)

    For J1 = 12 To 15:
        defPlay$(J1 - 5) = defPlay$(J1)
    Next

    teamIndicator$(0) = "VISITING TEAM"
    teamIndicator$(1) = "HOME TEAM"

    direction$(0) = "RIGHT"
    direction$(1) = "LEFT"

    gadget$(1) = "FLEA FLICKER"
    gadget$(2) = "REVERSE"
    gadget$(3) = "HB PASS"

    playSelect$(0) = "RUN"
    playSelect$(1) = "PASS"
    playSelect$(3) = "FIELD GOAL ATTEMPT"
    playSelect$(2) = "PUNT"
    playSelect$(4) = "SCOUTING REPORT"
    playSelect$(5) = "CHANGE/SELECT QB"
    playSelect$(6) = "SHOW GAME STATS"
    playSelect$(7) = "QB KNEEL DOWN"
    playSelect$(8) = "TEN MINUTE TICKER "
    playSelect$(9) = "CHANGE/SELECT KICKER"

    downDesc$(0) = "FIRST"
    downDesc$(1) = downDesc$(0)
    downDesc$(2) = "SECOND"
    downDesc$(3) = "THIRD"
    downDesc$(4) = "FOURTH"

    '-- For future releases, let's not initialize
    '-- anything that WILL get assigned by the interface
    autoPlay = 0
    ballFumbled = 0

    'Set this to 1 for a black/white experience
    BW = 0

    catchPctSuccess = 0
    chosenPlay = 0
    CP = 0
    compTeam = 0
    currDown = 0
    D = 0
    dLine = 0
    coverage = 0
    endAllGames = 0
    endGame = 0
    endYds = 0
    endZone = 0
    expCompPct = 0
    fgAttYds = 0
    ffPctSuccess = 0
    gameLoc = 0
    goalPostAdj = 0
    halfTime = 0

    isOT = 0
    JJ = 0
    K3 = 0
    fieldSide = 0
    kickYL = 0
    playSegment = 0
    overtimeOpt = 0
    nbrScores = 0
    playerMode = 0
    playerOpt = 0
    P1 = 0
    playCall = 0
    periodNbr = 0
    puntRetNbr = 0
    quarter = 0
    qback = 0
    qbTakeKnee = 0
    revFumble = 0
    ruleOptColl = 0
    ruleOptPro = 0
    ruleOptType = 0
    playType = 0
    rushYds = 0
    S6 = 0
    startYds = 0
    tickerGames = 0
    W5 = 0
    WE = 0
    WS = 0
    winTeam = 0
    yrdLine = 0
    X = 0
    X1 = 0
    XD = 0
    XE = 0
    ydsToFirst = 0
    YC = 0
    YF = 0
    Z1 = 0
    ydsGained = 0
    ydLine = 0
    ydLineTeam = 0
    ydsToScore = 0
    BO% = 0
    EG% = 0
    F% = 0
    FF% = 0
    goalLnYdAdj = 0
    HB% = 0
    intChance = 0
    indRushPct = 0
    PS% = 0
    firstDownClockOpt = 0

    gameClock! = 0
    pbpDelay! = 0
    timeElapsed! = 0

    actualAttendance& = 0
    avgAttendance& = 0

    defTeam$ = ""
    gameStadium$ = ""
    markers$ = ""
    pbpString$ = ""
    targetWRName$ = ""
    YN$ = ""

    'These below are for reading in game data
    'Erase defInts$, defSacks$, gameMascots$
    'Erase kicker$, kickRet$
    'Erase pret$, punter$, qbacks$, rbacks$
    'Erase wdRec$
    Erase kickerFGA, kickerFGPct, kickerIdx, kickerPATPct
    Erase krNumRet, krYdsPerRet, leagRat_GAME
    Erase playerInts, playerSacks, prNumRet, prYdsPerRet, ydsPerPunt
    Erase qbArmRat, qbCompPct, qbNumAtt, qbPctInt
    Erase rbNumRec, rbRushContrib, rbRushAvg, rbYdsPerC
    Erase teamInts, teamSacks ', teamRat_GAME
    Erase wrNumRec, wrYdsPerC, ydsPerComp


    Erase AF, AM, defInts, defSacks
    Erase FA, FM
    Erase gameLongFG, gameStatsTeam, gameStatsPlayer
    Erase gameZ, gameZ1, gameZ2
    Erase K1, K3
    Erase gameInts, hasRunFF, playerInts, IR
    Erase kickReturners
    Erase LC, LF, LI, LK, LP, LR
    Erase nbrPossOT, PK, puntReturners

    Erase oppScore, powerRating, teamScore

    Erase quarterbacks, qbNumber, qbMobility, runBacks
    Erase score, scoreQuarters, scoreTimes, schedGame
    Erase teamIdx_GAME, timeouts, timePoss
    Erase totalInts, totalSacks
    Erase useRandomQB, sackStatsTeam, WR, XD

    Erase HB%, NG%, indRushPct
    Erase activeKicker, RV%, passCovAdj, ST%, SX%
    Erase teamYears, thirdDownAtt, thirdDownFail
    Erase qbRushIdx, climate

    Erase diskIDs$, gameTeams$
    Erase gameIR$, gameKR$, gameLC$, gameLR$
    Erase gamePK$, gamePR$, gameQB$
    Erase gameRB$, gameSite$, gameWR$
    Erase locIndicator$, oppName$
    Erase SX$
    Erase tradeLF$, tradeLI$, tradeLK$, tradeLP$


End Sub


'----------------------------------------
'          AUTOSETUP Subroutine
'----------------------------------------
'This subroutine auto-assigns game param-
'eters from a schedule file's data.
Sub AUTOSETUP

    For P9 = 0 To 1

        teamNbr = NG%(P9)
        Call ReadGameTeam(diskIDs$(P9), gameTeams$(P9), teamNbr, P9)
        'NG%(P9) = teamNbr
        teamIdx_GAME(P9) = NG%(P9)
        X = 0

    Next P9

    'Assign settings from schedule / etc.
    gameLoc = NG%(2)
    ruleOptType = NG%(3)
    pbpDelay! = NG%(4) / 10
    ruleOptColl = NG%(7)
    playerOpt = NG%(10)
    ruleOptPro = NG%(11)
    tickerStart = NG%(12)

    'If college rules, pre-1974
    If ruleOptPro = 1 And ruleOptType <> 0 Then
        'is this an adjustment for
        'kicking distance based on
        'placement of the goal posts???
        goalPostAdj = -10
    End If

    'If WFL rules
    If ruleOptType = 4 Then
        goalPostAdj = 0
    End If

End Sub


'----------------------------------------
'           TEAMBOX Subroutine
'----------------------------------------
'This subroutine draws an ASCII box using
'team colors. The box starts at row 6 and
'is approx. 60 char's wide. When done, the
'cursor is set at the first printable row
'and column inside the box.
Sub TEAMBOX (boxHeight, teamIdx)

    Locate 6, 8
    Color teamRat_GAME(teamIdx, 2), teamRat_GAME(teamIdx, 1)

    'Print box top
    Print " " + Chr$(214); String$(61, 196); Chr$(183) + " ";

    'Print box sides
    For II = 1 To boxHeight
        Locate 6 + II, 8
        Print " " + Chr$(186); String$(61, 32); Chr$(186) + " ";
    Next

    'Print box bottom
    Locate 7 + boxHeight, 8
    Print " " + Chr$(211); String$(61, 196); Chr$(189) + " ";

    Locate 7, 11

End Sub


'----------------------------------------
'         FRAMEOPTION Subroutine
'----------------------------------------
'This subroutine draws an ASCII box using
'team colors. The box starts at row 8 and
'is approx. 60 char's wide. When done, the
'cursor is set at the first printable row
'and column inside the box.
Sub FRAMEOPTION (boxHeight, teamIdx)

    PCopy 0, 1
    Screen , , 1, 1

    Locate 8, 8
    Color teamRat_GAME(teamIdx, 2), teamRat_GAME(teamIdx, 1)

    'Print box top
    Print " " + Chr$(214); String$(61, 196); Chr$(183) + " ";

    'Print box sides
    For II = 1 To boxHeight
        Locate 8 + II, 8
        Print " " + Chr$(186); String$(61, 32); Chr$(186) + " ";
    Next

    'Print box bottom
    Locate 9 + boxHeight, 8
    Print " " + Chr$(211); String$(61, 196); Chr$(189) + " ";

    Locate 9, 12

End Sub


'----------------------------------------
'          OPTIONBOX Subroutine
'----------------------------------------
'This subroutine draws an ASCII box in grey.
'The box starts at row 8 and is approx. 60
'char's wide. When done, the cursor is set
'at the first printable row and column
'inside the box.
Sub OPTIONBOX (J)

    PCopy 0, 1
    Screen , , 1, 1

    Locate 8, 8
    Color 0, 7

    'Print box top
    Print " " + Chr$(214); String$(61, 196); Chr$(183) + " ";

    'Print box sides
    For II = 1 To J
        Locate 8 + II, 8
        Print " " + Chr$(186); String$(61, 32); Chr$(186) + " ";
    Next

    'Print box bottom
    Locate 9 + J, 8
    Print " " + Chr$(211); String$(61, 196); Chr$(189) + " ";

    Locate 10, 15

End Sub


'----------------------------------------
'         FRAMESTATS Subroutine
'----------------------------------------
'This subroutine draws an ASCII box in grey.
'The box starts at row 8 and is approx. 77
'char's wide. When done, the cursor is set
'at the first printable row and column
'inside the box.
Sub FRAMESTATS (J)

    Locate 1, 1
    Color 0, 7

    Print Chr$(214); String$(77, 196); Chr$(183);

    For II = 1 To J
        Locate 1 + II, 1
        Print Chr$(186); String$(77, 32); Chr$(186);
    Next

    Locate 2 + J, 1
    Print Chr$(211); String$(77, 196); Chr$(189);

    Locate 2, 32

End Sub


'----------------------------------------
'            d100 Subroutine
'----------------------------------------
'This subroutine assigns a random number
'between 1 and 100 to the variable that
'it is called with
Sub d100 (randNumVar)

    randNumVar = Int(Rnd(1) * 100) + 1

End Sub


'----------------------------------------
'            DELAY Subroutine
'----------------------------------------
'This subroutine simply serves as a place-
'holder for the command to initiate the
'pbp delay.
Sub DELAY ()

    _Delay pbpDelay!

End Sub


'----------------------------------------
'             PBP Subroutine
'----------------------------------------
'This is the classic Haffner Play-by-play
'subroutine. Given the target text, it will
'place it in the correct spot in the pbp
'text window. No text is placed if the
'delay is 0.
Sub PBP ()

    M$ = InKey$

    If UCase$(M$) = "Q" Then Cls: End
    If UCase$(M$) = "O" Then Call OPTIONS

    If pbpDelay! > 0 Then
        Call PLACEPBP
        Call DELAY
    End If

End Sub


'----------------------------------------
'           TICKER Subroutine
'----------------------------------------
'This subroutine displays the score of all
'current games being tracked by the ticker.
'For each game it will find the score that
'matches the current game time / period.
Sub TICKER ()

    If Not (autoPlay = 1 And playerMode = 2) Then

        Color 14, 0
        Cls

        Call d100(RN0)

        If halfTime = 1 Then

            If ruleOptType = 0 Then
                If RN0 <= 50 Then
                    Locate , 23: Print "PRUDENTIAL AT THE HALF SCOREBOARD"
                Else
                    Locate , 28: Print "RED MAN HALFTIME REPORT"
                End If
            Else
                If RN0 <= 50 Then
                    Locate , 29: Print "TOYOTA HALFTIME REPORT"
                Else
                    Locate , 29: Print "HONDA HALFTIME REPORT"
                End If
            End If

        Else

            If endGame = 1 Then

                If ruleOptType = 0 Then

                    Locate , 27: Print "CHEVROLET FINAL SCOREBOARD"

                Else
                    Select Case RN0
                        Case 1 To 33:
                            Locate , 27: Print "USA TODAY FINAL SCOREBOARD"

                        Case 34 To 67:
                            Locate , 24: Print "FEDERAL EXPRESS FINAL SCOREBOARD"

                        Case 67 To 100:
                            Locate , 23: Print "ESPN SPORTSCENTER FINAL SCOREBOARD"

                    End Select

                End If

            Else

                If halfTime = 0 Then Locate , 30: Print "LANCE HAFFNER GAMES SCOREBOARD"

            End If

        End If

        Color 15

        If tickerGames = 0 Then
            Locate 10, 28: Print "NO OTHER GAMES SCHEDULED"
        Else
            If quarter = 1 And gameClock! < 900 And gameClock! >= 600 Then timeInterval = 1
            If quarter = 1 And gameClock! < 600 And gameClock! >= 300 Then timeInterval = 2
            If quarter = 1 And gameClock! < 300 Then timeInterval = 3

            If quarter = 2 And gameClock! < 900 And gameClock! >= 600 Then timeInterval = 4
            If quarter = 2 And gameClock! < 600 And gameClock! >= 300 Then timeInterval = 5
            If quarter = 2 And gameClock! < 300 Then timeInterval = 6

            If quarter = 3 And gameClock! < 900 And gameClock! >= 600 Then timeInterval = 9
            If quarter = 3 And gameClock! < 600 And gameClock! >= 300 Then timeInterval = 10
            If quarter = 3 And gameClock! < 300 Then timeInterval = 11

            If quarter = 4 And gameClock! < 900 And gameClock! >= 600 Then timeInterval = 12
            If quarter = 4 And gameClock! < 600 And gameClock! >= 300 Then timeInterval = 13
            If quarter = 4 And gameClock! < 300 Then timeInterval = 14

            If quarter = 5 And gameClock! < 900 And gameClock! >= 600 Then timeInterval = 15
            If quarter = 5 And gameClock! < 600 And gameClock! >= 300 Then timeInterval = 16
            If quarter = 5 And gameClock! < 300 Then timeInterval = 17

            If halfTime = 1 Then timeInterval = 8

            If Left$(SX$(1, 0), 3) <> "XXX" Then
                Locate 2, 1
                X = 1
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(1, 0), 13);
                Locate , 15
                Print Using "## "; SX%(1, 0, tickInterval);
                Print tickerPeriod$(tickInterval)

                Print Left$(SX$(1, 1), 13);
                Locate , 15: Print Using "## #"; SX%(1, 1, tickInterval)
            End If

            If Left$(SX$(2, 0), 3) <> "XXX" Then
                Locate 5, 1
                X = 2
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(2, 0), 13);
                Locate , 15: Print Using "## "; SX%(2, 0, tickInterval);
                Print tickerPeriod$(tickInterval)

                Print Left$(SX$(2, 1), 13);
                Locate , 15: Print Using "## #"; SX%(2, 1, tickInterval)
            End If

            If Left$(SX$(3, 0), 3) <> "XXX" Then
                Locate 8, 1
                X = 3
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(3, 0), 13);
                Locate , 15: Print Using "## "; SX%(3, 0, tickInterval);
                Print tickerPeriod$(tickInterval)

                Print Left$(SX$(3, 1), 13);
                Locate , 15: Print Using "## #"; SX%(3, 1, tickInterval)
            End If

            If Left$(SX$(4, 0), 3) <> "XXX" Then
                Locate 11, 1
                X = 4
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(4, 0), 13);
                Locate , 15: Print Using "## "; SX%(4, 0, tickInterval);
                Print tickerPeriod$(tickInterval)

                Print Left$(SX$(4, 1), 13);
                Locate , 15: Print Using "## #"; SX%(4, 1, tickInterval)
            End If

            If Left$(SX$(5, 0), 3) <> "XXX" Then
                Locate 14, 1
                X = 5
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(5, 0), 13);
                Locate , 15: Print Using "## "; SX%(5, 0, tickInterval);
                Print tickerPeriod$(tickInterval)

                Print Left$(SX$(5, 1), 13);
                Locate , 15: Print Using "## #"; SX%(5, 1, tickInterval)
            End If

            If Left$(SX$(6, 0), 3) <> "XXX" Then
                Locate 17, 1
                X = 6
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(6, 0), 13);
                Locate , 15: Print Using "## "; SX%(6, 0, tickInterval);
                Print tickerPeriod$(tickInterval)

                Print Left$(SX$(6, 1), 13);
                Locate , 15: Print Using "## #"; SX%(6, 1, tickInterval)
            End If

            If Left$(SX$(7, 0), 3) <> "XXX" Then
                Locate 20, 1
                X = 7
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(7, 0), 13);
                Locate , 15: Print Using "## "; SX%(7, 0, tickInterval);
                Print tickerPeriod$(tickInterval)

                Print Left$(SX$(7, 1), 13);
                Locate , 15: Print Using "## #"; SX%(7, 1, tickInterval)
            End If

            If Left$(SX$(8, 0), 3) <> "XXX" Then
                Locate 23, 1
                X = 8
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(8, 0), 13);
                Locate , 15: Print Using "## "; SX%(8, 0, tickInterval);
                Print tickerPeriod$(tickInterval)

                Print Left$(SX$(8, 1), 13);
                Locate , 15: Print Using "## #"; SX%(8, 1, tickInterval);
            End If

            If Left$(SX$(9, 0), 3) <> "XXX" Then
                Locate 2, 21
                X = 9
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(9, 0), 13);
                Locate , 35: Print Using "## "; SX%(9, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 21

                Print Left$(SX$(9, 1), 13);
                Locate , 35: Print Using "## #"; SX%(9, 1, tickInterval)
            End If

            If Left$(SX$(10, 0), 3) <> "XXX" Then
                Locate 5, 21
                X = 10
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(10, 0), 13);
                Locate , 35: Print Using "## "; SX%(10, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 21

                Print Left$(SX$(10, 1), 13);
                Locate , 35: Print Using "## #"; SX%(10, 1, tickInterval)
            End If

            If Left$(SX$(11, 0), 3) <> "XXX" Then
                Locate 8, 21
                X = 11
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(11, 0), 13);
                Locate , 35: Print Using "## "; SX%(11, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 21

                Print Left$(SX$(11, 1), 13);
                Locate , 35: Print Using "## #"; SX%(11, 1, tickInterval)
            End If

            If Left$(SX$(12, 0), 3) <> "XXX" Then
                Locate 11, 21
                X = 12
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(12, 0), 13);
                Locate , 35: Print Using "## "; SX%(12, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 21

                Print Left$(SX$(12, 1), 13);
                Locate , 35: Print Using "## #"; SX%(12, 1, tickInterval)
            End If

            If Left$(SX$(13, 0), 3) <> "XXX" Then
                Locate 14, 21
                X = 13
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(13, 0), 13);
                Locate , 35: Print Using "## "; SX%(13, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 21

                Print Left$(SX$(13, 1), 13);
                Locate , 35: Print Using "## #"; SX%(13, 1, tickInterval)
            End If

            If Left$(SX$(14, 0), 3) <> "XXX" Then
                Locate 17, 21
                X = 14
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(14, 0), 13);
                Locate , 35: Print Using "## "; SX%(14, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 21

                Print Left$(SX$(14, 1), 13);
                Locate , 35: Print Using "## #"; SX%(14, 1, tickInterval)
            End If

            If Left$(SX$(15, 0), 3) <> "XXX" Then
                Locate 20, 21
                X = 15
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(15, 0), 13);
                Locate , 35: Print Using "## "; SX%(15, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 21

                Print Left$(SX$(15, 1), 13);
                Locate , 35: Print Using "## #"; SX%(15, 1, tickInterval)
            End If

            If Left$(SX$(16, 0), 3) <> "XXX" Then
                Locate 23, 21
                X = 16
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(16, 0), 13);
                Locate , 35: Print Using "## "; SX%(16, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 21

                Print Left$(SX$(16, 1), 13);
                Locate , 35: Print Using "## #"; SX%(16, 1, tickInterval);
            End If

            If Left$(SX$(17, 0), 3) <> "XXX" Then
                Locate 2, 41
                X = 17
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(17, 0), 13);
                Locate , 55: Print Using "## "; SX%(17, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 41

                Print Left$(SX$(17, 1), 13);
                Locate , 55: Print Using "## #"; SX%(17, 1, tickInterval)
            End If

            If Left$(SX$(18, 0), 3) <> "XXX" Then
                Locate 5, 41
                X = 18
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(18, 0), 13);
                Locate , 55: Print Using "## "; SX%(18, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 41

                Print Left$(SX$(18, 1), 13);
                Locate , 55: Print Using "## #"; SX%(18, 1, tickInterval)
            End If

            If Left$(SX$(19, 0), 3) <> "XXX" Then
                Locate 8, 41
                X = 19
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(19, 0), 13);
                Locate , 55: Print Using "## "; SX%(19, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 41

                Print Left$(SX$(19, 1), 13);
                Locate , 55: Print Using "## #"; SX%(19, 1, tickInterval)
            End If

            If Left$(SX$(20, 0), 3) <> "XXX" Then
                Locate 11, 41
                X = 20
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(20, 0), 13);
                Locate , 55: Print Using "## "; SX%(20, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 41

                Print Left$(SX$(20, 1), 13);
                Locate , 55: Print Using "## #"; SX%(20, 1, tickInterval)
            End If

            If Left$(SX$(21, 0), 3) <> "XXX" Then
                Locate 14, 41
                X = 21
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(21, 0), 13);
                Locate , 55: Print Using "## "; SX%(21, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 41

                Print Left$(SX$(21, 1), 13);
                Locate , 55: Print Using "## #"; SX%(21, 1, tickInterval)
            End If

            If Left$(SX$(22, 0), 3) <> "XXX" Then
                Locate 17, 41
                X = 22
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(22, 0), 13);
                Locate , 55: Print Using "## "; SX%(22, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 41

                Print Left$(SX$(22, 1), 13);
                Locate , 55: Print Using "## #"; SX%(22, 1, tickInterval)
            End If

            If Left$(SX$(23, 0), 3) <> "XXX" Then
                Locate 20, 41
                X = 23
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(23, 0), 13);
                Locate , 55: Print Using "## "; SX%(23, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 41

                Print Left$(SX$(23, 1), 13);
                Locate , 55: Print Using "## #"; SX%(23, 1, tickInterval)
            End If

            If Left$(SX$(24, 0), 3) <> "XXX" Then
                Locate 23, 41
                X = 24
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(24, 0), 13);
                Locate , 55: Print Using "## "; SX%(24, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 41

                Print Left$(SX$(24, 1), 13);
                Locate , 55: Print Using "## #"; SX%(24, 1, tickInterval);
            End If

            If Left$(SX$(25, 0), 3) <> "XXX" Then
                Locate 2, 61
                X = 25
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(25, 0), 13);
                Locate , 75: Print Using "## "; SX%(25, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 61

                Print Left$(SX$(25, 1), 13);
                Locate , 75: Print Using "## #"; SX%(25, 1, tickInterval)
            End If

            If Left$(SX$(26, 0), 3) <> "XXX" Then
                Locate 5, 61
                X = 26
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(26, 0), 13);
                Locate , 75: Print Using "## "; SX%(26, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 61

                Print Left$(SX$(26, 1), 13);
                Locate , 75: Print Using "## #"; SX%(26, 1, tickInterval)
            End If

            If Left$(SX$(27, 0), 3) <> "XXX" Then
                Locate 8, 61
                X = 27
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(27, 0), 13);
                Locate , 75: Print Using "## "; SX%(27, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 61

                Print Left$(SX$(27, 1), 13);
                Locate , 75: Print Using "## #"; SX%(27, 1, tickInterval)
            End If

            If Left$(SX$(28, 0), 3) <> "XXX" Then
                Locate 11, 61
                X = 28
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(28, 0), 13);
                Locate , 75: Print Using "## "; SX%(28, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 61

                Print Left$(SX$(28, 1), 13);
                Locate , 75: Print Using "## #"; SX%(28, 1, tickInterval)
            End If

            If Left$(SX$(29, 0), 3) <> "XXX" Then
                Locate 14, 61
                X = 29
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(29, 0), 13);
                Locate , 75: Print Using "## "; SX%(29, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 61

                Print Left$(SX$(29, 1), 13);
                Locate , 75: Print Using "## #"; SX%(29, 1, tickInterval)
            End If

            If Left$(SX$(30, 0), 3) <> "XXX" Then
                Locate 17, 61
                X = 30
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(30, 0), 13);
                Locate , 75: Print Using "## "; SX%(30, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 61

                Print Left$(SX$(30, 1), 13);
                Locate , 75: Print Using "## #"; SX%(30, 1, tickInterval)
            End If

            If Left$(SX$(31, 0), 3) <> "XXX" Then
                Locate 20, 61
                X = 31
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(31, 0), 13);
                Locate , 75: Print Using "## "; SX%(31, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 61

                Print Left$(SX$(31, 1), 13);
                Locate , 75: Print Using "## #"; SX%(31, 1, tickInterval)
            End If

            If Left$(SX$(32, 0), 3) <> "XXX" Then
                Locate 23, 61
                X = 32
                tickInterval = TickerTime%(timeInterval, X)

                Print Left$(SX$(32, 0), 13);
                Locate , 75: Print Using "## "; SX%(32, 0, tickInterval);
                Print tickerPeriod$(tickInterval)
                Locate , 61

                Print Left$(SX$(32, 1), 13);
                Locate , 75: Print Using "## #"; SX%(32, 1, tickInterval);
            End If

        End If

        I$ = GetKeyPress$

    End If

    Screen , , 0, 0

End Sub


'----------------------------------------
'          TickerTime Function
'----------------------------------------
'This subroutine determines which of the
'pre-determined time intervals the ticker
'display should be using.
Function TickerTime% (timeInterval, X)

    Shared tickerStart

    tickerIdx = timeInterval + (tickerStart - ST%(X)) * 3

    If tickerIdx < 0 Then tickerIdx = 0
    If tickerIdx > 14 Then tickerIdx = 14
    If endGame = 1 Then tickerIdx = 14

    TickerTime% = tickerIdx

End Function


'----------------------------------------
'        TICKERUPDATE Subroutine
'----------------------------------------
'This subroutine calculates the updated
'scores for the ticker games.
Sub TICKERUPDATE ()

    'Should this be <= 31???
    If autoPlay = 1 And schedGame(2) <= 32 Then

        If quarter = 1 And gameClock! <= 600 And SX%(schedGame(2) + 1, 0, 1) = -1 Then SX%(schedGame(2) + 1, 0, 1) = score(0, 0): SX%(schedGame(2) + 1, 1, 1) = score(1, 0)
        If quarter = 1 And gameClock! <= 300 And SX%(schedGame(2) + 1, 0, 2) = -1 Then SX%(schedGame(2) + 1, 0, 2) = score(0, 0): SX%(schedGame(2) + 1, 1, 2) = score(1, 0)

        If quarter = 2 And gameClock! <= 600 And SX%(schedGame(2) + 1, 0, 4) = -1 Then SX%(schedGame(2) + 1, 0, 4) = score(0, 0): SX%(schedGame(2) + 1, 1, 4) = score(1, 0)
        If quarter = 2 And gameClock! <= 300 And SX%(schedGame(2) + 1, 0, 5) = -1 Then SX%(schedGame(2) + 1, 0, 5) = score(0, 0): SX%(schedGame(2) + 1, 1, 5) = score(1, 0)

        If quarter > 2 And SX%(schedGame(2) + 1, 0, 3) = -1 Then SX%(schedGame(2) + 1, 0, 3) = score(0, 1): SX%(schedGame(2) + 1, 1, 3) = score(1, 1)
        If quarter > 2 And SX%(schedGame(2) + 1, 0, 6) = -1 Then For I2 = 6 To 8: SX%(schedGame(2) + 1, 0, I2) = score(0, 2) + score(0, 1): SX%(schedGame(2) + 1, 1, I2) = score(1, 2) + score(1, 1): Next

        If quarter = 3 And gameClock! <= 600 And SX%(schedGame(2) + 1, 0, 9) = -1 Then SX%(schedGame(2) + 1, 0, 9) = score(0, 0): SX%(schedGame(2) + 1, 1, 9) = score(1, 0)
        If quarter = 3 And gameClock! <= 300 And SX%(schedGame(2) + 1, 0, 10) = -1 Then SX%(schedGame(2) + 1, 0, 10) = score(0, 0): SX%(schedGame(2) + 1, 1, 10) = score(1, 0)

        If quarter > 3 And SX%(schedGame(2) + 1, 0, 11) = -1 Then SX%(schedGame(2) + 1, 0, 11) = score(0, 3) + score(0, 2) + score(0, 1): SX%(schedGame(2) + 1, 1, 11) = score(1, 3) + score(1, 2) + score(1, 1)

        If quarter = 4 And gameClock! <= 600 And SX%(schedGame(2) + 1, 0, 12) = -1 Then SX%(schedGame(2) + 1, 0, 12) = score(0, 0): SX%(schedGame(2) + 1, 1, 12) = score(1, 0)
        If quarter = 4 And gameClock! <= 300 And SX%(schedGame(2) + 1, 0, 13) = -1 Then SX%(schedGame(2) + 1, 0, 13) = score(0, 0): SX%(schedGame(2) + 1, 1, 13) = score(1, 0)

        SX%(schedGame(2) + 1, 0, 14) = score(0, 0)
        SX%(schedGame(2) + 1, 1, 14) = score(1, 0)

    End If

End Sub


'----------------------------------------
'       TickerSchedule Subroutine
'----------------------------------------
'This subroutine reads in the scores from
'the ticker and uses them to update the
'corresponding schedule file.
Sub TickerSchedule ()

    Shared scheduleFile$

    If schedGame(2) <= 32 Then

        For X = 0 To 1: SX$(schedGame(2) + 1, X) = gameTeams$(X): Next

        Open "TICKER" For Output As #1

        For I = 1 To 32:
            For I1 = 0 To 1:
                For I2 = 1 To 14
                    Print #1, SX%(I, I1, I2)
                Next
            Next
        Next

        For I = 1 To 32:
            For I1 = 0 To 1:
                Print #1, SX$(I, I1)
            Next
        Next

        For I = 1 To 32:
            Print #1, ST%(I)
        Next

        Close #1

    End If

    Open diskPaths$(0) + scheduleFile$ As #1 Len = 52

    For X = 0 To 20
        Field #1, X * 2 As X$, 2 As Q$(X + 1), 52 - 2 - 2 * X As X$
    Next

    Field #1, 42 As X$, 3 As Q$(22), 3 As Q$(23), 2 As Q$(24), 2 As Q$(25)

    NG%(13) = 1

    For X = 0 To 20: LSet Q$(X + 1) = MKI$(NG%(X)): Next

    For X = 0 To 1:
        LSet Q$(22 + X) = diskIDs$(X)
    Next

    LSet Q$(24) = MKI$(score(0, 0))
    LSet Q$(25) = MKI$(score(1, 0))

    Put #1, schedGame(0) + schedGame(2)

    Close #1

    schedGame(2) = schedGame(2) + 1

    autoPlay = 1

End Sub


'----------------------------------------
'            SCOUT Subroutine
'----------------------------------------
'This subroutine makes some sort of adj-
'ustments to each team, but it's still not
'clear why it's called "SCOUT"
Sub SCOUT ()

    For I = 0 To 1

        I2 = 0
        I3 = 0
        I4 = 0
        I5 = 0
        I6 = 0

        For I1 = 0 To 9
            If rbNumRec(I, I1) <> 0 Then I2 = rbNumRec(I, I1): I3 = 0
        Next I1

        For I1 = 0 To 9
            If I1 = 0 Then K3(I, 3) = K3(I, 3) + rbNumRec(I, I1) * rbYdsPerC(I, I1): K3(I, 3) = K3(I, 3) + wrYdsPerC(I, I1) * (wrNumRec(I, I1) - I2)
            If I1 > 0 And I1 < 10 And rbNumRec(I, I1) <> 0 Then K3(I, 3) = K3(I, 3) + rbYdsPerC(I, I1) * (rbNumRec(I, I1) - I3)
            If I1 > 0 And I1 < 6 And wrNumRec(I, I1) <> 0 Then K3(I, 3) = K3(I, 3) + wrYdsPerC(I, I1) * (wrNumRec(I, I1) - I6)
            If rbNumRec(I, I1) <> 0 Then I3 = rbNumRec(I, I1)
            If wrNumRec(I, I1) <> 0 Then I6 = wrNumRec(I, I1)
        Next I1

        K3(I, 3) = Int((K3(I, 3) * 3) / 100)

        I2 = 0
        I3 = 0
        I6 = 0

        For I1 = 0 To 9
            If rbRushContrib(I, I1) <> 0 Then I4 = rbRushContrib(I, I1): I5 = 0
        Next I1

        For I1 = 0 To 9
            If I1 = 0 Then K3(I, 0) = K3(I, 0) + rbRushContrib(I, I1) * rbRushAvg(I, I1): I2 = I2 + qbNumAtt(I, I1) * qbCompPct(I, I1): I3 = I3 + qbNumAtt(I, I1) * qbPctInt(I, I1)
            If I1 > 0 And rbRushContrib(I, I1) <> 0 Then K3(I, 0) = K3(I, 0) + rbRushAvg(I, I1) * (rbRushContrib(I, I1) - I5)
            If I1 > 0 And I1 < 4 And qbNumAtt(I, I1) <> 0 Then I2 = I2 + qbCompPct(I, I1) * (qbNumAtt(I, I1) - I6): I3 = I3 + qbPctInt(I, I1) * (qbNumAtt(I, I1) - I6)
            If rbRushContrib(I, I1) <> 0 Then I5 = rbRushContrib(I, I1)
            If qbNumAtt(I, I1) <> 0 Then I6 = qbNumAtt(I, I1)
        Next I1

        If I = 0 Then I1 = 1
        If I = 1 Then I1 = 0

        K3(I, 0) = ((K3(I, 0) * 3) / 100) + ((leagRat_GAME(I1, 0)) * 3): ' HAD (leagRat_GAME()+1)*3
        K3(I, 0) = Int(K3(I, 0) + .5) / 10
        K3(I, 1) = Int((I2 / 100) + leagRat_GAME(I1, 1) + .5)
        K3(I, 2) = Int((I3 / 100) - leagRat_GAME(I1, 2) + .5)
        K3(I, 4) = (ydsPerPunt(I, 0) * 2) + 28
        K3(I, 5) = (kickerFGA(I, activeKicker(I)) * 5) + 25
        K3(I, 6) = kickerIdx(I, activeKicker(I))

        XE = ydsPerComp(I1) / 100

        If XE < .5 Then XD(I1) = Int(XE * 30)
        If XE > .5 Then XD(I1) = Int(((1 - XE) * 30) * -1)

        I4 = I

        If I4 = gameLoc Then

            K3(I4, 0) = K3(I4, 0) + .3
            K3(I4, 1) = K3(I4, 1) + 3

        Else

            If gameLoc <> 2 Then
                K3(I4, 0) = K3(I4, 0) - .3
                K3(I4, 1) = K3(I4, 1) - 3
            End If

        End If

    Next I

End Sub


'----------------------------------------
'          HALFSTART Subroutine
'----------------------------------------
'This subroutine resets the game parameters
'for a new half: yard line, possession,
'new set of downs, timeouts, etc.
Sub HALFSTART ()

    'Team to Kick/Receive
    P1 = winTeam
    D = 1

    If P1 = D Then D = 0

    'Timeouts
    timeouts(0) = 3
    timeouts(1) = 3

    ydLine = 35
    ydsToScore = 65

    '1st and 10
    currDown = 1
    ydsToFirst = 10
    YF = -ydsToFirst

    If ruleOptType <> 0 And goalPostAdj < 0 Then
        ydLine = 40
        ydsToScore = 60
    End If

    If ruleOptType = 1 And ruleOptPro = 3 Or ruleOptType = 4 Then
        ydLine = 30
        ydsToScore = 70
    End If

    If ruleOptType = 0 And ruleOptColl = 0 Then
        ydLine = 40
        ydsToScore = 60
    End If

End Sub


'----------------------------------------
'           KICKER Subroutine
'----------------------------------------
'This subroutine gives the user a choice
'of kicker for a kicking attempt.
Sub CHOOSEKICKER (currTeam)

    Color , 1
    Cls

    Call OPTIONBOX(10)

    Locate , 31: Print "SELECT KICKER": Print
    Locate , 18
    Print " # NAME            FG%  %ATT  EP%  %ATT"

    For I = 0 To 1

        If kicker$(currTeam, I) <> "XXX" And kicker$(currTeam, I) <> "END" And kicker$(currTeam, I) <> "" Then
            Locate , 18
            Print I; "-"; kicker$(currTeam, I);
            Locate , 36
            Print Using " ###   ###  ###   ###"; kickerFGA(currTeam, I) * 5 + 25, kickerFGPct(currTeam, I), kickerIdx(currTeam, I), kickerPATPct(currTeam, I)
        End If

        Locate , 1

    Next I

    Print

    Locate , 19: Print "SELECT (0-1) ";

    Do
        I$ = GetKeyPress$
        choice = Val(I$)
    Loop Until choice = 0 Or choice = 1

    activeKicker(currTeam) = choice

End Sub


'----------------------------------------
'           PLACEPBP Subroutine
'----------------------------------------
'This subroutine is used to position the
'Play-by-Play text in the screen, taking
'care to center the text as well.
Sub PLACEPBP ()

    pbpString$ = Left$(pbpString$, 76)

    Color 14 - 7 * BW, 1 - BW

    Locate 22, 3

    Print String$(74, 46);

    Locate 22, 39 - Len(pbpString$) / 2

    Print " "; pbpString$; " ";

    'Print #11, pbpString$

End Sub


'----------------------------------------
'         DISPWEATHER Subroutine
'----------------------------------------
'This subroutine displays the current
'weather and wind conditions
Sub DISPWEATHER ()

    Color 15 * 1 - BW, 2 + 5 * BW

    Locate 7, 20

    Print "WIND: ";

    If climate(1) = 0 Then Print "NONE ";
    If climate(1) = -7 Then Print "LIGHT ";
    If climate(1) = -15 Then Print "STRONG ";

    If climate(1) <> 0 Then
        If climate(0) = P1 Then
            Print "+";
        Else
            Print "-";
        End If
    End If

    Locate , 40
    Print "WEATHER: ";

    If climate(4) = 0 Then Print "GOOD"
    If climate(4) = -3 Then Print "RAIN/SNOW"
    If climate(4) = -6 Then Print "DOWNPOUR/BLIZZARD"

End Sub


'----------------------------------------
'         CHANGEPOSS Subroutine
'----------------------------------------
'This subroutine literally just swaps the
'indicators as to which team in the arrays
'is defense and which is on offense.
Sub CHANGEPOSS ()

    Swap P1, D

End Sub


'----------------------------------------
'          SETSCREEN Subroutine
'----------------------------------------
'This subroutine essentially draws the
'football field and scoreboard on a fresh
'graphic screen. It has been gutted to
'create the DisplayField routine as one
'that is separate.
Sub SETSCREEN ()

    Screen , , 0, 0

    Color 0, 0, 0

    Cls

    Color 15, 1 + BW * 6

    Print

    'Converted these Chr$ statements so the graphics
    'are preserved in any text editor
    Print Chr$(219); String$(38, 223)
    Print Chr$(219)
    Print Chr$(219)
    Print Chr$(219)

    Call DisplayField

End Sub


'----------------------------------------
'        DisplayField Subroutine
'----------------------------------------
'This subroutine draws the football field
'and scoreboard.
Sub DisplayField ()

    Locate 2, 40
    Color 1 + BW * 6, 0
    Print Chr$(220)

    Locate 3, 2, 0
    Color 15 * (1 - BW), 1 + BW * 6
    Print String$(22, 32); "1  2  3  4   T   ";

    For I = 0 To 1

        Locate I + 4, 2
        Print "  ";

        Color teamRat_GAME(I, 2), teamRat_GAME(I, 1)
        Print gameTeams$(I);

        Color 15 * (1 - BW), 1 + BW * 6
        Print Tab(36);

        Locate I + 4, 23

        For J = 1 To quarter
            Print Using "## "; score(I, J);
        Next J

        Locate , 35
        Print Using "###   "; score(I, 0);

    Next I

    Locate 4 + P1, 3
    Print "*";

    Locate 5 - P1, 3
    Print " ";

    Locate 6, 1
    Color 0, 1 + BW * 6
    Print Chr$(219); String$(39, 220);

    Locate 2, 43
    Color 15, 4 + BW * 3
    Print Chr$(219); String$(36, 223);

    Locate 3, 43
    Print Chr$(219);

    Locate 4, 43
    Print Chr$(219);

    Locate 5, 43
    Print Chr$(219);

    Locate 2, 80
    Color 0: Print Chr$(223);

    Call YARDLINE

    Color 15 * (1 - BW), 4 + BW * 3
    TI = gameClock!
    TI = TI + TI * (TI < 0)
    I = Int(TI / 60)
    J = TI - I * 60

    Call TICKERUPDATE

    Locate 3, 44: Print Using " TIME: ##.##     QUARTER: # "; I + J / 100, quarter;
    Locate 3, 53: Print ":";: Locate , 72: Print Tab(80); " ";

    Locate 4, 44: Print Using " DOWN: #"; currDown;: Print Tab(60);
    Print Using " TO GO: ##.#"; ydsToFirst;: Print Tab(80); " ";

    Locate 5, 44: Print " BALL ON ";
    If ydLine <= 50 Then
        Print "OWN";
    Else
        Print "OPP";
    End If

    Print Using " ##.# YD LINE "; ydLineTeam;
    Print Tab(80); " ";
    Locate 6, 44

    Color 0, 4 + BW * 3: Print String$(37, 220);

    Color 15 * (1 - BW), 2 + 5 * BW

    For I = 7 To 19:
        Locate I, 1
        Print String$(80, 32);
    Next I

    Call DISPWEATHER

    'Replaced special characters with Chr$ references
    'This keeps modern text editors from messing things up

    'Print top row yard markers
    Locate 8, 4
    Color 15 * (1 - BW), 2 + 5 * BW
    Print Chr$(218) + String$(3, 196);

    For II = 1 To 21
        Print String$(2, 196) + Chr$(194);
    Next

    Print String$(5, 196) + Chr$(191);

    'Print end zones and major yard markers
    markers$ = Chr$(179) + String$(5, 176)

    For II = 1 To 20
        markers$ = markers$ + Chr$(179) + "  "
    Next

    markers$ = markers$ + Chr$(179) + String$(5, 176) + Chr$(179)

    For II = 9 To 17
        Locate II, 4
        Print markers$
    Next

    'Print bottom row yard markers
    Locate 18, 4
    Print Chr$(192) + String$(5, 196);

    For II = 1 To 21
        Print Chr$(193) + String$(2, 196);
    Next

    Print String$(3, 196) + Chr$(217);

    'Print minor yard markers
    For II = 10 To 67 Step 3
        For J = 1 To 2
            Locate 11, II + J
            Print Chr$(34);
            Locate 16, II + J
            Print Chr$(34);
        Next
    Next

    'Print major yard numbers
    For II = 1 To 9
        For J = 9 To 17 Step 8
            Locate J, II * 6 + 9
            Print Using "#" + Chr$(179) + "0"; II + 2 * (II - 5) * (II > 5);
        Next
    Next

    'Print play-by-play box
    Locate 19, 1: Print Tab(79)

    Color 14 - 7 * BW, 1 - BW
    Locate 20, 1: Print String$(80, 32);
    Locate 21, 1: Print Chr$(219); String$(78, 223); Chr$(219);
    Locate 22, 1: Print Chr$(219) + " "; String$(76, 46); " " + Chr$(219);
    Locate 23, 1: Print Chr$(219); String$(78, 220); Chr$(219);

End Sub


'----------------------------------------
'          YARDLINE Subroutine
'----------------------------------------
'This subroutine calculates the yardline
'for the ball. This is needed because our
'variables for tracking position only use
'yards to endzone.
Sub YARDLINE ()

    ydLineTeam = ydsToScore - (ydsToScore > 50) * (100 - ydsToScore - ydsToScore)

End Sub


'----------------------------------------
'          GOALLINE Subroutine
'----------------------------------------
'This subroutine calculates the yardage
'gains / loss from a play against the
'goalline defense
Sub GOALLINE ()

    ' *** GOAL LINE DEFENSE ***
    Call d100(RN6)

    If dLine > 3 And dLine < 6 Then

        Select Case ydLine

            Case 94.999 To 95.998

                Select Case RN6
                    Case 1 To 14: ydsGained = 6
                    Case 15 To 17: ydsGained = 4
                    Case 18 To 29: ydsGained = 3
                    Case 30 To 43: ydsGained = 2
                    Case 44 To 63: ydsGained = 1
                    Case 64 To 77: ydsGained = 0
                    Case 78 To 84: ydsGained = -1
                    Case 85 To 91: ydsGained = -2
                    Case 92 To 98: ydsGained = -3
                    Case 99 To 100: ydsGained = -4
                End Select

            Case 95.999 To 96.998

                Select Case RN6
                    Case 1 To 29: ydsGained = 5
                    Case 30 To 31: ydsGained = 3
                    Case 32 To 45: ydsGained = 2
                    Case 46 To 72: ydsGained = 1
                    Case 73 To 86: ydsGained = 0
                    Case 87 To 90: ydsGained = -1
                    Case 91: ydsGained = -2
                    Case 92 To 94: ydsGained = -3
                    Case 95 To 97: ydsGained = -4
                    Case 98 To 100: ydsGained = -5
                End Select

            Case 96.999 To 97.998

                Select Case RN6
                    Case 1 To 24: ydsGained = 4
                    Case 25 To 28: ydsGained = 2
                    Case 29 To 69: ydsGained = 1
                    Case 70 To 85: ydsGained = 0
                    Case 86 To 88: ydsGained = -1
                    Case 89 To 93: ydsGained = -2
                    Case 94 To 100: ydsGained = -4
                End Select

            Case 97.999 To 98.998

                Select Case RN6
                    Case 1 To 43: ydsGained = 3
                    Case 44 To 65: ydsGained = 1
                    Case 66 To 83: ydsGained = 0
                    Case 84 To 92: ydsGained = -1
                    Case 93 To 94: ydsGained = -2
                    Case 95 To 100: ydsGained = -4
                End Select

            Case Is >= 98.999
                Select Case RN6
                    Case 1 To 57: ydsGained = 2
                    Case 58 To 74: ydsGained = 0
                    Case 75 To 83: ydsGained = -1
                    Case 84 To 90: ydsGained = -2
                    Case 91 To 97: ydsGained = -3
                    Case 98 To 100: ydsGained = -4
                End Select

        End Select

    Else

        Select Case ydLine

            Case 94.999 To 95.998

                Select Case RN6
                    Case 1 To 17: ydsGained = 6
                    Case 18 To 22: ydsGained = 4
                    Case 23 To 35: ydsGained = 3
                    Case 36 To 52: ydsGained = 2
                    Case 53 To 76: ydsGained = 1
                    Case 77 To 93: ydsGained = 0
                    Case 94 To 100: ydsGained = -1
                End Select

            Case 95.999 To 96.998

                Select Case RN6
                    Case 1 To 26: ydsGained = 5
                    Case 27 To 29: ydsGained = 3
                    Case 30 To 49: ydsGained = 2
                    Case 50 To 78: ydsGained = 1
                    Case 79 To 94: ydsGained = 0
                    Case 95 To 100: ydsGained = -1
                End Select

            Case 96.999 To 97.998

                Select Case RN6
                    Case 1 To 27: ydsGained = 4
                    Case 28 To 32: ydsGained = 2
                    Case 33 To 80: ydsGained = 1
                    Case 81 To 98: ydsGained = 0
                    Case 99 To 100: ydsGained = -1
                End Select

            Case 97.999 To 98.998

                Select Case RN6
                    Case 1 To 46: ydsGained = 3
                    Case 47 To 72: ydsGained = 1
                    Case 73 To 91: ydsGained = 0
                    Case 92 To 100: ydsGained = -1
                End Select

            Case Is >= 98.999

                Select Case RN6
                    Case 1 To 61: ydsGained = 2
                    Case 62 To 96: ydsGained = 0
                    Case 97 To 100: ydsGained = -1
                End Select

        End Select

    End If

    Call d100(RN7)

    If I2 <= 30 Then
        'Added this to avoid scenarios
        'where the rushing average from
        'OTHERS was negative, therefore
        'leading to a negative index
        If I2 <= 0 Then I2 = 1

        If RN7 <= goalLnYdAdj(I2, 1) Then ydsGained = ydsGained + goalLnYdAdj(I2, 2)

    Else
        'I2 > 30
        'Given how I2 is calculated (based on Rushing Avg),
        'it is not clear how you get a value this high
        If RN7 <= goalLnYdAdj(30, 1) Then ydsGained = ydsGained + goalLnYdAdj(30, 2)

    End If

End Sub


'----------------------------------------
'          NAMEEDIT Subroutine
'----------------------------------------
'This subroutine trims all the spaces from
'team names, mascot names and player names.
Sub NAMEEDIT ()

    For I = 0 To 1

        gameTeams$(I) = RTrim$(gameTeams$(I))
        gameMascots$(I) = RTrim$(gameMascots$(I))

        For I1 = 0 To 9:
            rbacks$(I, I1) = RTrim$(rbacks$(I, I1))
        Next I1

        For I1 = 0 To 5:
            wdRec$(I, I1) = RTrim$(wdRec$(I, I1))
        Next I1

        For I1 = 0 To 3:
            qbacks$(I, I1) = RTrim$(qbacks$(I, I1))
        Next I1

        For I1 = 0 To 2:
            kickRet$(I, I1) = RTrim$(kickRet$(I, I1))
        Next I1

        For I1 = 0 To 2:
            pret$(I, I1) = RTrim$(pret$(I, I1))
        Next I1

        punter$(I, 0) = RTrim$(punter$(I, 0))

        For I1 = 0 To 1:
            kicker$(I, I1) = RTrim$(kicker$(I, I1))
        Next I1

        For I1 = 0 To 14:
            defSacks$(I, I1) = RTrim$(defSacks$(I, I1))
        Next I1

        For I1 = 0 To 9
            defInts$(I, I1) = RTrim$(defInts$(I, I1))
        Next I1

    Next I

End Sub


'----------------------------------------
'          SCOUTREPT Subroutine
'----------------------------------------
'This subroutine clears the screen so it
'can print a quick scouting report
Sub SCOUTREPT ()

    Call OPTIONBOX(13)
    Locate 8, 30: Print "SCOUTING REPORT"

    Locate 10, 30
    Color teamRat_GAME(0, 2), teamRat_GAME(0, 1): Print gameTeams$(0);

    Color 0, 7
    Locate , 52
    Color teamRat_GAME(1, 2), teamRat_GAME(1, 1): Print gameTeams$(1)

    Color 0, 7
    Print
    Locate , 12
    Print "RUSH AVG.";
    Locate , 34: Print Using "#.#"; K3(0, 0);
    Locate , 56: Print Using "#.#"; K3(1, 0)

    Locate , 12: Print "FUM %";
    Locate , 34: Print leagRat_GAME(0, 3) + leagRat_GAME(1, 4);
    Locate , 56: Print leagRat_GAME(1, 3) + leagRat_GAME(0, 4)

    Locate , 12: Print "COMP %";
    Locate , 34: Print K3(0, 1);
    Locate , 56: Print K3(1, 1)

    Locate , 12: Print "INT %";
    Locate , 34: Print K3(0, 2);
    Locate , 56: Print K3(1, 2)

    Locate , 12: Print "AVG/COMP";
    Locate , 34: Print Using "##.#"; K3(0, 3) + XD(1);
    Locate , 56: Print Using "##.#"; K3(1, 3) + XD(0)

    Locate , 12: Print "PUNT AVG";
    Locate , 34: Print K3(0, 4);
    Locate , 56: Print K3(1, 4)

    Locate , 12: Print "FG % ";
    Locate , 34: Print K3(0, 5);
    Locate , 56: Print K3(1, 5)

    Locate , 12: Print "EP % ";
    Locate , 34: Print K3(0, 6);
    Locate , 56: Print K3(1, 6)

    Locate , 12: Print "SACKED %";
    Locate , 34: Print leagRat_GAME(0, 5) + leagRat_GAME(1, 6);
    Locate , 56: Print leagRat_GAME(1, 5) + leagRat_GAME(0, 6)

    Locate , 12: Print "RUN/PASS %";
    Locate , 34: Print leagRat_GAME(0, 7);
    Locate , 56: Print leagRat_GAME(1, 7)

    Locate 12, 38: If K3(0, 0) - (leagRat_GAME(1, 0) * .3) > 4.5 And leagRat_GAME(0, 7) < 50 Then Print "*";
    Locate 12, 60: If K3(1, 0) - (leagRat_GAME(0, 0) * .3) > 4.5 And leagRat_GAME(1, 7) < 50 Then Print "*";

    Locate 16, 38
    If K3(0, 3) >= 18 Then Print "*";

    Locate 16, 60
    If K3(1, 3) >= 18 Then Print "*";

    I$ = GetKeyPress$

End Sub


'----------------------------------------
'          ADDBACKS Subroutine
'----------------------------------------
'This subroutine populates running backs
'to the list of receivers.
Sub ADDBACKS ()

    For I = 0 To 1

        For I1 = 0 To 9

            For I2 = 0 To 3

                If rbacks$(I, I1) = qbacks$(I, I2) Then
                    gameStatsPlayer(I, I1, 0) = gameStatsPlayer(I, I1, 0) + gameStatsPlayer(I, I2 + 10, 0)
                    gameStatsPlayer(I, I1, 1) = gameStatsPlayer(I, I1, 1) + gameStatsPlayer(I, I2 + 10, 1)
                    gameStatsPlayer(I, I1, 2) = gameStatsPlayer(I, I1, 2) + gameStatsPlayer(I, I2 + 10, 2)
                    gameStatsPlayer(I, I2 + 10, 0) = 0
                    gameStatsPlayer(I, I2 + 10, 1) = 0
                    gameStatsPlayer(I, I2 + 10, 2) = 0
                End If

            Next I2

        Next I1

    Next I

End Sub


'----------------------------------------
'           ONSIDE Subroutine
'----------------------------------------
'This subroutine handles the resolution of
'an onside kick and the associated text in
'the Play-by-Play
Sub ONSIDE ()

    Call d100(X)

    pbpString$ = kicker$(P1, activeKicker(P1)) + " IS PLACING THE BALL ON THE TEE"
    Call PBP

    If X <= 50 Then
        pbpString$ = gameTeams$(P1) + " IS LINING UP FOR THE KICKOFF"
    Else
        pbpString$ = gameMascots$(P1) + " ARE LINING UP FOR THE KICKOFF"
    End If

    Call PBP

    pbpString$ = "IT'S AN ONSIDE KICK!!"
    Call PBP

    Swap P1, D

    I = (Rnd(1) * 21) + 5
    ydLine = ydLine + I
    ydsGained = I

    Call AddPossessionTime(4, 7)

    If Int(ydsGained * 10 + .5) / 10 < 9.95 Then
        pbpString$ = "BALL DID NOT TRAVEL 10 YDS"
        Call PBP
        ydLine = 100 - ydLine
    Else
        I = (Rnd(1) * 100) + 1
        If I <= 78 Then
            pbpString$ = gameTeams$(P1) + " RECOVERS"
            Call PBP
            ydLine = 100 - ydLine
        Else
            pbpString$ = gameTeams$(D) + " RECOVERS !!!"
            Call PBP
            Call CHANGEPOSS
        End If
    End If

End Sub


'----------------------------------------
'         INCOMPLETE Subroutine
'----------------------------------------
'This subroutine develops the Play-by-play
'text string for an incomplete pass.
Sub INCOMPLETE ()

    Call QBTHROW

    Call d100(RN0)

    If RN0 > 10 Then

        Call d100(RN1)

        If RN1 > 60 Then

            Call d100(RN2)

            Select Case playCall

                Case 13
                    Select Case RN2
                        Case 1 To 40: pbpString$ = "BUT THE SCREEN FALLS INCOMPLETE"
                        Case 41 To 55: pbpString$ = "BUT THE SCREEN IS BROKEN UP"
                        Case 56 To 60: pbpString$ = "BUT " + targetWRName$ + " CAN'T HANDLE THE PASS"
                        Case 61 To 100: pbpString$ = "BUT THE PASS IS OVERTHROWN AND INCOMPLETE"
                    End Select

                Case 12
                    Select Case RN2
                        Case 1 To 15: pbpString$ = "BUT THE PASS IS BROKEN UP"
                        Case 16 To 44: pbpString$ = "BUT THE PASS IS OVERTHROWN"
                        Case 45 To 74: pbpString$ = "BUT " + targetWRName$ + " CAN'T GET TO THE BALL...INCOMPLETE"
                        Case 75 To 80: pbpString$ = "PASS IS ALMOST PICKED OFF!!...INCOMPLETE"
                        Case 81 To 100: pbpString$ = "BUT " + targetWRName$ + " IS COVERED...INCOMPLETE"
                    End Select

                Case Else
                    Select Case RN2
                        Case 1 To 23: pbpString$ = "BUT THE PASS FALLS INCOMPLETE"
                        Case 24 To 47: pbpString$ = "BUT THE PASS IS THROWN BEHIND " + targetWRName$ + "...INCOMPLETE"
                        Case 48 To 70: pbpString$ = "BUT THE PASS IS OVERTHROWN AND INCOMPLETE"
                        Case 71 To 75: pbpString$ = targetWRName$ + " CAN'T HANDLE THE PASS...INCOMPLETE"
                        Case 76 To 81: pbpString$ = "BUT THE PASS IS BATTED DOWN AT THE LINE OF SCRIMMAGE"
                        Case 82 To 96: pbpString$ = "BUT THE PASS TO " + targetWRName$ + " IS BROKEN UP"
                        Case 97 To 199: pbpString$ = "THE PASS IS BROKEN UP AND ALMOST PICKED OFF!!"
                    End Select

            End Select

        Else

            RN2 = Int(Rnd(1) * 26) + 1

            Select Case RN2
                Case 1: pbpString$ = targetWRName$ + " DROPS A PASS RIGHT TO HIM"
                Case 2: pbpString$ = targetWRName$ + " IS HIT AND CAN'T HANG ON....INCOMPLETE PASS"
                Case 3: pbpString$ = targetWRName$ + " BROKE THE WRONG WAY...INCOMPLETE"
                Case 4: pbpString$ = targetWRName$ + " DROPS IT!"
                Case 5: pbpString$ = "DROPPED BY " + targetWRName$
                Case 6: pbpString$ = "BUT " + targetWRName$ + " CAN'T REACH IT"
                Case 7: pbpString$ = targetWRName$ + " CAN'T HAUL IT IN...INCOMPLETE"
                Case 8: pbpString$ = targetWRName$ + " MISHANDLES IT....INCOMPLETE"
                Case 9: pbpString$ = targetWRName$ + " JUGGLES IT....INCOMPLETE"
                Case 10: pbpString$ = targetWRName$ + " CAN'T FIND THE HANDLE....INCOMPLETE"
                Case 11: pbpString$ = targetWRName$ + " CAN'T PULL IT IN....INCOMPLETE"
                Case 12: pbpString$ = targetWRName$ + " BOBBLES IT....INCOMPLETE"
                Case 13: pbpString$ = targetWRName$ + " WAS OUT OF BOUNDS....INCOMPLETE"
                Case 14: pbpString$ = targetWRName$ + " CAME DOWN OUT OF BOUNDS....INCOMPLETE"
                Case 15: pbpString$ = "THE PASS IS BROKEN UP"
                Case 16: pbpString$ = "BUT " + targetWRName$ + " CAN'T HANDLE THE PASS"
                Case 17: pbpString$ = "BUT THE PASS IS OVERTHROWN"
                Case 18: pbpString$ = "BUT " + targetWRName$ + " CAN'T GET TO THE BALL...INCOMPLETE"
                Case 19: pbpString$ = "PASS IS ALMOST PICKED OFF!!...INCOMPLETE"
                Case 20: pbpString$ = "BUT " + targetWRName$ + " IS COVERED...INCOMPLETE"
                Case 21: pbpString$ = "BUT THE PASS FALLS INCOMPLETE"
                Case 22: pbpString$ = "BUT THE PASS IS THROWN BEHIND " + targetWRName$ + "...INCOMPLETE"
                Case 23: pbpString$ = targetWRName$ + " CAN'T HANDLE THE PASS...INCOMPLETE"
                Case 24: pbpString$ = "BUT THE PASS IS BATTED DOWN AT THE LINE OF SCRIMMAGE"
                Case 25: pbpString$ = "BUT THE PASS TO " + targetWRName$ + " IS BROKEN UP"
                Case 26: pbpString$ = "THE PASS IS BROKEN UP AND ALMOST PICKED OFF!!"
            End Select

        End If

    Else

        ' ***PASS BROKEN UP ***
        RN1 = Int(Rnd(1) * 6) + 1
        Select Case RN1
            Case 1: pbpString$ = "PASS IS BROKEN UP"
            Case 2: pbpString$ = "PASS IS TIPPED AWAY"
            Case 3: pbpString$ = "PASS IS KNOCKED AWAY"
            Case 4: pbpString$ = "PASS IS KNOCKED DOWN"
            Case 5: pbpString$ = "PASS IS DEFLECTED"
            Case 6: pbpString$ = "PASS IS BATTED AWAY"
        End Select

        'Uses players with INTs as names for the work
        If defInts(1 - P1) > 0 Then
            Call FINDINT
            pbpString$ = pbpString$ + " BY " + defTeam$
        End If

    End If

    Call PBP

End Sub

'----------------------------------------
'           QBSNEAK Subroutine
'----------------------------------------
'This subroutine handles the yard gain and
'Play-by-Play text for a QB sneak.
Sub QBSNEAK ()

    ' *** QB SNEAK ***
    pbpString$ = qbacks$(P1, qbNumber(P1)) + " KEEPS IT ON THE SNEAK"

    Call PBP

    Call d100(RN4)

    If (coverage = 7 Or coverage = 9 Or coverage = 11) Then

        If RN4 <= 20 Then
            ydsGained = (Int(Rnd(1) * 6) - 3) / 10
        Else
            ydsGained = (Int(Rnd(1) * 7) + 3) / 10
        End If

    Else

        Select Case dLine

            Case 1, 2

                Select Case RN4
                    Case 1 To 10
                        ydsGained = (Int(Rnd(1) * 6) - 3) / 10

                    Case 11 To 80
                        ydsGained = (Int(Rnd(1) * 7) + 3) / 10

                    Case 81 To 100
                        ydsGained = Int(Rnd(1) * 2) + 1
                End Select

            Case 3, 4
                If RN4 <= 30 Then
                    ydsGained = (Int(Rnd(1) * 6) - 3) / 10
                Else
                    ydsGained = (Int(Rnd(1) * 7) + 3) / 10
                End If

            Case 5, 6
                If RN4 <= 15 Then
                    ydsGained = (Int(Rnd(1) * 6) - 3) / 10
                Else
                    ydsGained = (Int(Rnd(1) * 7) + 3) / 10
                End If

            Case 7 To 10
                Select Case RN4
                    Case 1 To 10
                        ydsGained = (Int(Rnd(1) * 6) - 3) / 10

                    Case 11 To 80
                        ydsGained = (Int(Rnd(1) * 7) + 3) / 10

                    Case 81 To 100
                        ydsGained = Int(Rnd(1) * 2) + 1

                End Select

            Case 11
                If RN4 <= 40 Then
                    ydsGained = (Int(Rnd(1) * 6) - 3) / 10
                Else
                    ydsGained = (Int(Rnd(1) * 7) + 3) / 10
                End If

        End Select

    End If

    pbpString$ = "HE GAINS"

    If Int((ydsGained + ydLine) * 10 + .5) / 10 >= 99.94999 Then
        pbpString$ = pbpString$ + Str$(Int(100 - ydLine))
    Else
        pbpString$ = pbpString$ + Str$(Int((ydsGained) * 10 + .5) / 10)
    End If

    pbpString$ = pbpString$ + " YARDS ON THE PLAY"
    Call PBP

    I1 = qback
    X = 4
    X1 = 9
    playType = 1

End Sub


'----------------------------------------
'           OVERUSE Subroutine
'----------------------------------------
'This subroutine attempts to adjust the
'opponents response to a play that is
'being overused.
Sub OVERUSE ()

    If gameStatsTeam(P1, 1) + gameStatsTeam(P1, 3) <> 0 Then

        If (gameStatsTeam(P1, 1) / (gameStatsTeam(P1, 1) + gameStatsTeam(P1, 3))) < (leagRat_GAME(P1, 7) / 100) And K3(P1, 3) >= 18 Then I4 = I4 * .65

        If I4 <= 0 Then I4 = 1

    End If

End Sub


'----------------------------------------
'           BLITZ Subroutine
'----------------------------------------
'This subroutine determines the yardage
'gains for a blitz defense for when a
'pass will be completed.
Sub BLITZ ()

    Call d100(RN5)
    XE = ydsPerComp(D) / 100

    If XE <= .5 Then
        XE = Int(XE * 100 + .5)
        If XE > 21 Then XE = 21
        If XE < 1 Then XE = 1
        If RN5 <= PS%(1, XE) Then I4 = I4 + PS%(2, XE)
    Else
        XE = Int((1 - XE) * 100 + .5)
        If XE > 21 Then XE = 21
        If XE < 1 Then XE = 1
        If RN5 <= PS%(1, XE) Then I4 = I4 - PS%(2, XE)
    End If

    Call d100(RN0)
    If qbArmRat(P1, qbNumber(P1)) > 20 Then qbArmRat(P1, qbNumber(P1)) = 20
    If qbArmRat(P1, qbNumber(P1)) < -20 Then qbArmRat(P1, qbNumber(P1)) = -20

    QX = qbArmRat(P1, qbNumber(P1))

    If QX <> 0 Then

        If QX < 0 Then QX = QX * -1

        If RN0 <= ydsPerCompPctAdj(QX, 1) Then
            If qbArmRat(P1, qbNumber(P1)) > 0 Then
                I4 = I4 + ydsPerCompPctAdj(QX, 2)
            Else
                I4 = I4 - ydsPerCompPctAdj(QX, 2)
            End If
        End If

    End If

    Call OVERUSE
    I7 = 4

    altYdg = 0

    Select Case playCall
        Case 9
            I7 = 1

        Case 10
            I7 = 3
            If coverage = 9 Or coverage = 10 Then
                I8 = 21
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

            If coverage = 11 Then
                I8 = 25
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

        Case 11
            If coverage = 7 Or coverage = 8 Then
                I8 = 21
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

            If coverage = 9 Or coverage = 10 Then
                I8 = 25
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

            If coverage = 11 Then I8 = 28: Call BlitzYardageLong(I8): altYdg = 1

        Case 12
            I7 = 5
            If coverage = 7 Or coverage = 8 Then
                I8 = 33
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

            If coverage >= 9 Then
                I8 = 42
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

        Case 13
            I7 = 2
            If coverage = 9 Or coverage = 10 Then
                I8 = 25
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

            If coverage = 11 Then
                I8 = 36
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

        Case 14
            If coverage = 7 Or coverage = 8 Then
                I8 = 25
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

            If coverage = 9 Or coverage = 10 Then
                I8 = 28
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

            If coverage = 11 Then
                I8 = 31
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

        Case 15
            I7 = 5
            If coverage >= 7 Then
                I8 = 42
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

        Case 16
            If coverage = 7 Or coverage = 8 Then
                I8 = 18
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

            If coverage = 9 Or coverage = 10 Then
                I8 = 28
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

            If coverage = 11 Then
                I8 = 33
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

        Case 17, 18
            If coverage = 7 Or coverage = 9 Then
                I8 = 21
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

            If coverage = 8 Then
                I8 = 25
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

            If coverage >= 10 Then
                I8 = 28
                Call BlitzYardageLong(I8)
                altYdg = 1
            End If

    End Select

    'If coverage >= 7 Then
    'Call BlitzYardageLong(I8)
    'altYdg = 1
    'End If

    If altYdg <> 1 Then

        I4 = I4 + passCovAdj(2, playCall - 8, coverage)

        If I4 > 10 Then I4 = 10
        If I4 < 1 Then I4 = 1

        Call d100(RN9)

        For I = 1 To 14
            If RN9 <= S2%(I7, I4, I) Then Exit For
        Next I

        'This calculates shorter gains after
        'an unsuccessful blitz attempt.
        ' Yardage ranges from -4 to 101
        ' 50% chance of gains under 30
        Select Case I
            Case 1: ydsGained = Int(Rnd(1) * 5) - 4
            Case 2: ydsGained = Int(Rnd(1) * 3) + 1
            Case 3: ydsGained = Int(Rnd(1) * 4) + 4
            Case 4: ydsGained = Int(Rnd(1) * 8) + 8
            Case 5: ydsGained = Int(Rnd(1) * 5) + 16
            Case 6: ydsGained = Int(Rnd(1) * 5) + 21
            Case 7: ydsGained = Int(Rnd(1) * 5) + 26
            Case 8: ydsGained = Int(Rnd(1) * 11) + 30
            Case 9: ydsGained = Int(Rnd(1) * 11) + 40
            Case 10: ydsGained = Int(Rnd(1) * 11) + 50
            Case 11: ydsGained = Int(Rnd(1) * 11) + 60
            Case 12: ydsGained = Int(Rnd(1) * 11) + 70
            Case 13: ydsGained = Int(Rnd(1) * 11) + 80
            Case 14: ydsGained = Int(Rnd(1) * 11) + 90
        End Select

    End If

End Sub


'----------------------------------------
'      BlitzYardageLong Subroutine
'----------------------------------------
'This subroutine calculates longer gains
'from a play after an unsuccessful blitz
'attempt.
'Yardage ranges from 31 to 101
Sub BlitzYardageLong (I8)

    ' *** BLITZ ***
    Select Case I8
        Case 18: ydsGained = Int(Rnd(1) * 21) + 10
        Case 21: ydsGained = Int(Rnd(1) * 31) + 10
        Case 25: ydsGained = Int(Rnd(1) * 41) + 10
        Case 28: ydsGained = Int(Rnd(1) * 51) + 10
        Case 31: ydsGained = Int(Rnd(1) * 41) + 20
        Case 33: ydsGained = Int(Rnd(1) * 71) + 10
        Case 36: ydsGained = Int(Rnd(1) * 91) + 10
        Case 42: ydsGained = Int(Rnd(1) * 71) + 30
        Case Else: ydsGained = ydsGained + (I4 - 4) * 3
    End Select

End Sub


'----------------------------------------
'           BLITZ2 Subroutine
'----------------------------------------
'This is another routine for calculating
'blitz yardage gains.
Sub BLITZ2 ()

    Call d100(RN4)

    Select Case I2

        Case Is <= 15

            Select Case RN4

                Case 1 To 15
                    ydsGained = Int(Rnd(1) * 5) - 4

                Case 16 To 60
                    ydsGained = Int(Rnd(1) * 2) + 1

                Case 61 To 100
                    ydsGained = Int(Rnd(1) * (I2 * 2.22)) + 1

            End Select

        Case 16, 17

            Select Case RN4

                Case 1 To 10
                    ydsGained = Int(Rnd(1) * 5) - 4

                Case 11 To 50
                    ydsGained = Int(Rnd(1) * 2) + 1

                Case 51 To 100
                    ydsGained = Int(Rnd(1) * (I2 * 1.78)) + 1

            End Select

        Case 18, 19

            Select Case RN4

                Case 1 To 10
                    ydsGained = Int(Rnd(1) * 5) - 4

                Case 11 To 40
                    ydsGained = Int(Rnd(1) * 2) + 1

                Case 41 To 100
                    ydsGained = Int(Rnd(1) * (I2 * 1.54)) + 1

            End Select

        Case 20, 21

            Select Case RN4

                Case 1 To 5
                    ydsGained = Int(Rnd(1) * 5) - 4

                Case 6 To 30
                    ydsGained = Int(Rnd(1) * 2) + 1

                Case 31 To 100
                    ydsGained = Int(Rnd(1) * (I2 * 1.32)) + 1

            End Select

        Case Else

            Select Case RN4

                Case 1 To 5
                    ydsGained = Int(Rnd(1) * 5) - 4

                Case 6 To 20
                    ydsGained = Int(Rnd(1) * 2) + 1

                Case 21 To 100
                    ydsGained = Int(Rnd(1) * (I2 * 1.2)) + 1

            End Select

    End Select

End Sub


'----------------------------------------
'          COMPCHEAT Subroutine
'----------------------------------------
'This subroutine appears to help the comp-
'uter team pick a more useful defense that
'is baseds on the chosen offense. But that
'is somewhat a guess.
Sub COMPCHEAT ()

    coverage = 1

    RN0 = Int(Rnd(1) * 4) + 1
    Call d100(RN3)

    If RN0 = 2 Then

        Select Case playCall

            Case 1, 2, 6, 8
                If RN3 <= 50 Then
                    dLine = 3
                Else
                    dLine = 4
                End If

            Case 3, 4, 5, 7
                dLine = 2

            Case 9, 10, 13
                coverage = 3

            Case 11, 14
                If RN3 <= 50 Then
                    coverage = 3
                Else
                    coverage = 5
                End If

            Case 12, 15
                coverage = 1

            Case 16
                coverage = 2

            Case Is >= 17
                coverage = 3

        End Select

    End If

End Sub


'----------------------------------------
'           PBPRINT Subroutine
'----------------------------------------
'This subroutine handles the Play-by-play
'text for an interceptions
Sub PBPINT ()

    pbpString$ = ""

    If defSacks(D) <> 0 Then

        M5 = 0
        RN0 = Int(Rnd(1) * teamSacks(D)) + 1

        For Z1 = 0 To 14
            M5 = M5 + playerSacks(D, Z1)
            If RN0 <= M5 Then pbpString$ = defSacks$(D, Z1) + " OF ": Exit For
        Next

    End If

End Sub


'----------------------------------------
'        CheckGameClock Subroutine
'----------------------------------------
'This subroutine looks to see if the clock
'has expired and adjust the play time and
'possession based on time that was left on
'the clock.
Sub CheckGameClock ()

    If gameClock! <= 0 Then

        TE = gameClock!
        timePoss(P1) = timePoss(P1) + TE
        gameClock! = 0

    End If

End Sub


'----------------------------------------
'           DEFENSE Subroutine
'----------------------------------------
'This subroutine appears to determine the
'defense to call bed on the game clock
'and yardage situation.
Sub DEFENSE ()

    skipY90 = 0

    If Not (quarter = 2 And gameClock! <= 120) Or Not (quarter >= 4 And gameClock! <= 120) Then

        Select Case currDown

            Case 1
                If ydsToFirst > 10 Then
                    Call d100(RN0)
                    dLine = 1
                    coverage = 2

                    Select Case RN0

                        Case 1 To 5
                            dLine = 9
                            Call d100(RN1)

                            Select Case RN1

                                Case 1 To 45: coverage = 7

                                Case 46 To 90: coverage = 8

                                Case 91 To 95: coverage = 9

                                Case 96 To 100: coverage = 10

                            End Select

                        Case 6 To 20
                            dLine = 10
                            coverage = 3

                        Case 21 To 55
                            dLine = 1
                            coverage = 5

                        Case 56 To 90
                            dLine = 2

                        Case 91 To 95
                            coverage = 1

                    End Select

                End If

                If ydsToFirst < 10 Then
                    '8867
                    Call d100(RN0)
                    dLine = 1
                    coverage = 1
                    If RN0 <= 50 Then
                        dLine = 2
                        coverage = 3
                    Else
                        dLine = 4
                    End If

                    skipY90 = 1

                End If

            Case 2

                If ydsToFirst > 8 Then

                    Call d100(RN0)
                    dLine = 1
                    coverage = 2

                    Select Case RN0

                        Case 1 To 5
                            dLine = 9
                            Call d100(RN1)

                            Select Case RN1

                                Case 1 To 45: coverage = 7

                                Case 46 To 90: coverage = 8

                                Case 91 To 95: coverage = 9

                                Case 96 To 100: coverage = 10

                            End Select

                        Case 6 To 20
                            dLine = 10
                            coverage = 3

                        Case 21 To 55
                            dLine = 1
                            coverage = 5

                        Case 56 To 90
                            dLine = 2

                        Case 91 To 95
                            coverage = 1

                    End Select

                End If

                If ydsToFirst < 5 Then
                    '8867
                    Call d100(RN0)
                    dLine = 1
                    coverage = 1
                    If RN0 <= 50 Then
                        dLine = 2
                        coverage = 3
                    Else
                        dLine = 4
                    End If

                    skipY90 = 1

                End If

            Case 3

                If ydsToFirst >= 3 Then

                    Call d100(RN0)
                    dLine = 1
                    coverage = 2

                    Select Case RN0
                        Case 1 To 5
                            dLine = 9
                            Call d100(RN1)

                            Select Case RN1

                                Case 1 To 45: coverage = 7

                                Case 46 To 90: coverage = 8

                                Case 91 To 95: coverage = 9

                                Case 96 To 100: coverage = 10

                            End Select

                        Case 6 To 20
                            dLine = 10
                            coverage = 3

                        Case 21 To 55
                            dLine = 1
                            coverage = 5

                        Case 56 To 90
                            dLine = 2

                        Case 91 To 95
                            coverage = 1

                    End Select

                Else
                    '8867
                    Call d100(RN0)
                    dLine = 1
                    coverage = 1
                    If RN0 <= 50 Then
                        dLine = 2
                        coverage = 3
                    Else
                        dLine = 4
                    End If

                    skipY90 = 1

                End If

            Case 4

                If ydLine >= 68 Then

                    If (quarter = 4 And score(D, 0) - score(P1, 0) > 10) Or (quarter >= 3 And score(D, 0) - score(P1, 0) > 21) Then

                        Call d100(RN0)
                        dLine = 1
                        coverage = 2

                        Select Case RN0

                            Case 1 To 5
                                dLine = 9
                                Call d100(RN1)

                                Select Case RN1

                                    Case 1 To 45: coverage = 7

                                    Case 46 To 90: coverage = 8

                                    Case 91 To 95: coverage = 9

                                    Case 96 To 100: coverage = 10

                                End Select

                            Case 6 To 20
                                dLine = 10
                                coverage = 3

                            Case 21 To 55
                                dLine = 1
                                coverage = 5

                            Case 56 To 90
                                dLine = 2

                            Case 91 To 95
                                coverage = 1

                        End Select

                    End If

                End If

                If ydsToFirst >= 3 Then

                    Call d100(RN0)
                    dLine = 1
                    coverage = 2

                    Select Case RN0

                        Case 1 To 5
                            dLine = 9
                            Call d100(RN1)

                            Select Case RN1

                                Case 1 To 45: coverage = 7

                                Case 46 To 90: coverage = 8

                                Case 91 To 95: coverage = 9

                                Case 96 To 100: coverage = 10

                            End Select

                        Case 6 To 20
                            dLine = 10
                            coverage = 3

                        Case 21 To 55
                            dLine = 1
                            coverage = 5

                        Case 56 To 90
                            dLine = 2

                        Case 91 To 95
                            coverage = 1

                    End Select

                Else
                    '8867
                    Call d100(RN0)
                    dLine = 1
                    coverage = 1
                    If RN0 <= 50 Then
                        dLine = 2
                        coverage = 3
                    Else
                        dLine = 4
                    End If

                    skipY90 = 1

                End If

            Case Else

                Call d100(RN0)

                If RN0 <= leagRat_GAME(P1, 7) Then

                    Call d100(RN1)
                    dLine = 1
                    coverage = 1
                    If RN1 <= 50 Then
                        dLine = 2
                        coverage = 3
                    Else
                        dLine = 4
                    End If

                Else

                    Call d100(RN1)
                    dLine = 1
                    coverage = 2

                    Select Case RN1

                        Case 1 To 5
                            dLine = 9
                            Call d100(RN2)

                            Select Case RN2

                                Case 1 To 45: coverage = 7

                                Case 46 To 90: coverage = 8

                                Case 91 To 95: coverage = 9

                                Case 96 To 100: coverage = 10

                            End Select

                        Case 6 To 20
                            dLine = 10
                            coverage = 3

                        Case 21 To 55
                            dLine = 1
                            coverage = 5

                        Case 56 To 90
                            dLine = 2

                        Case 91 To 95
                            coverage = 1

                    End Select

                End If

        End Select

        If skipY90 <> 1 Then
            If ydLine >= 90 And coverage = 5 Or ydsToFirst < 7 And coverage = 5 Then coverage = 3
        End If

    Else

        If score(P1, 0) - score(D, 0) <= 0 Then

            Call d100(RN0)

            If RN0 <= 50 Then
                dLine = Int(Rnd(1) * 3) + 4
            Else
                dLine = 3
            End If

            Call d100(RN0)

            Select Case RN0

                Case 1 To 33: coverage = 1

                Case 34 To 67: coverage = 2

                Case 68 To 100: coverage = 5

            End Select

        Else

            Call d100(RN0)
            Call d100(RN1)

            Select Case RN1
                Case 1 To 50:
                    dLine = 1
                    If RN0 <= 90 Then
                        coverage = 1
                    Else
                        coverage = 2
                    End If

                Case 51 To 70
                    dLine = 2
                    If RN0 <= 90 Then
                        coverage = 1
                    Else
                        coverage = 2
                    End If

                Case 71 To 94
                    dLine = 3
                    If RN0 <= 90 Then
                        coverage = 1
                    Else
                        coverage = 2
                    End If

                Case 95
                    dLine = 9
                    coverage = 10

                Case 96
                    dLine = 10
                    coverage = 9

                Case 97, 98
                    dLine = 9

                Case 99, 100
                    dLine = 10
                    coverage = 7

            End Select

        End If

    End If

End Sub


'----------------------------------------
'           MARKER Subroutine
'----------------------------------------
'This subroutine prints the yard line
'marker on the field
Sub MARKER ()

    If P1 Then
        J = FNP(ydsToScore) - 3
    Else
        J = FNP(100 - ydsToScore) - 3
    End If

    Locate 13, 4

    Color 15 * (1 - BW), 2 + 5 * BW

    If J < WS Then

        Print Left$(markers$, J - 1);

        Color 0: Print Chr$(16 + P1);

        Color 15 * (1 - BW): Print Mid$(markers$, J + 1, WS - J - 1);

        Color 2 + 5 * BW, 7 * (1 - BW): Print Mid$(markers$, WS, 7);

        Color 15 * (1 - BW), 2 + 5 * BW: Print Right$(markers$, 67 - WS);

    Else

        If J > WE Then

            Print Left$(markers$, WS - 1);

            Color 2 + 5 * BW, 7 * (1 - BW): Print Mid$(markers$, WS, 7);

            Color 15 * (1 - BW), 2 + 5 * BW: Print Mid$(markers$, WE + 1, J - WE - 1);

            Color 0: Print Chr$(16 + P1);

            Color 15 * (1 - BW), 2 + 5 * BW: Print Right$(markers$, 73 - J);

        Else

            Print Left$(markers$, WS - 1);

            Color 2 + 5 * BW, 7 * (1 - BW): Print Mid$(markers$, WS, J - WS);

            Color BW * 15: Print Chr$(16 + P1);

            Color 2 + 5 * BW, 7 * (1 - BW): Print Mid$(markers$, J + 1, WE - J);

            Color 15 * (1 - BW), 2 + 5 * BW: Print Right$(markers$, 73 - WE);

        End If

    End If


End Sub


'----------------------------------------
'           MARKER1 Subroutine
'----------------------------------------
'This subroutine also has something to do
'with printing the yard marker.
Sub MARKER1 ()

    ydsToScore = 100 - ydLine
    YF = -ydsToFirst

    If P1 Then
        WS = FNP(ydsToScore + YF) - 3
        WE = WS + 6
    Else
        WE = FNP(100 - ydsToScore - YF) - 3
        WS = WE - 6
    End If

    Call MARKER

End Sub


'----------------------------------------
'          MOVEBALL Subroutine
'----------------------------------------
'This subroutine handles the play-by-play
'text for after the ball has moved. With
'that also comes Play-by-Play for the
'tackle / stop.
Sub MOVEBALL ()

    If pbpDelay! <> 0 Then

        I$ = "He's at "

        If endYds <= startYds Then

            For ydsToScore = startYds To endYds Step -1

                Call MARKER
                _Delay pbpDelay! / 20
                Call YARDLINE
                yrdLine = ydLineTeam

                If ydsToScore <= 0 Then
                    ydsToScore = endYds
                Else
                    If Int(yrdLine / 10) * 10 = yrdLine Then
                        pbpString$ = I$ + "the" + Str$(yrdLine) + " yard line"
                        Call PLACEPBP
                        I$ = ""
                    End If
                End If

            Next ydsToScore

        Else

            For ydsToScore = startYds To endYds

                Call MARKER
                _Delay pbpDelay! / 20
                Call YARDLINE
                yrdLine = ydLineTeam

                If ydsToScore <= 0 Then
                    ydsToScore = endYds
                Else
                    If Int(yrdLine / 10) * 10 = yrdLine Then
                        pbpString$ = I$ + "the" + Str$(yrdLine) + " yard line"
                        Call PLACEPBP
                        I$ = ""
                    End If
                End If

            Next ydsToScore

        End If

        If ydsToScore <= 0 Then
            ydsToScore = 0
        Else
            ydsToScore = ydsToScore + 1

            Call YARDLINE

            RN0 = Int(Rnd(1) * 21) + 1

            Select Case RN0
                Case 1: I$ = " IS TACKLED"
                Case 2: I$ = " GOES OUT OF BOUNDS"
                Case 3: I$ = " IS HIT"
                Case 4: I$ = " GOES DOWN"
                Case 4: I$ = " IS GANG TACKLED"
                Case 5: I$ = " IS STOPPED"
                Case 6: I$ = " IS TRIPPED UP"
                Case 7: I$ = " IS WRAPPED UP"
                Case 8: I$ = " IS RIDDEN DOWN"
                Case 9: I$ = " IS LEVELED"
                Case 10: I$ = " IS PULLED DOWN"
                Case 11: I$ = " IS HAULED DOWN"
                Case 12: I$ = " IS FORCED OUT"
                Case 13: I$ = " IS SNOWED UNDER"
                Case 14: I$ = " IS THROWN DOWN"
                Case 15: I$ = " IS POPPED"
                Case 16: I$ = " IS SMACKED"
                Case 17: I$ = " IS SLAMMED"
                Case 18: I$ = " IS STOPPED"
                Case 19: I$ = " IS POPPED"
                Case 19: I$ = " STEPS OUT"
                Case 20: I$ = " IS PUSHED OUT"
                Case 21: I$ = " IS KNOCKED OUT OF BOUNDS"
            End Select

            If ydsToScore >= 100 Then
                pbpString$ = "THE BALL CARRIER" + I$ + " FOR A" + Str$(Int(-ydLine)) + " YARD LOSS"
            Else
                If ydsToScore > 0 Then
                    Call MARKER
                    pbpString$ = "AND HE" + I$ + " AT THE" + Str$(Int(yrdLine)) + " YARD LINE"
                End If

            End If

        End If

        If ydsToScore <> 0 Then Call PBP

    End If

End Sub


'----------------------------------------
'         SCOREBOARD Subroutine
'----------------------------------------
'This subroutine updates the scoreboard
'with the latest values for the clock,
'and possession location.
Sub SCOREBOARD ()

    Screen , , 0, 0

    If Screen(2, 1) <> 219 Then
        Call SETSCREEN
        Call MARKER1
    Else

        YF = -ydsToFirst
        ydsToScore = 100 - ydLine

        '-- The value of BW is always 0.
        Color 15 * (1 - BW), 1 + BW * 6
        Locate 4 + P1, 3

        'Print indicator of team with possession
        Print "*";: Locate 5 - P1, 3: Print " ";

        Call YARDLINE

        Color 15 * (1 - BW), 4 + BW * 3

        TI = gameClock!
        TI = TI + TI * (TI < 0)
        I = Int(TI / 60)

        Call TICKERUPDATE

        J = TI - I * 60

        Locate 3, 44: Print Using " TIME: ##.##     QUARTER: # "; I + J / 100, quarter;
        Locate 3, 53: Print ":";
        Locate , 72: Print Tab(80); " ";

        Locate 4, 44: Print Using " DOWN: #"; currDown;
        Print Tab(60);

        If ydsToFirst >= 0.7 Then
            Print Using " TO GO: ##"; Int(ydsToFirst + 0.5);
        Else
            Print " TO GO: INCHES";
        End If

        Print Tab(80); " ";

        Locate 5, 44: Print " BALL ON ";
        If ydLine <= 50 Then
            Print "OWN";
        Else
            Print "OPP";
        End If

        'Print Using " ##.# YD LINE "; ydLineTeam;
        Print Using " ## YD LINE "; Int(ydLineTeam + 0.5);
        Print Tab(80); " ";

    End If

End Sub


'----------------------------------------
'           DEFMENU Subroutine
'----------------------------------------
'This subroutine gives the user a menu for
'selecting their defensive choices.
Sub DEFMENU ()


    If playerMode = 1 And D = compTeam Or playerMode = 2 Then
        'Computer on Defense or PC vs PC

        Call DEFENSE
        Call DEFGOALLINE

    Else

        Do

            goodSelection = 1

            'Determine Line Formation
            Call FRAMEOPTION(10, D)
            Print "SELECT DEFENSIVE LINE PLAY "; gameTeams$(D)

            For dLine = 1 To 5:
                Locate 10 + dLine, 14
                Print dLine - 1; "- "; defPlay$(dLine); Tab(41); dLine + 4; "- "; defPlay$(dLine + 5)
            Next dLine

            Print
            Locate , 27: Print "G - GOAL LINE (5 YL AND IN)"

            If timeouts(D) > 0 Then
                Locate , 27: Print "T - CALL TIMEOUT"
            End If

            Do
                I$ = GetKeyPress$

                If UCase$(I$) = "Q" Then
                    Cls
                    End
                End If

                If UCase$(I$) = "G" And ydLine >= 94.999 Then
                    dLine = 11
                    Screen , , 0, 0
                    Exit Sub
                End If

                If UCase$(I$) = "T" Then
                    Call TimeoutCalled(P1, periodOver)
                    Call CheckGameClock
                    If gameClock! = 0 Then Exit Sub
                    'If periodOver = 1 Then GoTo 3360
                End If

                dLine = Val(I$)

            Loop Until dLine >= 0 And dLine <= 9 And UCase$(I$) <> "G" And UCase$(I$) <> "T"

            'Adjustments ???
            dLine = dLine + 1

            Screen , , 0, 0

            Select Case dLine
                Case 5, 6
                    dLine = dLine + 4
                Case 7 To 10
                    dLine = dLine - 2
            End Select

            'Determine Pass Coverage
            Call FRAMEOPTION(10, D)
            Print "SELECT PASS COVERAGE "; gameTeams$(D)

            For coverage = 1 To 5
                Locate 10 + coverage, 14
                Print coverage - 1; "- "; passCov$(coverage); Tab(41); coverage + 4; "- "; passCov$(coverage + 5)
            Next coverage

            Locate , 41: Print " B - BLITZ ENGAGE"

            Do
                J$ = GetKeyPress$

                'Main loop condition
                If J$ <> Chr$(27) Then
                    If UCase$(I$) = "B" Then coverage = 10: Exit Do
                    coverage = Val(J$)
                End If

            Loop Until coverage >= 0 And coverage <= 9 Or I$ = Chr$(27)

            If J$ <> Chr$(27) Then

                ' 3065
                coverage = coverage + 1
                Screen , , 0, 0

                'More loop conditions
                '(5-2 Pass Rush Or Inside Stunt) AND (Blitz ILB or Blitz Engage)
                If (dLine = 5 Or dLine = 6) And (coverage = 7 Or coverage = 9 Or coverage = 11) Then
                    goodSelection = 0
                End If

                '(5-2 Inside Stunt Or 4-3 Outside Stunt) AND (Blitz OLB or Blitz Engage)
                If (dLine = 7 Or dLine = 8) And (coverage = 8 Or coverage = 10 Or coverage = 11) Then
                    goodSelection = 0
                End If

            End If

        Loop Until goodSelection = 1

    End If

End Sub


'----------------------------------------
'         DEGFOALLINE Subroutine
'----------------------------------------
'This subroutine automatically picks a
'goal line defense when 5 yards are left.
Sub DEFGOALLINE ()

    If ydLine > 94.999 Then
        dLine = 11
        coverage = 1
    End If

End Sub


'----------------------------------------
'            LGPBP Subroutine
'----------------------------------------
'This subroutine chooses the play-by-play
'text for the start of a play where any
'long gain has been calculated.
Sub LGPBP ()

    RN0 = Int(Rnd(1) * 20) + 1

    Select Case RN0
        Case 1: pbpString$ = "HE BREAKS FREE"
        Case 2: pbpString$ = "HE DODGES A TACKLER"
        Case 3: pbpString$ = "HE STIFF ARMS A TACKLER"
        Case 4: pbpString$ = "HE HEADS DOWN THE SIDELINE"
        Case 5: pbpString$ = "HE SEES DAYLIGHT"
        Case 6: pbpString$ = "HE CUTS BACK"
        Case 7: pbpString$ = "HE CUTS UPFIELD"
        Case 8: pbpString$ = "HE CHARGES THROUGH A GAPING HOLE"
        Case 9: pbpString$ = "HE MAY GO ALL THE WAY"
        Case 10: pbpString$ = "HE BREAKS IT"
        Case 11: pbpString$ = "HE TURNS IT ON"
        Case 12: pbpString$ = "HE SHIFTS INTO HIGH GEAR"
        Case 13: pbpString$ = "HE BREAKS IT BIG!!"
        Case 14: pbpString$ = "HE BURSTS THROUGH"
        Case 15: pbpString$ = "HE RUNS WILD"
        Case 16: pbpString$ = "HE POPS A BIG ONE"
        Case 17: pbpString$ = "HE BUSTS ONE"
        Case 18: pbpString$ = "HE POPS THROUGH THERE"
        Case 19: pbpString$ = "HE POPS OUT THERE"
        Case 20: pbpString$ = "HE SEES A BREAK"
    End Select

    Call PBP

End Sub


'----------------------------------------
'          RUNTDPBP Subroutine
'----------------------------------------
'This subroutine generates the Play-by-Play
'for a rushing TD.
Sub RUNTDPBP ()

    RN0 = Int(Rnd(1) * 29) + 1

    Select Case RN0
        Case 1: pbpString$ = "HE BREAKS FREE"
        Case 2: pbpString$ = "HE DODGES A TACKLER"
        Case 3: pbpString$ = "HE STIFF ARMS A TACKLER"
        Case 4: pbpString$ = "HE HEADS DOWN THE SIDELINE"
        Case 5: pbpString$ = "HE SEES DAYLIGHT"
        Case 6: pbpString$ = "HE CUTS BACK"
        Case 7: pbpString$ = "HE CUTS UPFIELD"
        Case 8: pbpString$ = "HE CHARGES THROUGH A GAPING HOLE"
        Case 9: pbpString$ = "HE MAY GO ALL THE WAY"
        Case 10: pbpString$ = "HE BREAKS IT"
        Case 11: pbpString$ = "HE TURNS IT ON"
        Case 12: pbpString$ = "HE SHIFTS INTO HIGH GEAR"
        Case 13: pbpString$ = "HE BREAKS IT BIG!!"
        Case 14: pbpString$ = "HE BURSTS THROUGH"
        Case 15: pbpString$ = "HE RUNS WILD"
        Case 16: pbpString$ = "HE POPS A BIG ONE"
        Case 17: pbpString$ = "HE BUSTS ONE"
        Case 18: pbpString$ = "HE POPS THROUGH THERE"
        Case 19: pbpString$ = "HE POPS OUT THERE"
        Case 20: pbpString$ = "HE SEES A BREAK"
        Case 21: pbpString$ = "HE'S GONE!"
        Case 22: pbpString$ = "NO ONE WILL CATCH HIM"
        Case 23: pbpString$ = "SEE YOU LATER!!"
        Case 24: pbpString$ = "HE GETS THE CORNER"
        Case 25: pbpString$ = "HE COULD GO"
        Case 26: pbpString$ = "HE'LL GO THE DISTANCE"
        Case 27: pbpString$ = "THERE HE GOES!!"
        Case 28: pbpString$ = "HE'LL GO ALL THE WAY"
        Case 29: pbpString$ = "HE'S IN THE SECONDARY"
    End Select

    Call PBP

End Sub


'----------------------------------------
'         SETWEATHER Subroutine
'----------------------------------------
'This subroutine presents the user a menu
'that helps set the wind and weather options
Sub SETWEATHER ()

    If teamRat_GAME(gameLoc, 3) <> 1 Then

        Call SETSCREEN

        Call OPTIONBOX(9)

        Print "WEATHER OPTIONS "
        Locate 12, 14
        Locate , 14: Print "0 - GOOD"
        Locate , 14: Print "1 - BAD (RAIN/SNOW)"
        Locate , 14: Print "2 - TERRIBLE (BLIZZARD/HEAVY RAIN)"
        Locate , 14: Print "3 - COMPUTER SELECT"

        If autoPlay = 1 Then
            'read weather from schedule options
            I2 = NG%(20)
        Else

            Do
                I$ = GetKeyPress$
                choice = Val(I$)
            Loop Until choice >= 1 And choice <= 3 Or I$ = "0"

        End If

        Screen , , 0, 0

        'If choice = 0 Then 200

        If choice = 1 Then
            climate(4) = -3
            climate(5) = 1
            climate(6) = -10
        End If

        If choice = 2 Then
            climate(4) = -6
            climate(5) = 2
            climate(6) = -20
        End If

        If choice = 3 Then

            RN1 = Int(Rnd(1) * 22) + 1

            If RN1 = 18 Then
                climate(4) = -3
                climate(5) = 1
                climate(6) = -10
            End If

            If RN1 = 7 Then
                climate(4) = -6
                climate(5) = 2
                climate(6) = -20
            End If

        End If

        Call OPTIONBOX(9)

        Print "WIND OPTIONS "
        Locate 12, 14
        Locate , 14: Print "0 - NONE"
        Locate , 14: Print "1 - LIGHT TO MODERATE"
        Locate , 14: Print "2 - STRONG AND GUSTY"
        Locate , 14: Print "3 - COMPUTER SELECT"

        If autoPlay = 1 Then
            choice = NG%(19)
        Else
            Do
                I$ = GetKeyPress$
                choice = Val(I$)
            Loop Until choice >= 0 And choice <= 3
        End If

        Screen , , 0, 0

        'If choice = 0 Then 299
        If choice = 1 Then
            climate(1) = -7
            climate(2) = -5
            climate(3) = -10
        End If

        If choice = 2 Then
            climate(1) = -15
            climate(2) = -15
            climate(3) = -30
        End If

        If choice = 3 Then

            RN1 = Int(Rnd(1) * 22) + 1

            If RN1 = 14 Then
                climate(1) = -7
                climate(2) = -5
                climate(3) = -10
            End If

            If RN1 = 3 Then
                climate(1) = -15
                climate(2) = -15
                climate(3) = -30
            End If

        End If

    End If

End Sub


'----------------------------------------
'            FGATT Subroutine
'----------------------------------------
'This subroutine tracks and increments FG
'attempts based on yardage increments for
'the current kicker.
Sub FGATT ()

    fgAttYds = Int(117 + goalPostAdj - ydLine + .5)

    Select Case fgAttYds

        Case 1 To 19
            FA(P1, activeKicker(P1), 0) = FA(P1, activeKicker(P1), 0) + 1

        Case 20 To 29
            FA(P1, activeKicker(P1), 1) = FA(P1, activeKicker(P1), 1) + 1

        Case 30 To 39
            FA(P1, activeKicker(P1), 2) = FA(P1, activeKicker(P1), 2) + 1

        Case 40 To 49
            FA(P1, activeKicker(P1), 3) = FA(P1, activeKicker(P1), 3) + 1

        Case Is >= 50
            FA(P1, activeKicker(P1), 4) = FA(P1, activeKicker(P1), 4) + 1

    End Select

End Sub


'----------------------------------------
'            FGMADE Subroutine
'----------------------------------------
'This subroutine tracks and increments FG
'made based on yardage increments for
'the current kicker.
Sub FGMADE ()

    Select Case fgAttYds

        Case 1 To 19
            FM(P1, activeKicker(P1), 0) = FM(P1, activeKicker(P1), 0) + 1

        Case 20 To 29
            FM(P1, activeKicker(P1), 1) = FM(P1, activeKicker(P1), 1) + 1

        Case 30 To 39
            FM(P1, activeKicker(P1), 2) = FM(P1, activeKicker(P1), 2) + 1

        Case 40 To 49
            FM(P1, activeKicker(P1), 3) = FM(P1, activeKicker(P1), 3) + 1

        Case 50 To gameLongFG(P1, activeKicker(P1))
            FM(P1, activeKicker(P1), 4) = FM(P1, activeKicker(P1), 4) + 1

        Case Is > gameLongFG(P1, activeKicker(P1))
            gameLongFG(P1, activeKicker(P1)) = fgAttYds

    End Select

End Sub


'----------------------------------------
'           ComputerOffense Subroutine
'----------------------------------------
'This subroutine gives the user a menu for
'selecting their offensive choices.
Sub ComputerOffense ()

    If endZone = 0 Then

        '4th Quarter
        'Any down, Less than 35 seconds to go, 0 Timeouts Opponent, Offense leading
        If quarter = 4 And gameClock! <= 35 * (4 - currDown) And timeouts(D) = 0 And score(P1, 0) > score(D, 0) Then

            playCall = 9
            playType = 1
            qbTakeKnee = 1

        Else

            'If not inside 2 minute warning and past Own 40 Yd Line
            If Not (quarter = 2 And gameClock! <= 120 And ydLine > 40 Or quarter >= 4 And gameClock! <= 120) Then

                '4th Quarter+,
                '4th Down, Opponent's side of field, Score = Down by 11 or more
                If currDown = 4 And quarter >= 4 And ydLine >= 50 And score(D, 0) - score(P1, 0) > 10 Then
                    GoTo 8833
                End If

                '3rd Quarter+,
                '4th Down, Inside opponent's 45, Score = Down by 15 or more
                If currDown = 4 And quarter >= 3 And ydLine >= 55 And score(D, 0) - score(P1, 0) > 14 Then
                    GoTo 8833
                End If

                '4th Down, 1 Yd To Go, 3 Pt. Lead, Opponent's 32 to 38, Score Not Tied
                If currDown = 4 And ydsToFirst <= 1 And score(P1, 0) - score(D, 0) <= 3 And score(P1, 0) - score(D, 0) > 0 And ydLine >= 62 + goalPostAdj And ydLine <= 68 + goalPostAdj Then
                    GoTo 8827
                End If

                '4th Quarter+
                '---------------
                '4th Down, Opponent's 32 to 38, Score = 3 Pt. Margin, Pro Rules or College Rules + Team after 1960 = Field Goal
                If quarter >= 4 And currDown = 4 And score(P1, 0) - score(D, 0) <= 3 And score(P1, 0) - score(D, 0) >= -3 And ydLine >= 62 + goalPostAdj And ydLine < 68 + goalPostAdj And (ruleOptType <> 0 Or ruleOptType = 0 And teamYears(1) > 1960 And teamYears(0) > 1960) Then
                    playCall = 20
                    GoTo 8800
                End If

                '4th Down, Opponent's 32 to 38, Score <= 3 Pt. Lead, Pro Rules or College Rules + Team after 1960 = Field Goal
                If quarter >= 4 And currDown = 4 And ydLine >= 62 + goalPostAdj And ydLine < 68 + goalPostAdj And score(D, 0) - score(P1, 0) <= 3 And (ruleOptType <> 0 Or ruleOptType = 0 And teamYears(1) > 1960 And teamYears(0) > 1960) Then
                    playCall = 20
                    GoTo 8800
                End If

                '4th Down, <= 2 Yds to Go, Opponent's 25 - 35, Score - Down by 11 or more
                If quarter >= 4 And ydLine >= 65 + goalPostAdj And ydLine < 75 + goalPostAdj And ydsToFirst <= 2 And score(D, 0) - score(P1, 0) > 10 Then
                    GoTo 8827
                End If

                '4th Down, <= 2 Yds to Go, Opponent's 25 - 35, Score - Down by 10 or less, 4 Minutes Remaining, Pro Rules or College Rules + Team after 1960 = Field Goal
                If quarter >= 4 And ydLine >= 65 + goalPostAdj And ydLine < 75 + goalPostAdj And score(D, 0) - score(P1, 0) <= 10 And score(D, 0) - score(P1, 0) > 0 And currDown = 4 And gameClock! < 240 And (ruleOptType <> 0 Or ruleOptType = 0 And teamYears(1) > 1960 And teamYears(0) > 1960) Then
                    playCall = 20
                    GoTo 8800
                End If


                '4th Quarter Only
                '---------------
                '4th Down, More Than 2 Minutes, Score - Down by 4 to 10, Inside Red Zone = Field Goal
                If currDown = 4 And quarter = 4 And gameClock! > 240 And score(D, 0) - score(P1, 0) >= 4 And score(D, 0) - score(P1, 0) <= 10 And ydLine > 80 + goalPostAdj Then
                    playCall = 20
                    GoTo 8800
                End If

                '4th Down, Down by 4 or More, Inside Red Zone
                If currDown = 4 And quarter = 4 And score(D, 0) - score(P1, 0) >= 4 And ydLine >= 80 + goalPostAdj Then
                    GoTo 8833
                End If


                'Any Quarter
                '---------------
                '4th Down, Any Time, <= 2 Yds to Go, Inside Opponent's 40
                If currDown = 4 And ydLine >= 60 And ydsToFirst <= 2 Then
                    GoTo 8827
                End If

                '4th Down, Any Time, >= 3 Yds To Go, Inside Opponent's 32, Pro Rules or College Rules + Team after 1960 = Field Goal
                If currDown = 4 And ydLine >= 68 + goalPostAdj And ydsToFirst > 2 And (ruleOptType <> 0 Or ruleOptType = 0 And teamYears(1) > 1960 And teamYears(0) > 1960) Then
                    playCall = 20
                    GoTo 8800
                End If

                '4th Down, Any Time, Outside Opponent's 32 = Punt
                If currDown = 4 And ydLine < 68 + goalPostAdj Then
                    playCall = 19
                    GoTo 8800
                End If

                Call d100(RN0)

                '-- Several of these could be triggered
                '-- during a 2-point conversion
                'Any Time, Inside Team's Own 8 Yd Line
                If ydLine <= 8 Then
                    GoTo 8827
                End If

                '2nd Down, Any Time, <=12 Yds To Go
                If leagRat_GAME(P1, 7) > 75 And currDown = 2 And ydsToFirst <= 12 Then
                    GoTo 8827
                End If

                '3rd Down, Any Time, <= 5 Yds to Go
                If leagRat_GAME(P1, 7) > 75 And currDown = 3 And ydsToFirst <= 5 Then
                    GoTo 8827
                End If

                '1st or 2nd Down, Any Time
                If currDown = 1 And leagRat_GAME(P1, 7) >= 70 Or currDown <= 2 And leagRat_GAME(P1, 7) >= 80 Then
                    GoTo 8827
                End If

                '1st Down, Any Time, >= 11 Yds To Go
                If currDown = 1 And ydsToFirst > 10 Then
                    GoTo 8833
                End If

                '2nd Down, Any Time, >= 11 Yds To Go
                If currDown = 2 And ydsToFirst > 10 Then
                    GoTo 8833
                End If

                '2nd Down, Any Time, <= 4 Yds to Go
                If currDown = 2 And ydsToFirst < 5 Then
                    GoTo 8827
                End If

                '3nd Down, Any Time, >= 4 Yds to Go
                If currDown = 3 And ydsToFirst >= 4 Then
                    GoTo 8833
                End If

                '3rd Down, Any Time, 1 Yds to Go
                '---> this could be triggered during a 2 Pt Conversion
                If currDown = 3 And ydsToFirst < 2 Then
                    GoTo 8827
                End If

                '4th Down, Any Time, >= 4 Yds to Go
                If currDown = 4 And ydsToFirst >= 4 Then
                    GoTo 8833
                End If

                '4th Down, Any Time, 1 Yds To Go
                If currDown = 4 And ydsToFirst < 2 Then
                    GoTo 8827
                End If

                '4th Quarter
                '---------------
                'Less than 8 Minutes in Game
                'Either team is ahead by 9 or more pts
                If quarter = 4 And gameClock! <= 480 And score(D, 0) - score(P1, 0) >= 9 And R0 >= leagRat_GAME(P1, 7) - leagRat_GAME(P1, 7) / 2 Then
                    GoTo 8833
                End If
                If quarter = 4 And gameClock! <= 480 And score(P1, 0) - score(D, 0) >= 9 And R0 >= leagRat_GAME(P1, 7) + leagRat_GAME(P1, 7) / 2 Then
                    GoTo 8833
                End If

                If RN0 > leagRat_GAME(P1, 7) Then
                    GoTo 8833
                End If


                '----- DECISION BRANCH 1 -----
                8827 '
                If gameStatsTeam(P1, 1) + gameStatsTeam(P1, 3) <> 0 Then
                    If gameStatsTeam(P1, 1) / (gameStatsTeam(P1, 1) + gameStatsTeam(P1, 3)) > leagRat_GAME(P1, 7) / 100 And leagRat_GAME(P1, 7) < 50 Then
                        GoTo 8833
                    End If
                End If

                Call d100(RN0)

                Select Case qbMobility(P1)

                    Case 1 To 3
                        '17% - 31%
                        sneakChance = (qbMobility(P1) * 7) + 10

                    Case 4 To 6
                        '42% - 58%
                        sneakChance = (qbMobility(P1) * 8) + 10

                    Case 7 To 9
                        '73% - 91%
                        sneakChance = (qbMobility(P1) * 9) + 10

                End Select

                If ydsToFirst <= 1.3 Then

                    'QB Sneak
                    'Print #11, "Evaluating sneak"
                    If RN0 <= sneakChance Then playCall = 8

                Else

                    Call d100(RN1)

                    Select Case RN1

                        'Long Pass
                        Case 1 To 2:
                            If leagRat_GAME(P1, 7) < 75 Then
                                playCall = 15
                            End If

                            'Med Pass
                        Case 3 To 5:
                            If leagRat_GAME(P1, 7) < 75 Then
                                playCall = 14
                            End If

                            'Option
                        Case 6 To 10:
                            playCall = 5

                            'Sweep
                        Case 11 To 20:
                            playCall = 4

                            'Trap
                        Case 21 To 30:
                            playCall = 7

                            'Off Tackle
                        Case 31 To 50:
                            playCall = 3

                            'Dive
                        Case 51 To 70:
                            playCall = 2

                            'Blast
                        Case 71 To 100:
                            playCall = 1

                    End Select

                End If

                GoTo 8800



                '----- DECISION BRANCH 2 -----
                8833 '
                Call d100(RN0)

                Select Case RN0

                    ' Draw
                    Case 1 To 5:
                        playCall = 6

                        ' Rollout
                    Case 6 To 30:
                        playCall = 17

                        ' 3-Step Drop
                    Case 31 To 36:
                        playCall = 9

                        ' Screen
                    Case 37 To 44:
                        playCall = 13

                        ' Long Pass
                    Case 45 To 63:
                        playCall = 12

                        ' Medium Pass
                    Case 64 To 89:
                        playCall = 11

                        ' Short Pass
                    Case 90 To 100:
                        playCall = 10

                End Select

                '3rd / 4th Down, Any Time, >= 5 Yds To Go
                '----------------------------------------
                '-- If Draw was selected, change to Long Pass
                If currDown >= 3 And ydsToFirst >= 5 And playCall = 6 Then
                    playCall = 12
                End If

                '-- If Short Pass was selected, change to Medium Pass
                If currDown >= 3 And ydsToFirst >= 5 And playCall = 10 Then
                    playCall = 11
                End If

                '-- If 3-Step Drop or screen was selected, change to Rollout
                If currDown >= 3 And ydsToFirst >= 5 And (playCall = 13 Or playCall = 9) Then
                    playCall = 17
                End If

                'Any Down, Any Time, Inside Opponent's 10 or < 7 Yds To Go
                '----------------------------------------
                '-- If Long Pass was selected, change to Medium Pass
                If ydLine > 90 And (playCall = 12 Or playCall = 15) Or ydsToFirst < 7 And (playCall = 12 Or playCall = 15) Then
                    playCall = 11
                End If

                GoTo 8800

            Else

                '2nd / 4th Quarter
                '2 Min Warning
                '--------------------
                'Score - Down by >= 4
                If score(D, 0) - score(P1, 0) > 3 Then
                    If quarter >= 4 Then 8880
                    If quarter = 2 Then 8885
                End If

                'Score Tied or Down
                If score(D, 0) - score(P1, 0) >= 0 Then 8885

                Call d100(RN0)

                '2nd Quarter, Past Own 40 Yd Line
                If ydLine > 40 And quarter = 2 Then 8885

                playCall = 4

                If RN0 <= 60 Then playCall = 3

                '2nd Quarter / Overtime / 4th Quarter and Tied Score
                'Any down, Inside Opponent's 35, Less than 22 seconds to go = Field Goal
                If ydLine >= 65 + goalPostAdj And gameClock! <= 22 And quarter = 2 Or ydLine >= 65 + goalPostAdj And gameClock! <= 22 And quarter > 4 Or ydLine >= 65 + goalPostAdj And gameClock! <= 22 And quarter = 4 And score(P1, 0) - score(D, 0) = 0 Then
                    playCall = 20
                End If

                '4th Down, Inside Opponent's 35, >= 1 Yard to go, Pro Rules or College Rules + Team after 1960 = Field Goal
                If ydLine >= 65 + goalPostAdj And currDown = 4 And ydsToFirst >= 2 And (ruleOptType <> 0 Or ruleOptType = 0 And teamYears(1) > 1960 And teamYears(0) > 1960) Then
                    playCall = 20
                End If

                '4th Down, Outside Opponents' 35 = Punt
                If currDown = 4 And ydLine <= 65 + goalPostAdj Then
                    playCall = 19
                End If

            End If

            '----- DECISION MADE -----
            8880 '
            '2nd Quarter
            '4th Down, Any Time
            If quarter = 2 And currDown = 4 Then

                If ydLine < 65 + goalPostAdj Then
                    'Outside Opponent's 35 = Punt
                    playCall = 19
                Else
                    'Inside Opponent's 35, Pro Rules, or College Rules + Team after 1960 = Field Goal
                    '--- COMPARE A
                    If (ruleOptType <> 0 Or ruleOptType = 0 And teamYears(1) > 1960 And teamYears(0) > 1960) Then
                        playCall = 20
                    End If
                End If

                GoTo 8800

            End If

            'Any Quarter
            '---------------
            'Any Down, Less than 22 seconds to go, 0 Timeouts Offense = Pass
            If gameClock! <= 22 And timeouts(P1) = 0 Then
                playCall = 12
                GoTo 8800
            End If

            'Any Down, Less than 12 sceonds to go, 1 or more timeouts Offense = Pass
            If gameClock! <= 12 And timeouts(P1) <> 0 Then
                playCall = 12
                GoTo 8800
            End If

            GoTo 8892

            '-----

            8885 '
            '2nd / 4th Quarter
            '4th Down, Any Time, Outside Opponent's 35, Score Tied = Punt
            If quarter = 2 And currDown = 4 And ydLine < 65 + goalPostAdj Or quarter >= 4 And ydLine < 65 + goalPostAdj And score(P1, 0) = score(D, 0) And currDown = 4 Then
                playCall = 19
                GoTo 8800
            End If


            '2nd Quarter Only
            '4th Down, Any Time, Inside Opponent's 35, Pro Rules or College Rules + Team after 1960 = FIeld Goal
            '--- COMPARE B
            If quarter = 2 And currDown = 4 And ydLine >= 65 + goalPostAdj And (ruleOptType <> 0 Or ruleOptType = 0 And teamYears(1) > 1960 And teamYears(0) > 1960) Then
                playCall = 20
                GoTo 8800
            End If


            'Any Quarter
            '---------------
            'Any Down, Less than 37 seconds to go, 0 Timeouts Offense, Outside Opponent's 35 = Pass (???)
            If gameClock! <= 37 And timeouts(P1) = 0 And ydLine < 65 + goalPostAdj Then
                playCall = 12
                GoTo 8800
            End If

            'Any Down, Less than 22 seconds to go, 0 Timeouts Offense, Outside opponent's 35 = Pass
            If gameClock! <= 12 And timeouts(P1) <> 0 And ydLine < 65 + goalPostAdj Then
                playCall = 12
                GoTo 8800
            End If

            'Any Down, Less than 22 seconds to go, 0 Timeouts Offense, Inside Opponent's 35 = Field Goal
            If gameClock! <= 22 And timeouts(P1) = 0 And ydLine >= 65 + goalPostAdj Then
                playCall = 20
                GoTo 8800
            End If

            'Any Down, Less than 12 seconds to go, 0 Timeouts Offense, Inside Opponent's 35 = Field Goal
            '--- This is essentially the same as above ???
            If gameClock! <= 12 And timeouts(P1) <> 0 And ydLine >= 65 + goalPostAdj Then
                playCall = 20
                GoTo 8800
            End If

            '4th Down, Any Time, Inside Opponent's 35, Pro Rules or College Rules + Team after 1960 = Field Goal
            If currDown = 4 And ydLine >= 65 + goalPostAdj And (ruleOptType <> 0 Or ruleOptType = 0 And teamYears(1) > 1960 And teamYears(0) > 1960) Then
                playCall = 20
                GoTo 8800
            End If

            8892 '
            Call d100(R0)

            'Sideline pass
            playCall = 16

            '50% random chance of Short/Med/Long Pass
            If R0 < 50 Then
                playCall = Int(Rnd(1) * 3) + 10
            End If

            'Inside Opponent's 10 = Medium Pass
            If ydLine >= 90 Then
                playCall = 11
            End If

            'In the Red Zone, Score - Down by <3 ---or--- Inside Opponent's 10 and Down by 3
            'Random choice of Blast / Dive / Off Tackle / Sweep
            If ydLine > 80 And score(D, 0) - score(P1, 0) < 3 Or ydLine > 90 And score(D, 0) - score(P1, 0) = 3 Then
                playCall = Int(Rnd(1) * 4) + 1
            End If

        End If


        'Final Checks
        '-----

        8800 '
        'Overtime and 4th Down
        'Change to Long Pass
        If quarter > 4 And NG%(14) = 3 And currDown = 4 Then

            'Punt chosen but score tied
            If playCall = 19 And score(D, 0) = score(P1, 0) Then
                playCall = 12
            End If

            'Punt chosen, Score - Ahead by 3 or less
            '(ie, if we punt and give the team the ball,
            'they could win by a FG; let's go for it
            If playCall = 19 And score(D, 0) - score(P1, 0) <= 3 Then
                playCall = 12
            End If

            'Punt or FG chosen, Down by 4 or more
            '(ie, we have to try and get a 1st down)
            If (playCall = 19 Or playCall = 20) And score(D, 0) - score(P1, 0) > 3 Then
                playCall = 12
            End If

        End If


    Else

        'We should only be here if going for 2
        Call d100(R0)

        Select Case R0

            'Rollout Pass
            Case 1 To 25:
                playCall = 17

                ' Medium Pass
            Case 26 To 50:
                playCall = 11

                ' Short Pass
            Case 51 To 70:
                playCall = 10

                ' Option
            Case 71 To 80:
                playCall = 5

                ' Sweep
            Case 81 To 90:
                playCall = 4

                ' Off Tackle
            Case 91 To 100:
                playCall = 3

        End Select

    End If

End Sub


'----------------------------------------
'         MEASUREMENT Subroutine
'----------------------------------------
'This subroutine handles the play-by-play
'for when a measurement is done to check
'for first down.
Sub MEASUREMENT ()

    pbpString$ = "HE'S CLOSE TO A FIRST DOWN"
    Call PBP

    pbpString$ = "THE OFFICIAL SIGNALS FOR A MEASUREMENT"
    Call PBP

    Call PBP

    test! = ydsGained - ydsToFirst
    Select Case ydsGained - ydsToFirst

        Case Is < -.2
            pbpString$ = "HE'S ABOUT A FOOT SHORT"

        Case -0.20 To -0.11
            pbpString$ = "HE'S ABOUT 6 INCHES SHORT"

        Case -0.10 To -0.01
            pbpString$ = "HE'S SHORT BY INCHES"

        Case Else
            pbpString$ = "THE OFFICIAL SIGNALS FIRST DOWN!"

    End Select

    Call PBP
    Call PBP

End Sub


'----------------------------------------
'        WHICHRECEIVER Subroutine
'----------------------------------------
'This subroutine determines which receiver
'to include in the Play-by-Play text
Sub WHICHRECEIVER

    If I3 > 9 Then
        pbpString$ = pbpString$ + wdRec$(P1, I3 - 10)
    Else
        pbpString$ = pbpString$ + rbacks$(P1, I3)
    End If

End Sub


'----------------------------------------
'          CLIPPING Subroutine
'----------------------------------------
'This subroutine handles the yardage and
'Play-by-Play for a clipping penalty.
Sub CLIPPING ()

    pbpString$ = "*** PENALTY - 10 YD ILLEGAL BLOCK ON THE RETURN ***"
    Call PBP

    If ydLine < 20 Then
        ydLine = ydLine / 2
        gameStatsTeam(P1, 12) = gameStatsTeam(P1, 12) + ydLine / 2
    End If

    gameStatsTeam(P1, 11) = gameStatsTeam(P1, 11) + 1

    If ydLine >= 20 Then
        ydLine = ydLine - 10
        gameStatsTeam(P1, 12) = gameStatsTeam(P1, 12) + 10
    End If

End Sub


'----------------------------------------
'          CHOOSEQB Subroutine
'----------------------------------------
'This subroutine gives the user a menu for
'choosing a quarterback and sets up the
'corresponding arrays for the game.
Sub CHOOSEQB (currTeam)

    Color 15, 0
    Cls

    Call TEAMBOX(12, currTeam)

    useRandomQB(currTeam) = 0

    Locate , 31
    Print "SELECT QUARTERBACK"
    Print
    Locate , 18: Print " # QB NAME     % OF  COMP  INT"
    Locate , 18: Print "               ATTS  PCT   PCT  ARM"

    For currQB = 0 To 3

        If qbacks$(currTeam, currQB) <> "XXX" And qbacks$(currTeam, currQB) <> "END" And qbacks$(currTeam, currQB) <> "" Then

            Locate , 18: Print currQB; "- "; qbacks$(currTeam, currQB);
            Locate , 33

            If currQB = 0 Then

                Print qbNumAtt(currTeam, currQB);

                Locate , 39: Print qbCompPct(currTeam, currQB);
                Locate , 45: Print qbPctInt(currTeam, currQB);
                Locate , 50: Print qbArmRat(currTeam, currQB)

            Else

                'Only print extra QB's if they
                'don't have 100% contribution
                If qbNumAtt(currTeam, currQB - 1) < 100 Then

                    Print Int(qbNumAtt(currTeam, currQB)) - qbNumAtt(currTeam, currQB - 1);

                    Locate , 39: Print qbCompPct(currTeam, currQB);
                    Locate , 45: Print qbPctInt(currTeam, currQB);
                    Locate , 50: Print qbArmRat(currTeam, currQB)

                End If

                Locate , 1

            End If

        End If

    Next currQB

    Locate , 19: Print "4 - COMPUTER SELECT PASSER EACH PLAY"
    Print
    Locate , 19: Print "SELECT (0-4) ";
    Color 15, 0

    Do
        I$ = GetKeyPress$
        choice = Val(I$)
    Loop Until choice >= 0 And choice <= 4

    If choice <= 3 Then
        qbNumber(currTeam) = choice
    End If

    If choice = 4 Then
        useRandomQB(currTeam) = 1
        'qbNumber(currTeam) = choice
        qbContrib = GetQBRushPctGame%(qbacks$(currTeam, qbNumber(currTeam)))
        qbMobility(currTeam) = GetQBMobility%(qbContrib)
    End If

    If choice <= 3 And ruleOptType <> 0 And useRandomQB(currTeam) = 0 Then

        qbContrib = GetQBRushPctGame%(qbacks$(currTeam, qbNumber(currTeam)))
        suggestedMobility = GetQBMobility%(qbContrib)

        Call TEAMBOX(12, currTeam)

        Locate 8, 16: Print "ENTER "; qbacks$(currTeam, choice); " MOBILITY RATING (1-9)"
        Locate , 16: Print "(1-LOW, 9-HIGH)"
        Print
        Locate , 16: Print "THIS IS HOW OFTEN HE RUNS OUT OF THE POCKET"
        Locate , 16: Print "SOME QB'S LIKE MARINO & BRADY ALMOST NEVER DO"
        Locate , 16: Print "OTHERS LIKE MAHOMES & LAMAR JACKSON WILL TAKE OFF"
        Locate , 16: Print "FOR AN AVERAGE QB, SIMPLY ENTER 5"
        Print
        Locate , 16: Print "THIS QB CONTRIBUTED"; qbContrib; "% OF TEAM RUNS"
        Locate , 16: Print "A SUGGESTED VALUE FOR THEM IS"; suggestedMobility

        Color 15, 0

        Do
            I$ = GetKeyPress$
            qbMobility(currTeam) = Val(I$)
        Loop Until qbMobility(currTeam) >= 1 And qbMobility(currTeam) <= 9

    End If

    value = qbMobility(currTeam)

    'Update rusher index of each benched QB
    For I = 0 To 3:
        qbRushIdx(currTeam, I) = -1
    Next I

    'If we selected a QB
    If useRandomQB(currTeam) = 0 Then

        For currBack = 0 To 9:

            For currQB = 0 To 3
                'Store the rushing index of other QB's
                If rbacks$(currTeam, currBack) = qbacks$(currTeam, currQB) And I5 <> qbNumber(currTeam) Then
                    qbRushIdx(currTeam, currQB) = currBack
                End If
            Next currQB

        Next currBack

    End If

End Sub


'----------------------------------------
'        TimeoutCalled Subroutine
'----------------------------------------
Sub TimeoutCalled (teamIdx, periodOver)

    pbpString$ = "TIME OUT " + gameTeams$(teamIdx) + " - THEY HAVE" + Str$(timeouts(teamIdx) - 1) + " REMAINING"
    Call PBP
    timeouts(teamIdx) = timeouts(teamIdx) - 1
    playSegment = 9

    Call AddPossessionTime(7, 10)
    'Call CheckGameClock(periodOver)

End Sub


'----------------------------------------
'         Kickoff_Pro Subroutine
'----------------------------------------
'This subroutine gives the user choices
'to handle a pro kick off.
Sub Kickoff_Pro ()

    Call SCOREBOARD

    If playerMode = 1 And P1 = compTeam Or playerMode = 2 Then

        I9 = 2

    Else
        Call OPTIONBOX(9)
        Print gameTeams$(winTeam); " DO YOU ELECT TO:"
        Locate 12, 14
        Locate , 14: Print "1 - KICK OFF"
        Locate , 14: Print "2 - RECEIVE"
        Locate , 14: Print "3 - DEFEND GOAL WITH WIND AT BACK"
        Locate , 14: Print "4 - DEFEND GOAL WITH WIND IN FACE"

        Do
            I$ = GetKeyPress$
            I9 = Val(I$)
        Loop Until I9 >= 1 And I9 <= 4

    End If

    Screen , , 0, 0

    If I9 = 1 Or I9 = 2 Then

        Call SCOREBOARD

        If playerMode = 1 And D = compTeam Or playerMode = 2 Then
            I1 = 1
        Else
            Call OPTIONBOX(7)

            Print gameTeams$(1 - winTeam); " DO YOU ELECT TO:"
            Locate 12, 14
            Locate , 14: Print "1 - DEFEND GOAL WITH WIND AT BACK"
            Locate , 14: Print "2 - DEFEND GOAL WITH WIND IN FACE"

            Do
                I$ = GetKeyPress$
                I1 = Val(I$)
            Loop Until I1 >= 1 And I1 <= 2

        End If

        Screen , , 0, 0
        climate(0) = D

        If I1 = 2 Then climate(0) = 1 - D

        If I9 = 1 Then
            Call CHANGEPOSS
            winTeam = 1 - winTeam
        End If

    End If

    If I9 = 3 Or I9 = 4 Then

        If I9 = 3 Then climate(0) = P1
        If I9 = 4 Then climate(0) = D

        Swap P1, D

        Call SCOREBOARD

        If playerMode = 1 And P1 = compTeam Or playerMode = 2 Then
            I1 = 2
        Else
            Call OPTIONBOX(7)
            Print gameTeams$(1 - winTeam); " DO YOU ELECT TO:"
            Locate 12, 14
            Locate , 14: Print "1 - KICK OFF"
            Locate , 14: Print "2 - RECEIVE"

            Do
                I$ = GetKeyPress$
                I1 = Val(I$)
            Loop Until I1 >= 1 And I1 <= 2

        End If

        Screen , , 0, 0
        If I1 = 1 Then Call CHANGEPOSS

    End If

End Sub


'----------------------------------------
'         Kickoff_College Subroutine
'----------------------------------------
'This subroutine gives the user choices
'to handle a college kick off.
Sub Kickoff_College ()

    If quarter > 2 Then
        Call Kickoff_Pro
    Else

        Call SCOREBOARD

        If playerMode = 1 And P1 = compTeam Or playerMode = 2 Then
            I9 = 2
        Else
            Call OPTIONBOX(9)
            Print gameTeams$(P1); " DO YOU ELECT TO:"
            Locate 12, 14
            Locate , 14: Print "1 - KICK OFF"
            Locate , 14: Print "2 - RECEIVE"
            Locate , 14: Print "3 - DEFEND GOAL WITH WIND AT BACK"
            Locate , 14: Print "4 - DEFEND GOAL WITH WIND IN FACE"
            Locate , 14: Print "5 - DEFER CHOICE TO 2ND HALF"

            Do
                I$ = GetKeyPress$
                I9 = Val(I$)
            Loop Until I9 >= 1 And I9 <= 5

        End If

        Screen , , 0, 0

        Select Case I9

            Case 1, 2

                Call SCOREBOARD

                If playerMode = 1 And D = compTeam Or playerMode = 2 Then
                    I1 = 1
                Else
                    Call OPTIONBOX(7)
                    Print gameTeams$(D); " DO YOU ELECT TO:"
                    Locate 12, 14
                    Locate , 14: Print "1 - DEFEND GOAL WITH WIND AT BACK"
                    Locate , 14: Print "2 - DEFEND GOAL WITH WIND IN FACE"

                    Do
                        I$ = GetKeyPress$
                        I1 = Val(I$)
                    Loop Until I1 >= 1 And I1 <= 2
                End If

                Screen , , 0, 0
                climate(0) = D

                If I1 = 2 Then
                    climate(0) = 1 - D
                End If

                If I9 = 1 Then
                    Call CHANGEPOSS
                    winTeam = 1 - winTeam
                End If

            Case 3, 4

                If I9 = 3 Then
                    climate(0) = P1
                End If

                If I9 = 4 Then
                    climate(0) = D
                End If

                Swap P1, D

                Call SCOREBOARD

                If playerMode = 1 And P1 = compTeam Or playerMode = 2 Then
                    I1 = 2
                Else
                    Call OPTIONBOX(7)
                    Print gameTeams$(P1); " DO YOU ELECT TO:"

                    Locate 12, 14
                    Locate , 14: Print "1 - KICK OFF"
                    Locate , 14: Print "2 - RECEIVE"

                    Do
                        I$ = GetKeyPress$
                        I1 = Val(I$)
                    Loop Until I1 >= 1 And I1 <= 2
                End If

                Screen , , 0, 0

                If I1 = 1 Then
                    Call CHANGEPOSS
                End If

            Case 5
                Swap P1, D
                winTeam = P1
                Call Kickoff_Pro

        End Select

    End If

End Sub


'----------------------------------------
'         TAKESAFETY Subroutine
'----------------------------------------
'This subroutine handles ball position,
'variables and Play-by-play for when a
'player takes a safety voluntarily.
Sub TAKESAFETY ()

    Call AddPossessionTime(4, 7)

    ydsGained = (ydLine * -1) - 1
    gameStatsTeam(P1, 1) = gameStatsTeam(P1, 1) + 1
    gameStatsTeam(P1, 2) = gameStatsTeam(P1, 2) + ydsGained

    pbpString$ = punter$(P1, 0) + " IS HEADED OUT OF THE END ZONE"
    Call PBP

    score(D, quarter) = score(D, quarter) + 2
    score(D, 0) = score(D, 0) + 2

End Sub


'----------------------------------------
'           FINDINT Subroutine
'----------------------------------------
'This subroutine appears to determine
'something related to interceptions.
'Perhaps the player to associate with
'an interception attempt?
Sub FINDINT ()

    M5 = 0
    RN0 = Int(Rnd(1) * teamInts(1 - P1)) + 1

    For Z1 = 0 To 9

        M5 = M5 + playerInts(1 - P1, Z1)

        If RN0 <= M5 Then
            defTeam$ = defInts$(1 - P1, Z1)
            Exit For
        End If

    Next

End Sub


'----------------------------------------
'           YARDAGE Subroutine
'----------------------------------------
'This subroutine I believe is used to
'round yardage gained
Sub YARDAGE ()

    ydsGained = ydsGained + (Int(Rnd(1) * 11) - 5) / 10

End Sub


'----------------------------------------
'          PUNTYARDS Subroutine
'----------------------------------------
'This subroutine handles the calculations
'for punt yardage and who receives the
'punt.
Sub PUNTYARDS ()

    'Call d100(I3)
    ydLine = ydLine + ydsGained

    Call CHANGEPOSS

    ydsToScore = ydLine
    ydLine = 100 - ydLine

    Call YARDLINE

    RN0 = Int(Rnd(1) * 1000) + 1

    Select Case prYdsPerRet(P1, puntRetNbr)

        Case 1 To 4

            Select Case RN0

                Case 1 To 50
                    '-5 to 0
                    ydsGained = Int((Rnd(1) * 6) - 5)

                Case 51 To 899
                    '-6 to 16
                    ydsGained = Int((Rnd(1) * 14)) + (prYdsPerRet(P1, puntRetNbr) - 3) * 3

                Case 900 To 988
                    If I2 <> 1 Then
                        '6 to 53
                        ydsGained = Int((Rnd(1) * 39) + 12) + (prYdsPerRet(P1, puntRetNbr) - 3) * 3
                    End If

                Case 989 To 1000
                    '1.1% chance of TD
                    If I2 <> 1 Then
                        ydsGained = 110
                    End If

            End Select

        Case 5

            Select Case RN0

                Case 1 To 900
                    '0 - 24
                    ydsGained = Int((Rnd(1) * 25) + 0)

                Case 901 To 940
                    '12 - 50
                    ydsGained = Int((Rnd(1) * 39) + 12)

                Case 941 To 1000
                    '6% chance of TD
                    If I2 <> 1 Then
                        ydsGained = 110
                    End If

            End Select

        Case Is >= 6

            If RN0 > 900 And I2 <> 1 Then
                '10% chance of TD
                ydsGained = 110
            Else
                '0 to 34
                ydsGained = Int((Rnd(1) * 26)) + (prYdsPerRet(P1, puntRetNbr) - 6) * 3
            End If

    End Select

    X1 = 4
    Call YARDAGE
    Call MARKER

    pbpString$ = "THE BALL IS TAKEN BY " + pret$(P1, puntRetNbr) + " AT THE" + Str$(Int(ydLineTeam))
    Call PBP

    startYds = 100 - ydLine
    endYds = startYds - ydsGained

    Call MOVEBALL

End Sub


'----------------------------------------
'          FAKEYARDS Subroutine
'----------------------------------------
'This subroutine handles yardage gains and
'Play-by-Play text for a fake play.
Sub FAKEYARDS (choice)

    pbpString$ = "IT'S A FAKE!!! THE UPBACK IS RUNNING THE BALL"
    Call PBP

    Call d100(RN0)

    Select Case choice

        Case 0

            If RN0 <= 36 Then
                ydsGained = Int(Rnd(1) * 27)
            Else
                ydsGained = Int(Rnd(1) * 6) - 2
            End If

        Case 1

            Select Case RN0

                Case 1 To 36
                    ydsGained = Int(Rnd(1) * 21)

                Case 37 To 90
                    ydsGained = Int(Rnd(1) * 11) - 4

                Case 91 To 100
                    ydsGained = 100 - ydLine

            End Select

        Case 2

            If RN0 <= 8 Then
                ydsGained = Int(Rnd(1) * 21)
            Else
                ydsGained = Int(Rnd(1) * 7) - 7
            End If

    End Select

    Call YARDAGE

    playSegment = 1

    startYds = 100 - ydLine
    endYds = startYds - ydsGained

    Call MOVEBALL

    pbpString$ = "THE UPBACK GAINED"
    Print
    X = 10
    X1 = 11

    If Int((ydsGained + ydLine) * 10 + .5) / 10 >= 99.94999 Then
        pbpString$ = pbpString$ + Str$(Int(100 - ydLine))
    Else
        pbpString$ = pbpString$ + Str$(Int((ydsGained) * 10 + .5) / 10)
    End If

    pbpString$ = pbpString$ + " YARDS ON THE FAKE RUN"
    Call PBP

End Sub


'----------------------------------------
'           FUMBLE Subroutine
'----------------------------------------
'This subroutine I believe tries to calc-
'ulate if a player has fumbled the ball.
Sub FUMBLE ()

    Call d100(F8)

    If revFumble = 1 Then

        F8 = F8 - 10
        revFumble = 0

    Else

        If F% = 1 Then F8 = F8 - 6
        If F% = 2 Then F8 = F8 + 1
        If F% = 4 Then F8 = F8 - 2
        If F% = 5 Then F8 = F8 + 2

        If playCall = 1 Then F8 = F8 + 2
        If playCall = 2 Then F8 = F8 - 1
        If playCall = 5 Then F8 = F8 - 1

    End If

    F8 = F8 - climate(5)
    F% = 0

    If F8 <= leagRat_GAME(P1, 3) + leagRat_GAME(D, 4) Then ballFumbled = 1

End Sub


'----------------------------------------
'            FLEA Subroutine
'----------------------------------------
'This subroutine handles yardage and the
'Play-by-Play text for a flea flicker.
Sub FLEA (ffChance)

    I1 = qbNumber(P1)

    catchPctSuccess = qbCompPct(P1, qbNumber(P1)) + Int(leagRat_GAME(D, 1)) - 4

    intChance = qbPctInt(P1, qbNumber(P1)) + leagRat_GAME(D, 2) + 5

    ffPctSuccess = 5
    If coverage = 7 Or coverage = 8 Then ffPctSuccess = 30
    If coverage = 9 Or coverage = 10 Then ffPctSuccess = 50
    If coverage = 11 Then ffPctSuccess = 70

    Call d100(RN0)

    If RN0 <= 25 Then
        ydsGained = Int(Rnd(1) * 50) + 51
    Else
        ydsGained = Int(Rnd(1) * 15) + 35
    End If

    CP = Int(Rnd(1) * 16) + 35

    If hasRunFF(P1) > 0 Then

        catchPctSuccess = 3

        'increase chance of INT
        intChance = intChance + 10

        If ffPctSuccess = 5 Then ffPctSuccess = 30

    End If

    hasRunFF(P1) = 1

    pbpString$ = rbacks$(P1, 0) + " TAKES THE HANDOFF"
    Call PBP

    pbpString$ = "HE TOSSES IT BACK TO " + qbacks$(P1, qbNumber(P1)) + " FOR A FLEA FLICKER!!!"
    Call PBP

    I3 = Int(Rnd(1) * 3)

    pbpString$ = wdRec$(P1, I3) + " IS OPEN DOWNFIELD"
    Call PBP

    Call d100(ffChance)

End Sub


'----------------------------------------
'           REVGAIN Subroutine
'----------------------------------------
'This subroutine handles yardage and the
'Play-by-Play text for a reverse play.
Sub REVGAIN ()

    pbpString$ = qbacks$(P1, qbNumber(P1)) + " TAKES THE SNAP"

    pbpString$ = pbpString$ + " AND HANDS OFF TO " + rbacks$(P1, I1) + " ON THE REVERSE!!"
    Call PBP

    Call d100(RN0)

    If RV%(P9) = 0 Then

        Select Case RN0

            Case 1 To 20
                ydsGained = (Int(Rnd(1) * 8) + 5) * -1
                revFumble = 1

            Case 21 To 80
                ydsGained = Int(Rnd(1) * 11)

            Case 81 To 100
                ydsGained = Int(Rnd(1) * 81)

        End Select

    Else

        Select Case RN0

            Case 1 To 5:
                ydsGained = Int(Rnd(1) * 81)

            Case 6 To 55:
                ydsGained = Int(Rnd(1) * 11)

            Case 56 To 100:
                ydsGained = (Int(Rnd(1) * 8) + 5) * -1
                revFumble = 1

        End Select

    End If

    RV%(P9) = 1

End Sub


'----------------------------------------
'           FAKERUN Subroutine
'----------------------------------------
'This subroutine handles yardage and the
'Play-by-Play text for a fake run on a
'kicking lineup.
Sub FAKERUN (choice)

    pbpString$ = "IT'S A FAKE!!! THE HOLDER IS RUNNING THE BALL"
    Call PBP

    Call d100(RN0)

    Select Case choice
        Case 1
            Select Case RN0
                Case 1 To 20: ydsGained = Int(Rnd(1) * 21)
                Case 21 To 95: ydsGained = Int(Rnd(1) * 11) - 4
                Case 96 To 100: ydsGained = 100 - ydLine
            End Select

        Case 2
            If RN0 <= 8 Then
                ydsGained = Int(Rnd(1) * 21)
            Else
                ydsGained = Int(Rnd(1) * 7) - 7
            End If

    End Select

    Call YARDAGE

    playSegment = 1

    startYds = 100 - ydLine
    endYds = startYds - ydsGained

    Call MOVEBALL

    pbpString$ = "THE HOLDER GAINED"
    Print

    X = 10
    X1 = 11

    If Int((ydsGained + ydLine) * 10 + .5) / 10 >= 99.94999 Then
        pbpString$ = pbpString$ + Str$(Int(100 - ydLine))
    Else
        pbpString$ = pbpString$ + Str$(Int((ydsGained) * 10 + .5) / 10)
    End If

    pbpString$ = pbpString$ + " YARDS ON THE FAKE RUN"
    Call PBP

End Sub


'----------------------------------------
'           HANDOFF Subroutine
'----------------------------------------
'This subroutine handles the Play-by-Play
'text for a run play.
Sub HANDOFF ()

    If dLine = 11 Then
        pbpString$ = defFormation$(dLine)
    Else
        pbpString$ = defFormation$(dLine) + " " + passCov$(coverage)
    End If

    Call PBP

    pbpString$ = qbacks$(P1, qbNumber(P1)) + " TAKES THE SNAP"

    If playCall = 4 Or playCall = 5 Then

        fieldSide = Int(Rnd(1) * 2)

        If rbacks$(P1, I1) = qbacks$(P1, qbNumber(P1)) Then

            pbpString$ = pbpString$ + " AND KEEPS IT ON THE " + playDesc$(chosenPlay)
            Call PBP

        Else
            RN0 = Int(Rnd(1) * 2) + 1

            If RN0 = 1 Then
                pbpString$ = pbpString$ + " AND TOSSES OUT TO " + rbacks$(P1, I1)
            Else
                'RN0 = 2
                pbpString$ = pbpString$ + " AND PITCHES OUT TO " + rbacks$(P1, I1)
            End If

            Call PBP

            pbpString$ = rbacks$(P1, I1) + " GOES AROUND THE " + direction$(fieldSide) + " END"
            Call PBP

        End If

    Else

        If rbacks$(P1, I1) = qbacks$(P1, qbNumber(P1)) Then
            pbpString$ = pbpString$ + " AND KEEPS IT ON THE " + playDesc$(chosenPlay)
        Else
            pbpString$ = pbpString$ + " AND HANDS OFF TO " + rbacks$(P1, I1) + " ON THE " + playDesc$(chosenPlay)
        End If

        Call PBP

    End If

End Sub


'----------------------------------------
'          PASSBALL Subroutine
'----------------------------------------
'This subroutine helps setup the stage for
'a pass play, including Play-by-Play text.
'This includes determining who the ball is
'going to and where they might be.
Sub PASSBALL (passChance)

    S6 = 0

    Call QBLINE

    playType = 2

    'Pick QB based on contribution
    Call d100(qbChance)

    For J = 3 To 0 Step -1
        If qbChance <= qbNumAtt(P1, J) Then I1 = J
    Next J

    If useRandomQB(P1) = 1 Then
        'Update selected QB from random choice
        If I1 > 0 Then
            qbNumber(P1) = I1
        End If
        qbNumber(P1) = I1
        qbContrib = GetQBRushPctGame%(qbacks$(P1, qbNumber(P1)))
        qbMobility(P1) = GetQBMobility%(qbContrib)
    Else
        'Assign seleted QB
        I1 = qbNumber(P1)
    End If

    Do

        Call d100(recChance)
        newRandomNum% = 0

        For J = 15 To 10 Step -1
            If recChance <= wrNumRec(P1, J - 10) Then I3 = J
        Next J

        For J = 9 To 0 Step -1
            If recChance <= rbNumRec(P1, J) Then I3 = J
        Next J

        If I3 > 9 Then
            If wdRec$(P1, I3 - 10) = qbacks$(P1, qbNumber(P1)) Then newRandomNum% = 1
        End If

        If I3 < 10 Then
            If rbacks$(P1, I3) = qbacks$(P1, qbNumber(P1)) Then newRandomNum% = 1
        End If

    Loop Until newRandomNum% = 0

    If dLine = 11 Then
        pbpString$ = defFormation$(dLine)
    Else
        pbpString$ = defFormation$(dLine) + " " + passCov$(coverage)
    End If

    Call PBP
    Call PASSPBP

    If I3 > 9 Then targetWRName$ = wdRec$(P1, I3 - 10) Else targetWRName$ = rbacks$(P1, I3)

    RN0 = Int(Rnd(1) * 6) + 1

    Select Case RN0
        Case 1: pbpString$ = "HE'S GOT "
        Case 2: pbpString$ = "HE SPOTS "
        Case 3: pbpString$ = "HE'S LOOKING FOR "
        Case 4: pbpString$ = "HE SEES "
        Case 5: pbpString$ = "HE'S SEARCHING FOR "
        Case 6: pbpString$ = "HE'S GOT "
    End Select

    Call WHICHRECEIVER

    passDir = Int(Rnd(1) * 5) + 1
    fieldSide = Int(Rnd(1) * 2)

    Select Case passDir

        Case 1, 6
            pbpString$ = pbpString$ + " IN THE OPEN"

        Case 2
            pbpString$ = pbpString$ + " ON THE SIDELINE"

        Case 3
            If playCall <> 13 Then
                pbpString$ = pbpString$ + " OVER THE MIDDLE"
            End If

        Case 4
            If playCall <> 13 Then
                pbpString$ = pbpString$ + " DOWNFIELD"
            End If

        Case 5
            If playCall = 12 Or playCall = 15 Then
                pbpString$ = pbpString$ + " DEEP"
            End If

    End Select

    Call PBP

    Call d100(passChance)

End Sub


'----------------------------------------
'      AdjustCompPct Subroutine
'----------------------------------------
'This subroutine performs adjustments to
'the QB's completion %. It shifts it based
'on the defense's overall rating), ball
'position, play type and wind (windCmpAdj)

'Passes are completed if the random # generated
' is below the sum of all these ratings

Sub AdjustCompPct ()

    'Comp %, adjusted for DEF team's rating
    expCompPct = qbCompPct(P1, I1) + Int(leagRat_GAME(D, 1))

    If P1 = gameLoc Then
        'Add 3% for home game
        expCompPct = expCompPct + 3
    Else
        'P1 <> gameLoc
        If gameLoc <> 2 Then expCompPct = expCompPct - 3
    End If

    'Adjust ?? based on position
    Select Case ydLine

        'Goal Line to Own 0
        Case 0 To 40.001:
            ballPosCmpAdj = 3

            'Own 40 to Opp 20
        Case 40.002 To 80.001:
            ballPosCmpAdj = -2

            'Opp 20 to Opp 15
        Case 80.002 To 85.001:
            ballPosCmpAdj = -4

            'Opp 15 to Opp 10
        Case 85.002 To 90.001:
            ballPosCmpAdj = -10

            'Opp 10 to Opp 5
        Case 90.002 To 95.001:
            ballPosCmpAdj = -26

            'Opp 5 to  Goal
        Case Is > 95.001:
            ballPosCmpAdj = -13

    End Select

    'Adjust for pass type
    passTypeCmpAdj = passCovAdj(1, playCall - 8, coverage)

    'Adjust for wind direction NOT in team's favor
    windCmpAdj = 0

    If climate(0) <> P1 Then

        If (playCall = 11 Or playCall = 14 Or playCall = 16 Or playCall = 17 Or playCall = 18) Then
            windCmpAdj = climate(2)
        End If

        If (playCall = 12 Or playCall = 15) Then
            windCmpAdj = climate(3) + climate(6)
        End If

    End If

End Sub


'----------------------------------------
'          SCOREUPD Subroutine
'----------------------------------------
'This subroutine tracks variables related
'to team score and also updates the current
'score in the game.
Sub SCOREUPD ()

    'current "score" of the game
    nbrScores = nbrScores + 1

    scoreTimes(nbrScores) = gameClock!
    scoreQuarters(nbrScores) = quarter

    'If quarter = 4 Then
    '
    'End If

    If X1 <> 7 Then
        K1(nbrScores, 1) = P1
    Else
        K1(nbrScores, 1) = D
    End If

    K1(nbrScores, 0) = X1
    K1(nbrScores, 5) = 100 - ydLine

    If X1 = 3 Then K1(nbrScores, 5) = 100 - kickYL

    If X1 = 8 Then K1(nbrScores, 5) = 117 + goalPostAdj - ydLine

    K1(nbrScores, 2) = I1
    If X1 = 5 Or X1 = 12 Then
        K1(nbrScores, 2) = Z1
    End If

    K1(nbrScores, 3) = I3
    If K1(nbrScores, 5) < 1 Then
        K1(nbrScores, 5) = 1
    End If

End Sub


'----------------------------------------
'          COMPILER Subroutine
'----------------------------------------
' ...explanation...
Sub COMPILER ()

    If X >= 1 And X <= 4 Then

        Select Case X

            Case 1, 4
                'Increase team rush attempts
                gameStatsTeam(P1, 1) = gameStatsTeam(P1, 1) + 1

                'Increase team rush yards
                If ydsGained + ydLine < 99.95 Then
                    gameStatsTeam(P1, 2) = gameStatsTeam(P1, 2) + ydsGained
                Else
                    gameStatsTeam(P1, 2) = gameStatsTeam(P1, 2) + 100 - ydLine
                End If

            Case 2
                'Increase team pass attempts
                gameStatsTeam(P1, 3) = gameStatsTeam(P1, 3) + 1

            Case 3
                'Increase team pass attempts + completions
                gameStatsTeam(P1, 3) = gameStatsTeam(P1, 3) + 1
                gameStatsTeam(P1, 4) = gameStatsTeam(P1, 4) + 1

                'Increase team passing yards
                '-- Do we never substract passing yards for a sack?
                '-- Or does that happen elsewuere?
                If ydsGained + ydLine < 99.95 Then
                    gameStatsTeam(P1, 6) = gameStatsTeam(P1, 6) + ydsGained
                Else
                    gameStatsTeam(P1, 6) = gameStatsTeam(P1, 6) + 100 - ydLine
                End If

        End Select


        Select Case X

            Case 1

                'Increase player rush attempt
                gameStatsPlayer(P1, I1, 0) = gameStatsPlayer(P1, I1, 0) + 1

                'Inrease player rush yards and scores
                If ydsGained + ydLine >= 99.95 Then
                    gameStatsPlayer(P1, I1, 1) = gameStatsPlayer(P1, I1, 1) + 100 - ydLine
                    gameStatsPlayer(P1, I1, 2) = gameStatsPlayer(P1, I1, 2) + 1
                    BG = 100 - ydLine
                    If BG > runBacks(P1, I1) Then
                        gameRB$(P1, I1) = "t"
                        runBacks(P1, I1) = BG
                    End If
                Else
                    gameStatsPlayer(P1, I1, 1) = gameStatsPlayer(P1, I1, 1) + ydsGained
                    If ydsGained > runBacks(P1, I1) Then
                        gameRB$(P1, I1) = ""
                        runBacks(P1, I1) = ydsGained
                    End If
                End If

            Case 2

                If HB% = 0 Then
                    gameStatsPlayer(P1, I1, 9) = gameStatsPlayer(P1, I1, 9) + 1
                End If

            Case 3

                If HB% <> 1 Then

                    gameStatsPlayer(P1, I1, 9) = gameStatsPlayer(P1, I1, 9) + 1
                    gameStatsPlayer(P1, I1, 10) = gameStatsPlayer(P1, I1, 10) + 1

                    If ydsGained + ydLine >= 99.95 Then
                        gameStatsPlayer(P1, I1, 12) = gameStatsPlayer(P1, I1, 12) + 100 - ydLine
                        gameStatsPlayer(P1, I1, 13) = gameStatsPlayer(P1, I1, 13) + 1
                    Else
                        gameStatsPlayer(P1, I1, 12) = gameStatsPlayer(P1, I1, 12) + ydsGained
                    End If

                End If

                If I3 > 9 Then

                    If ydsGained + ydLine >= 99.95 Then
                        gameStatsPlayer(P1, I3 - 10, 7) = gameStatsPlayer(P1, I3 - 10, 7) + 100 - ydLine
                        gameStatsPlayer(P1, I3 - 10, 8) = gameStatsPlayer(P1, I3 - 10, 8) + 1
                    Else
                        gameStatsPlayer(P1, I3 - 10, 7) = gameStatsPlayer(P1, I3 - 10, 7) + ydsGained
                    End If

                    gameStatsPlayer(P1, I3 - 10, 6) = gameStatsPlayer(P1, I3 - 10, 6) + 1

                Else

                    If ydsGained + ydLine >= 99.95 Then
                        gameStatsPlayer(P1, I3, 4) = gameStatsPlayer(P1, I3, 4) + 100 - ydLine
                        gameStatsPlayer(P1, I3, 5) = gameStatsPlayer(P1, I3, 5) + 1
                    Else
                        gameStatsPlayer(P1, I3, 4) = gameStatsPlayer(P1, I3, 4) + ydsGained
                    End If

                    gameStatsPlayer(P1, I3, 3) = gameStatsPlayer(P1, I3, 3) + 1

                End If

            Case 4

                If W5 = 0 Or W5 = 1 And ruleOptType = 0 Then
                    gameStatsPlayer(P1, qbNumber(P1) + 10, 0) = gameStatsPlayer(P1, qbNumber(P1) + 10, 0) + 1
                End If

                If W5 = 0 And ydsGained + ydLine < 99.95 Or W5 = 1 And ruleOptType = 0 And ydsGained + ydLine < 99.95 Then
                    gameStatsPlayer(P1, qbNumber(P1) + 10, 1) = gameStatsPlayer(P1, qbNumber(P1) + 10, 1) + ydsGained
                End If

                If W5 = 0 And ydsGained + ydLine >= 99.95 Or W5 = 1 And ruleOptType = 0 And ydsGained + ydLine >= 99.95 Then
                    gameStatsPlayer(P1, qbNumber(P1) + 10, 1) = gameStatsPlayer(P1, qbNumber(P1) + 10, 1) + 100 - ydLine
                    gameStatsPlayer(P1, qbNumber(P1) + 10, 2) = gameStatsPlayer(P1, qbNumber(P1) + 10, 2) + 1
                End If

                If W5 = 1 Then
                    sackStatsTeam(P1, 0) = sackStatsTeam(P1, 0) + 1
                    sackStatsTeam(P1, 1) = sackStatsTeam(P1, 1) + ydsGained * -1
                    totalSacks(1 - P1, Z1, 0) = totalSacks(1 - P1, Z1, 0) + 1
                    totalSacks(1 - P1, Z1, 1) = totalSacks(1 - P1, Z1, 1) + ydsGained * -1
                End If


        End Select

    End If

    If HB% = 1 Then HB% = 0
    X = 0

End Sub


'----------------------------------------
'           TWOMIN Subroutine
'----------------------------------------
'This subroutine gives the user a menu of
'options for the last 2 minutes of the
'game and handles the appropriate changes
'to team variables
Sub TwoMinMenu (menuChoice)

    If pbpDelay > 0 Then

        Call OPTIONBOX(7)

        Locate 9, 14: Print "1 - HUDDLE"
        Locate , 14: Print "2 - TIME OUT "; gameTeams$(P1); " "; timeouts(P1)
        Locate , 14: Print "3 - TIME OUT "; gameTeams$(D); " "; timeouts(D):
        Locate , 14: Print "4 - HURRY UP OFFENSE"
        Locate , 14: Print "5 - THROW AWAY PASS"

    End If

    Do

        If playerMode > 0 Then

            scoreDiff = score(P1, 0) - score(D, 0)

            'Is it possible for more than one of these to be TRUE at the same time?
            'If not, consider setting a flag value to then be checked outside the if
            'That would only give us one Exit Do
            If (playerMode = 1 And D = compTeam Or playerMode = 2) Then

                If scoreDiff > 0 And scoreDiff <= 14 And timeouts(D) > 0 And quarter = 4 Then
                    menuChoice = 3
                Else

                    If timeouts(P1) > 0 Then
                        menuChoice = 2
                    Else
                        menuChoice = 4
                    End If

                End If
            End If

            If playerMode = 2 Then
                menuChoice = 1
                Exit Do
            End If

            If quarter = 2 And ydLine > 15 And (playerMode = 1 And P1 = compTeam Or playerMode = 2) And scoreDiff <= 0 Then
                If timeouts(P1) > 0 Then
                    menuChoice = 2
                Else
                    menuChoice = 4
                End If
                Exit Do
            End If

            If quarter = 2 And ydLine > 40 And scoreDiff > 0 And leagRat_GAME(P1, 7) < 65 And (playerMode = 1 And P1 = compTeam Or playerMode = 2) And timeouts(P1) > 0 Then
                menuChoice = 2
                Exit Do
            End If

            If quarter = 2 And ydLine > 40 And scoreDiff >= 0 And leagRat_GAME(P1, 7) < 65 And (playerMode = 2 Or playerMode = 1 And P1 = compTeam) Then
                menuChoice = 4
                Exit Do
            End If

        End If

        Do
            I$ = GetKeyPress$
            menuChoice = Val(I$)
        Loop Until menuChoice >= 1 And menuChoice <= 5

    Loop Until Not ((menuChoice = 2 Or menuChoice = 4 Or menuChoice = 5) And playerMode = 1 And P1 = compTeam) Or (menuChoice = 3 And playerMode = 1 And D = compTeam)
    '(menuChoice = 1 Or menuChoice = 3 And Not (playerMode = 1 And P1 = compTeam)) Or (menuChoice <> 3 And Not (playerMode = 1 And D=compTeam))

    Color 0, 5 * (1 - BW)

End Sub


'----------------------------------------
'           FGDIST Subroutine
'----------------------------------------
'This subroutine calculcates the distance
'for a FG
Sub FGDIST ()

    I9 = 0

    If climate(0) = P1 Then
        If climate(1) = -7 Then I9 = -7
        If climate(1) = -15 Then I9 = -15
    Else
        If climate(1) = -7 Then I9 = 7
        If climate(1) = -15 Then I9 = 15
    End If

    For I = 4 To 0 Step -1:

        If ydLine >= adjF1(I) + goalPostAdj + I9 Then
            fgSuccessChance = adjF0(I) - ((9 - kickerFGA(P1, activeKicker(P1))) * 5)
        End If

        If ydLine >= 68 + goalPostAdj And ydLine >= adjF1(I) + goalPostAdj + I9 Then
            fgSuccessChance = fgSuccessChance + (ydLine - adjF1(I)) + goalPostAdj - 4 + I9
        End If

    Next I

    'Force there to always be a 5% chance of failure?
    If fgSuccessChance <= 0 Then
        fgSuccessChance = 5
    End If

    If fgSuccessChance >= 100 Then
        fgSuccessChance = 95
    End If

End Sub


'----------------------------------------
'           RUNMENU Subroutine
'----------------------------------------
'This subroutine prints the different
'options for a run play
Sub RUNMENU ()

    Call FRAMEOPTION(12, P1)

    Print "SELECT RUNNING PLAY "; gameTeams$(P1)
    Print
    Locate , 14: Print "1 - BLAST                 9 - POWER SWEEP "
    Locate , 14: Print "2 - DIVE                 10 - QUICK PITCH "
    Locate , 14: Print "3 - OFF TACKLE           11 - QUICK TRAP  "
    Locate , 14: Print "4 - TOSS SWEEP           12 - ISOLATION   "
    Locate , 14: Print "5 - OPTION RUN           13 - SPRINT DRAW "
    Locate , 14: Print "6 - DRAW PLAY            14 - OVER THE TOP"
    Locate , 14: Print "7 - TRAP/MISDIRECTION    15 - COUNTER     "
    Locate , 14: Print "8 - QB SNEAK                              "

End Sub


'----------------------------------------
'          PASSMENU Subroutine
'----------------------------------------
'This subroutine prints the different
'options for a pass play
Sub PASSMENU ()

    Call FRAMEOPTION(11, P1)

    Print "SELECT PASS PLAY "; gameTeams$(P1)
    Print
    Locate , 14: Print "1 - 3 STEP DROP           8 - SIDELINE PASS"
    Locate , 14: Print "2 - SHORT PASS            9 - ROLLOUT PASS "
    Locate , 14: Print "3 - MEDIUM PASS          10 - BOOTLEG PASS "
    Locate , 14: Print "4 - LONG PASS            11 - CROSS        "
    Locate , 14: Print "5 - SCREEN PASS          12 - CURL PASS    "
    Locate , 14: Print "6 - MED PLAY ACT PASS    13 - DEEP POST    "
    Locate , 14: Print "7 - LONG PLAY ACT PASS   14 - DEEP CORNER  "

End Sub


'----------------------------------------
'        SHORTYARDAGE Subroutine
'----------------------------------------
'This subroutine calculates yardage gains
'for Goal Line situations.
Sub SHORTYARDAGE ()

    Call d100(RN6)

    If dLine > 3 And dLine < 6 Then

        Select Case ydLine

            Case 94.999 To 95.998

                Select Case RN6
                    Case 1 To 17: ydsGained = 6
                    Case 18 To 22: ydsGained = 4
                    Case 23 To 35: ydsGained = 3
                    Case 36 To 52: ydsGained = 2
                    Case 53 To 76: ydsGained = 1
                    Case 77 To 93: ydsGained = 0
                    Case 94 To 100: ydsGained = -1
                End Select

            Case 95.999 To 96.998

                Select Case RN6
                    Case 1 To 26: ydsGained = 5
                    Case 27 To 29: ydsGained = 3
                    Case 30 To 49: ydsGained = 2
                    Case 50 To 78: ydsGained = 1
                    Case 79 To 94: ydsGained = 0
                    Case 95 To 100: ydsGained = -1
                End Select

            Case 96.999 To 97.998

                Select Case RN6
                    Case 1 To 27: ydsGained = 4
                    Case 28 To 32: ydsGained = 2
                    Case 33 To 80: ydsGained = 1
                    Case 81 To 98: ydsGained = 0
                    Case 99 To 100: ydsGained = -1
                End Select

            Case 97.999 To 98.998

                Select Case RN6
                    Case 1 To 46: ydsGained = 3
                    Case 47 To 72: ydsGained = 1
                    Case 73 To 91: ydsGained = 0
                    Case 92 To 100: ydsGained = -1
                End Select

            Case Is >= 98.999

                Select Case RN6
                    Case 1 To 61: ydsGained = 2
                    Case 62 To 96: ydsGained = 0
                    Case 97 To 100: ydsGained = -1
                End Select

        End Select

    Else

        Select Case ydLine

            Case 94.999 To 95.998

                Select Case RN6
                    Case 1 To 14: ydsGained = 6
                    Case 15 To 17: ydsGained = 4
                    Case 18 To 29: ydsGained = 3
                    Case 30 To 43: ydsGained = 2
                    Case 44 To 63: ydsGained = 1
                    Case 64 To 77: ydsGained = 0
                    Case 78 To 84: ydsGained = -1
                    Case 85 To 91: ydsGained = -2
                    Case 92 To 98: ydsGained = -3
                    Case 99 To 100: ydsGained = -4
                End Select

            Case 95.999 To 96.998

                Select Case RN6
                    Case 1 To 29: ydsGained = 5
                    Case 30 To 31: ydsGained = 3
                    Case 32 To 45: ydsGained = 2
                    Case 46 To 72: ydsGained = 1
                    Case 73 To 86: ydsGained = 0
                    Case 87 To 90: ydsGained = -1
                    Case 91: ydsGained = -2
                    Case 92 To 94: ydsGained = -3
                    Case 95 To 97: ydsGained = -4
                    Case 98 To 100: ydsGained = -5
                End Select

            Case 96.999 To 97.998

                Select Case RN6
                    Case 1 To 24: ydsGained = 4
                    Case 25 To 28: ydsGained = 2
                    Case 29 To 69: ydsGained = 1
                    Case 70 To 85: ydsGained = 0
                    Case 86 To 88: ydsGained = -1
                    Case 89 To 93: ydsGained = -2
                    Case 94 To 100: ydsGained = -4
                End Select

            Case 97.999 To 98.998

                Select Case RN6
                    Case 1 To 43: ydsGained = 3
                    Case 44 To 65: ydsGained = 1
                    Case 66 To 83: ydsGained = 0
                    Case 84 To 92: ydsGained = -1
                    Case 93 To 94: ydsGained = -2
                    Case 95 To 100: ydsGained = -4
                End Select

            Case Is >= 98.999

                Select Case RN6
                    Case 1 To 57: ydsGained = 2
                    Case 58 To 74: ydsGained = 0
                    Case 75 To 83: ydsGained = -1
                    Case 84 To 90: ydsGained = -2
                    Case 91 To 97: ydsGained = -3
                    Case 98 To 100: ydsGained = -4
                End Select

        End Select

    End If

    Call d100(RN7)

    If I2 <= 30 Then
        If RN7 <= goalLnYdAdj(I2, 1) Then ydsGained = ydsGained + goalLnYdAdj(I2, 2)
    Else
        'I2 < 30
        If RN7 <= goalLnYdAdj(30, 1) Then ydsGained = ydsGained + goalLnYdAdj(30, 2)
    End If

End Sub


'----------------------------------------
'           PASSPBP Subroutine
'----------------------------------------
'This subroutine develops the Play-by-play
'string for pass play.
Sub PASSPBP ()

    Call d100(RN0)

    If coverage >= 7 Then

        Select Case RN0

            Case 1 To 33:
                pbpString$ = "BLITZ IS COMING...!!"
                Call PBP

            Case 34 To 66:
                pbpString$ = "HERE COMES THE BLITZ...!!"
                Call PBP

            Case 67 To 109:
                pbpString$ = "BLITZ IS ON...!!"
                Call PBP

                If defSacks(1 - P1) = 0 Then
                    defTeam$ = gameTeams$(1 - P1)
                Else

                    M5 = 0
                    RN1 = Int(Rnd(1) * teamSacks(1 - P1)) + 1

                    For Z1 = 0 To 14
                        M5 = M5 + playerSacks(1 - P1, Z1)
                        If RN1 <= M5 Then
                            defTeam$ = defSacks$(1 - P1, Z1)
                            Exit For
                        End If
                    Next

                End If

        End Select

        RN0 = Int(Rnd(1) * 15) + 1
        If (coverage >= 7 Or dLine >= 9) Then

            Select Case RN0

                Case 1: pbpString$ = qbacks$(P1, I1) + " HAS TO HURRY"
                Case 2: pbpString$ = qbacks$(P1, I1) + " IS UNDER A HEAVY PASS RUSH"
                Case 3: pbpString$ = qbacks$(P1, I1) + " IS BEING RUSHED"
                Case 4: pbpString$ = qbacks$(P1, I1) + " IS BEING PRESSURED"
                Case 5: pbpString$ = qbacks$(P1, I1) + " FEELS THE HEAT"
                Case 6: pbpString$ = qbacks$(P1, I1) + " IS BEING CHASED"
                Case 7: pbpString$ = qbacks$(P1, I1) + " IS BEING HURRIED"
                Case 8: pbpString$ = qbacks$(P1, I1) + " IS FORCED OUT OF THE POCKET"
                Case 9: pbpString$ = qbacks$(P1, I1) + " IS SCRAMBLING"
                Case 10: pbpString$ = qbacks$(P1, I1) + " IS UNDER THE GUN"
                Case 11: pbpString$ = qbacks$(P1, I1) + "'S POCKET IS COLLAPSING"
                Case 12: pbpString$ = qbacks$(P1, I1) + "'S PROTECTION IS BREAKING DOWN"
                Case 13: pbpString$ = "HERE COMES " + defTeam$
                Case 14: pbpString$ = defTeam$ + " BREAKS THROUGH"
                Case 15: pbpString$ = defTeam$ + " IS COMING THROUGH"

            End Select

        Else

            Select Case playCall

                Case 9, 10
                    If RN0 <= 50 Then
                        pbpString$ = qbacks$(P1, I1) + " TAKES THE SNAP AND DROPS BACK...LOOKING SHORT..."
                    Else
                        pbpString$ = qbacks$(P1, I1) + " DROPS QUICKLY AND SETS..."
                    End If

                Case 11, 16

                    Select Case RN0

                        Case 1 To 30: pbpString$ = qbacks$(P1, I1) + " DROPS BACK AND LOOKS..."
                        Case 31 To 40: pbpString$ = qbacks$(P1, I1) + " DROPS BACK...HE'S UNDER PRESSURE..."
                        Case 41 To 60: pbpString$ = qbacks$(P1, I1) + " TO THROW...HAS TIME..."
                        Case 61 To 70: pbpString$ = qbacks$(P1, I1) + " BACK TO PASS...HAS PROTECTION..."
                        Case 71 To 80: pbpString$ = qbacks$(P1, I1) + " DROPS BACK...IS BEING CHASED!!"
                        Case 81 To 100: pbpString$ = qbacks$(P1, I1) + " HAS PLENTY OF TIME TO THROW..."

                    End Select

                Case 12
                    If RN0 <= 50 Then
                        pbpString$ = qbacks$(P1, I1) + " DROPS BACK...HE WANTS TO GO DEEP"
                    Else
                        pbpString$ = qbacks$(P1, I1) + " TAKES A DEEP DROP...HE'S LOOKING LONG"
                    End If

                Case 13
                    If RN0 <= 50 Then
                        pbpString$ = qbacks$(P1, I1) + " DROPPING...BEING CHASED..."
                    Else
                        pbpString$ = qbacks$(P1, I1) + " DROPS BACK...HE'S UNDER PRESSURE..."
                    End If

                Case 14, 15
                    If RN0 <= 50 Then
                        pbpString$ = qbacks$(P1, I1) + " MAKES A GOOD PLAY FAKE..."
                    Else
                        pbpString$ = qbacks$(P1, I1) + " FAKES THE GIVE AND WANTS TO THROW IT..."
                    End If

                Case 17
                    pbpString$ = qbacks$(P1, I1) + " ROLLS OUT..."

                Case 18
                    pbpString$ = qbacks$(P1, I1) + " HAS IT ON THE BOOTLEG"

            End Select

        End If

    End If

    Call PBP

End Sub


'----------------------------------------
'            SACK Subroutine
'----------------------------------------
'This subroutine develops the Play-by-play
'text for a sack.
Sub SACK ()

    pbpString$ = "BUT HE WON'T BE ABLE TO GET THIS PASS AWAY!"
    Call PBP

    pbpString$ = qbacks$(P1, I1)

    If HB% = 1 Then pbpString$ = rbacks$(P1, I1)

    If endZone = 1 Then
        pbpString$ = pbpString$ + " IS DROPPED"
    Else

        RN0 = Int(Rnd(1) * 12) + 1
        Call d100(RN1)

        Select Case RN0
            Case 1: pbpString$ = pbpString$ + " IS SACKED"
            Case 2: If RN1 <= 20 Then pbpString$ = pbpString$ + " IS NAILED"
            Case 3: If RN1 <= 30 Then pbpString$ = pbpString$ + " IS BURIED"
            Case 4: If RN1 <= 40 Then pbpString$ = pbpString$ + " IS DRILLED"
            Case 5: If RN1 <= 50 Then pbpString$ = pbpString$ + " IS HIT"
            Case 6: If RN1 <= 60 Then pbpString$ = pbpString$ + " IS THROWN"
            Case 7: If RN1 <= 70 Then pbpString$ = pbpString$ + " IS HAMMERED"
            Case 8: If RN1 <= 80 Then pbpString$ = pbpString$ + " IS TACKLED"
            Case 9: If RN1 <= 90 Then pbpString$ = pbpString$ + " IS SWARMED UNDER"
            Case 10: pbpString$ = pbpString$ + " IS LEVELED"
            Case 11: pbpString$ = pbpString$ + " IS SNOWED UNDER"
            Case 12: pbpString$ = pbpString$ + " IS WRAPPED UP"
        End Select

    End If

    pbpString$ = pbpString$ + " FOR A" + Str$(Int(ydsGained + .5)) + " YARD LOSS"

End Sub


'----------------------------------------
'           QBLINE Subroutine
'----------------------------------------
'This subroutine handles Play-by-Play for
'when the QB approaches the o-line.
Sub QBLINE ()

    qback = 0

    If useRandomQB(P1) = 1 Then
        'Assign the starting QB
        qbNumber(P1) = 0
        qbContrib = GetQBRushPctGame%(qbacks$(P1, qbNumber(P1)))
        qbMobility(P1) = GetQBMobility%(qbContrib)
    Else
        'use the randomly assigned QB
        qback = qbNumber(P1)
    End If

    Call d100(RN1)
    If Not (currDown <= 2 Or currDown = 3 And RN1 <= 80) Then

        RN0 = Int(Rnd(1) * 8) + 1

        Select Case RN0
            Case 1: pbpString$ = gameTeams$(P1) + " COMES UP TO THE LINE"
            Case 2: pbpString$ = gameTeams$(P1) + " BREAKS THE HUDDLE AND LINES UP"
            Case 3: pbpString$ = "THE " + gameMascots$(P1) + " COME UP TO THE LINE"
            Case 4: pbpString$ = "THE " + gameMascots$(P1) + " BREAK THE HUDDLE AND LINE UP"
            Case 5: pbpString$ = qbacks$(P1, qback) + " BARKS OUT THE SIGNALS"
            Case 6: pbpString$ = qbacks$(P1, qback) + " LOOKS OVER THE DEFENSE"
            Case 7: pbpString$ = "HERE IS THE " + downDesc$(currDown) + " DOWN AND" + Str$(Int(ydsToFirst)) + " YARD PLAY FOR " + gameTeams$(P1)
            Case 8: pbpString$ = "HERE IS THE " + downDesc$(currDown) + " DOWN AND" + Str$(Int(ydsToFirst)) + " YARD PLAY FOR THE " + gameMascots$(P1)
        End Select

        Call PBP

    End If

End Sub


'----------------------------------------
'            SQUIB Subroutine
'----------------------------------------
'This subroutine handles the yardage AND
'Play-by-Play text for a squib kick
Sub SQUIB ()

    pbpString$ = kicker$(P1, activeKicker(P1)) + " IS PLACING THE BALL ON THE TEE"
    Call PBP

    pbpString$ = gameTeams$(P1) + " IS LINING UP FOR THE KICKOFF"
    Call PBP

    pbpString$ = "IT'S A SQUIBBER DOWN THE FIELD"
    Call PBP

    Swap P1, D

    X = 0

    kickYL = 100 - (ydLine + Int(Rnd(1) * 16) + 29)
    kickYL = kickYL + (Int(Rnd(1) * 11) - 5) / 10
    ydLine = kickYL
    ydsToScore = 100 - ydLine

    Call MARKER
    Call YARDLINE

    pbpString$ = "KICK TAKEN BY AN UPMAN AT THE" + Str$(Int(kickYL + .5)) + " YD LINE "
    Call PBP

    ydsGained = Int(Rnd(1) * 7) + 9

    startYds = 100 - ydLine
    endYds = startYds - ydsGained

    Call MOVEBALL

    ydLine = kickYL + ydsGained
    F% = 3

End Sub


'----------------------------------------
'           PUNTPBP Subroutine
'----------------------------------------
'This subroutine creates the Play-by-play
'text for a punt.
Sub PUNTPBP ()

    RN0 = Int(Rnd(1) * 12) + 1

    Select Case RN0
        Case 1: pbpString$ = "HE HITS IT"
        Case 2: pbpString$ = "HE NAILS IT"
        Case 3: pbpString$ = "HE KICKS IT"
        Case 4: pbpString$ = "HE LAUNCHES IT"
        Case 5: pbpString$ = "HE BOOTS IT"
        Case 6: pbpString$ = "HE BOOMS IT"
        Case 7: pbpString$ = "HE HITS ONE"
        Case 8: pbpString$ = "HE NAILS ONE"
        Case 9: pbpString$ = "HE KICKS ONE"
        Case 10: pbpString$ = "HE LAUNCHES ONE"
        Case 11: pbpString$ = "HE BOOTS ONE"
        Case 12: pbpString$ = "HE BOOMS ONE"
    End Select

    Call PBP

    RN0 = Int(Rnd(1) * 6) + 1

    Select Case RN0
        Case 1: pbpString$ = "IT'S A HIGH, SPIRALING KICK"
        Case 2: pbpString$ = "IT'S A HIGH FLOATER"
        Case 3: pbpString$ = "IT'S A HIGH END OVER END KICK"
        Case 4: pbpString$ = "IT'S A LOW, LINE DRIVE"
        Case 5: pbpString$ = "HE GETS IT TO TURN OVER"
        Case 6: pbpString$ = "HE DOESN'T GET IT TO TURN OVER"
    End Select

End Sub


'----------------------------------------
'           STUFFED Subroutine
'----------------------------------------
'This subroutine generates a Play-by-Play
'for "stuffed" run plays / losses
Sub STUFFED ()

    RN0 = Int(Rnd(1) * 12) + 1

    Select Case RN0
        Case 1: pbpString$ = "HE'S SNOWED UNDER BY A GANG OF TACKLERS"
        Case 2: pbpString$ = "HE MEETS A STONE WALL"
        Case 3: pbpString$ = "HE BOUNCES OFF ONE TACKLER"
        Case 4: pbpString$ = "HE SLIPS AND FALLS"
        Case 5: pbpString$ = "THE HOLE IS CLOSED"
        Case 6: pbpString$ = "HE FIGHTS FOR EXTRA YARDAGE"
        Case 7: pbpString$ = "HE'S STOOD UP!"
        Case 8: pbpString$ = "HIS FEET GO OUT FROM UNDER HIM"
        Case 9: pbpString$ = "HE SLIPS TO THE TURF"
        Case 10: pbpString$ = "DOWN HE GOES!!"
        Case 11: pbpString$ = "HE SLIPS TRYING TO MAKE HIS CUT"
        Case 12: pbpString$ = "HE CAN'T KEEP HIS FOOTING"
    End Select

    Call PBP

End Sub


'----------------------------------------
'          MISSEDFG Subroutine
'----------------------------------------
'This subroutine builds the Play-by-Play
'string for a missed Field Goal.
Sub MISSEDFG

    'direction of kick (right/left)
    fieldSide = Int(Rnd(1) * 2)

    Call d100(RN0)

    Select Case RN0

        Case 1 To 35: pbpString$ = "NO GOOD"

        Case 36 To 60: pbpString$ = "IT SAILS WIDE...NO GOOD"

        Case 61 To 80: pbpString$ = "HE MISSED IT!"

        Case 81 To 85: pbpString$ = "IT HITS THE UPRIGHTS....NO GOOD!"

        Case 86 To 90: pbpString$ = "HE HOOKS IT...NO GOOD!"

        Case 91 To 95:
            If 117 + goalPostAdj - ydLine > 45 Then
                pbpString$ = "IT FALLS SHORT....NO GOOD!"
            Else
                pbpString$ = "IT'S NO GOOD ... OFF TO THE " + direction$(fieldSide)
            End If

        Case 96 To 100: pbpString$ = "IT'S NO GOOD!"

    End Select

    Call PBP

End Sub


'----------------------------------------
'           ITSGOOD Subroutine
'----------------------------------------
'This subroutine builds the Play-by-Play
'string for a made Field Goal.
Sub ITSGOOD

    Call d100(RN0)

    Select Case RN0

        Case 1 To 10: pbpString$ = "IT'S GOOD!!!"
        Case 11 To 20: pbpString$ = "THE KICK IS GOOD!!!"
        Case 21 To 30: pbpString$ = "HE PUTS IT THROUGH THE UPRIGHTS!!!"
        Case 31 To 40: pbpString$ = "HE SPLITS THE UPRIGHTS!!!"
        Case 41 To 50: pbpString$ = "IT'S GOOD!!!"
        Case 51 To 60: pbpString$ = "HE HAMMERS IT THROUGH!!!"
        Case 61 To 70: pbpString$ = "RIGHT DOWN THE MIDDLE!!!"
        Case 71 To 80: pbpString$ = "IT HAS THE DISTANCE...IT'S GOOD!!!"
        Case 81 To 90: pbpString$ = "HE NAILS IT...IT'S GOOD!!!"
        Case 91 To 100: pbpString$ = "IT'S GOOD!"

    End Select

    Call PBP

End Sub


'----------------------------------------
'           MISSPAT Subroutine
'----------------------------------------
'This subroutine builds the Play-by-Play
'string for a missed PAT.
Sub MISSPAT

    Call d100(RN0)

    Select Case RN0

        Case 1 To 50: pbpString$ = "NO GOOD"
        Case 51 To 70: pbpString$ = "HE MISSED IT!"
        Case 71 To 75: pbpString$ = "BAD SNAP FROM CENTER!! NO KICK": gameStatsPlayer(P1, 6 + activeKicker(P1) * 4, 17) = gameStatsPlayer(P1, 6 + activeKicker(P1) * 4, 17) - 1
        Case 76 To 95: pbpString$ = "THE KICK SAILS WIDE!!...NO GOOD!"
        Case 96 To 100: pbpString$ = "IT HITS THE UPRIGHTS!!...NO GOOD!"

    End Select

    Call PBP

End Sub


'----------------------------------------
'           MADEPATSubroutine
'----------------------------------------
'This subroutine builds the Play-by-Play
'string for a made PAT.
Sub MADEPAT

    Call d100(RN0)

    Select Case RN0
        Case 1 To 25: pbpString$ = "IT'S GOOD!!!"
        Case 26 To 50: pbpString$ = "THE KICK IS GOOD!!!"
        Case 51 To 60: pbpString$ = "HE PUTS IT THROUGH THE UPRIGHTS!!!"
        Case 61 To 70: pbpString$ = "HE SPLITS THE UPRIGHTS!!!"
        Case 71 To 80: pbpString$ = "IT'S GOOD!!!"
        Case 81 To 90: pbpString$ = "HE HAMMERS IT THROUGH!!!"
        Case 91 To 100: pbpString$ = "RIGHT DOWN THE MIDDLE!!!"
    End Select

    Call PBP

End Sub


'----------------------------------------
'           KICKSET Subroutine
'----------------------------------------
'This subroutine handles the setup for a
'kick off by handling Play-by-Play
Sub KICKSET ()

    pbpString$ = kicker$(P1, activeKicker(P1)) + " IS PLACING THE BALL ON THE TEE"
    Call PBP

    Call d100(RN0)

    If RN0 <= 50 Then
        pbpString$ = gameTeams$(P1) + " IS LINING UP FOR THE KICKOFF"
    Else
        pbpString$ = "THE " + gameMascots$(P1) + " ARE LINING UP FOR THE KICKOFF"
    End If

    Call PBP

    pbpString$ = "AND THE KICK IS ON ITS WAY"
    Call PBP

    'Swap defense and offense
    Swap P1, D

    Call d100(RN0)

    If RN0 <= 88 Then
        '88% chance of kick travelling 55 to 67 yds
        kickYL = 100 - (ydLine + Int(Rnd(1) * 13) + 55 + goalPostAdj)
    Else
        '12% chance of kick travelling 68 to 75 yds
        kickYL = 100 - (ydLine + Int(Rnd(1) * 8) + 68 + goalPostAdj)
    End If

    If ruleOptType = 0 And ruleOptColl = 0 Then kickYL = kickYL + 10

    If climate(0) = P1 Then
        kickYL = kickYL - Int(Rnd(1) * 7) + 3 - climate(1)
    Else
        kickYL = kickYL + Int(Rnd(1) * 7) - 3 + climate(1)
    End If

End Sub


'----------------------------------------
'           QBTHROW Subroutine
'----------------------------------------
'This subroutine creates a play-by-play
'string for a QB pass.
Sub QBTHROW ()

    Call d100(RN0)
    Select Case RN0
        Case 1: pbpString$ = "HE PASSES IT"
        Case 2: pbpString$ = "HE FIRES IT"
        Case 3: pbpString$ = "HE FIRES THE BALL"
        Case 4: pbpString$ = "HE FIRES ONE"
        Case 5: pbpString$ = "HE FIRES A PASS"
        Case 6: pbpString$ = "HE RIFLES IT"
        Case 7: pbpString$ = "HE RIFLES THE BALL"
        Case 8: pbpString$ = "HE RIFLES ONE"
        Case 9: pbpString$ = "HE RIFLES A PASS"
        Case 10: pbpString$ = "HE THROWS"
        Case 11: pbpString$ = "HE TOSSES"
        Case 12: pbpString$ = "HE DRILLS A PASS"
        Case 13: pbpString$ = "HE FLOATS A PASS"
        Case 14: pbpString$ = "HE THREADS IT"
        Case 15: pbpString$ = "HE DRILLS THE BALL"
        Case 16: pbpString$ = "HE UNLOADS THE BALL"
        Case 17: pbpString$ = "HE UNLOADS IT"
        Case 18: pbpString$ = "HE RIPS THE BALL"
        Case 19: pbpString$ = "HE RIPS IT"
        Case 20: pbpString$ = "THROWN ON A LINE"
        Case 21: pbpString$ = "HE DRILLS IT"
        Case 22: pbpString$ = "HE FLINGS IT"
        Case 23: pbpString$ = "HE FIRES A BULLET"
    End Select

    Call PBP

End Sub


'----------------------------------------
'           RUNPBP Subroutine
'----------------------------------------
'This subroutine creates a play-by-play
'string for a run.
Sub RUNPBP ()

    If playCall <> 4 And playCall <> 5 Then

        RN0 = Int(Rnd(1) * 46) + 1

        Select Case RN0
            Case 1: pbpString$ = rbacks$(P1, I1) + " GAINS"
            Case 2: pbpString$ = rbacks$(P1, I1) + " BLASTS AHEAD FOR"
            Case 3: pbpString$ = rbacks$(P1, I1) + " SCOOTS OUT FOR"
            Case 4: pbpString$ = rbacks$(P1, I1) + " SCOOTS AHEAD FOR"
            Case 5: pbpString$ = rbacks$(P1, I1) + " IS AHEAD FOR"
            Case 6: pbpString$ = rbacks$(P1, I1) + " POPS THROUGH FOR"
            Case 7: pbpString$ = rbacks$(P1, I1) + " SLASHES AHEAD FOR"
            Case 8: pbpString$ = rbacks$(P1, I1) + " HAMMERS AHEAD FOR"
            Case 9: pbpString$ = rbacks$(P1, I1) + " HAMMERS THROUGH FOR"
            Case 10: pbpString$ = rbacks$(P1, I1) + " HAMMERS OUT"
            Case 11: pbpString$ = rbacks$(P1, I1) + " MUSCLES AHEAD FOR"
            Case 12: pbpString$ = rbacks$(P1, I1) + " MUSCLES THROUGH FOR"
            Case 13: pbpString$ = rbacks$(P1, I1) + " MUSCLES OUT"
            Case 14: pbpString$ = rbacks$(P1, I1) + " DASHES AHEAD FOR"
            Case 15: pbpString$ = rbacks$(P1, I1) + " DASHES THROUGH FOR"
            Case 16: pbpString$ = rbacks$(P1, I1) + " DASHES OUT FOR"
            Case 17: pbpString$ = rbacks$(P1, I1) + " RUMBLES AHEAD FOR"
            Case 18: pbpString$ = rbacks$(P1, I1) + " RUMBLES THROUGH FOR"
            Case 19: pbpString$ = rbacks$(P1, I1) + " RUMBLES FOR"
            Case 20: pbpString$ = rbacks$(P1, I1) + " CRACKS AHEAD FOR"
            Case 21: pbpString$ = rbacks$(P1, I1) + " CRACKS THROUGH FOR"
            Case 22: pbpString$ = rbacks$(P1, I1) + " DARTS AHEAD FOR"
            Case 23: pbpString$ = rbacks$(P1, I1) + " DARTS THROUGH FOR"
            Case 24: pbpString$ = rbacks$(P1, I1) + " DARTS FOR"
            Case 25: pbpString$ = rbacks$(P1, I1) + " KNIFES AHEAD FOR"
            Case 26: pbpString$ = rbacks$(P1, I1) + " KNIFES THROUGH FOR"
            Case 27: pbpString$ = rbacks$(P1, I1) + " BULLS HIS WAY FOR"
            Case 28: pbpString$ = rbacks$(P1, I1) + " BULLS AHEAD FOR"
            Case 29: pbpString$ = rbacks$(P1, I1) + " BULLS THROUGH FOR"
            Case 30: pbpString$ = rbacks$(P1, I1) + " MUSCLES HIS WAY FOR"
            Case 31: pbpString$ = rbacks$(P1, I1) + " BLASTS HIS WAY FOR"
            Case 32: pbpString$ = rbacks$(P1, I1) + " POWERS HIS WAY FOR"
            Case 33: pbpString$ = rbacks$(P1, I1) + " POWERS AHEAD FOR"
            Case 34: pbpString$ = rbacks$(P1, I1) + " POWERS THROUGH FOR"
            Case 35: pbpString$ = rbacks$(P1, I1) + " DIVES AHEAD FOR"
            Case 36: pbpString$ = rbacks$(P1, I1) + " DIVES OUT FOR"
            Case 37: pbpString$ = rbacks$(P1, I1) + " DIVES THROUGH FOR"
            Case 38: pbpString$ = rbacks$(P1, I1) + " CHARGES AHEAD FOR"
            Case 39: pbpString$ = rbacks$(P1, I1) + " CHARGES HIS WAY FOR"
            Case 40: pbpString$ = rbacks$(P1, I1) + " CHARGES THROUGH FOR"
            Case 41: pbpString$ = rbacks$(P1, I1) + " SMASHES HIS WAY FOR"
            Case 42: pbpString$ = rbacks$(P1, I1) + " SMASHES AHEAD FOR"
            Case 43: pbpString$ = rbacks$(P1, I1) + " SMASHES THROUGH FOR"
            Case 44: pbpString$ = rbacks$(P1, I1) + " BOLTS AHEAD FOR"
            Case 45: pbpString$ = rbacks$(P1, I1) + " BOLTS THROUGH FOR"
            Case 46: pbpString$ = rbacks$(P1, I1) + " BOLTS FOR"
        End Select

    Else

        RN0 = Int(Rnd(1) * 23) + 1

        Select Case RN0
            Case 1: pbpString$ = rbacks$(P1, I1) + " GALLOPS AHEAD FOR"
            Case 2: pbpString$ = rbacks$(P1, I1) + " GALLOPS THROUGH FOR"
            Case 3: pbpString$ = rbacks$(P1, I1) + " GALLOPS FOR"
            Case 4: pbpString$ = rbacks$(P1, I1) + " DASHES AHEAD FOR"
            Case 5: pbpString$ = rbacks$(P1, I1) + " DASHES THROUGH FOR"
            Case 6: pbpString$ = rbacks$(P1, I1) + " DASHES FOR"
            Case 7: pbpString$ = rbacks$(P1, I1) + " SCOOTS AHEAD FOR"
            Case 8: pbpString$ = rbacks$(P1, I1) + " SCOOTS THROUGH FOR"
            Case 9: pbpString$ = rbacks$(P1, I1) + " SCOOTS OUT FOR"
            Case 10: pbpString$ = rbacks$(P1, I1) + " DASHES OUT FOR"
            Case 11: pbpString$ = rbacks$(P1, I1) + " SCAMPERS THROUGH FOR"
            Case 12: pbpString$ = rbacks$(P1, I1) + " SCAMPERS OUT FOR"
            Case 13: pbpString$ = rbacks$(P1, I1) + " SCAMPERS FOR"
            Case 14: pbpString$ = rbacks$(P1, I1) + " SCAMPERS AHEAD FOR"
            Case 15: pbpString$ = rbacks$(P1, I1) + " SLIPS THROUGH FOR"
            Case 16: pbpString$ = rbacks$(P1, I1) + " SLIPS OUT FOR"
            Case 17: pbpString$ = rbacks$(P1, I1) + " SLIPS AHEAD FOR"
            Case 18: pbpString$ = rbacks$(P1, I1) + " SKIRTS OUTSIDE FOR"
            Case 19: pbpString$ = rbacks$(P1, I1) + " SKIRTS OUT FOR"
            Case 20: pbpString$ = rbacks$(P1, I1) + " TURNS THE CORNER FOR"
            Case 21: pbpString$ = rbacks$(P1, I1) + " TURNS UPFIELD FOR"
            Case 22: pbpString$ = rbacks$(P1, I1) + " GETS THE CORNER FOR"
            Case 23: pbpString$ = rbacks$(P1, I1) + " GETS UPFIELD FOR"

        End Select

    End If

End Sub


'----------------------------------------
'            PCPBP Subroutine
'----------------------------------------
'This subroutine builds the play-by-play
'text for a pass completion, along with
'udpating variables for position.
Sub PCPBP ()

    Call QBTHROW

    If FF% = 0 Then
        'Adjust Comp % based on ???

        Select Case playCall

            Case 9, 10
                If rushYds < 3 Then
                    CP = Int(Rnd(1) * 7) - 2
                Else
                    CP = Int(Rnd(1) * 6) + 2
                End If

            Case 13:
                If rushYds < 3 Then
                    CP = Int(Rnd(1) * 4) - 7
                Else
                    CP = (Int(Rnd(1) * 4) + 1) * -1
                End If

            Case 11, 14, 16, 17, 18:
                If rushYds < 4 Then
                    CP = Int(Rnd(1) * 5) + 3
                Else
                    If rushYds = 4 Or rushYds = 5 Then
                        CP = Int(Rnd(1) * 9) + 6
                    Else
                        CP = Int(Rnd(1) * 9) + 8
                    End If
                End If

            Case 12, 15
                If rushYds < 3 Then
                    CP = Int(Rnd(1) * 5) + 3
                Else
                    If rushYds = 3 Or rushYds = 4 Then
                        CP = Int(Rnd(1) * 10) + 10
                    Else
                        CP = Int(Rnd(1) * 36) + 20
                    End If
                End If
        End Select

    End If

    PC = CP

    If CP > ydsGained Then
        CP = ydsGained
        IT = 1
    End If

    ydsToScore = 100 - ydLine - CP

    Call YARDLINE
    Call MARKER

    If ydsToScore <= 0 Then
        pbpString$ = targetWRName$ + " MAKES THE CATCH IN THE END ZONE!!!"
        Call PBP
    Else
        Call d100(RN0)

        Select Case playCall
            Case 9, 10
                Select Case RN0
                    Case 1 To 33
                        pbpString$ = targetWRName$ + " HAS IT OVER THE MIDDLE"
                    Case 34 To 67
                        pbpString$ = targetWRName$ + " CATCHES IT IN THE FLAT"
                    Case 68 To 199
                        pbpString$ = "HE DUMPS IT TO " + targetWRName$
                End Select

            Case 13
                Select Case RN0
                    Case 1 To 33
                        pbpString$ = "HE SCREENS IT TO " + targetWRName$
                    Case 34 To 67
                        pbpString$ = "HE DUMPS THE SCREEN TO " + targetWRName$
                    Case 68 To 199
                        pbpString$ = "HE HAS " + targetWRName$ + " ON THE SCREEN PASS"
                End Select

            Case 11, 12, 14, 15
                Select Case RN0
                    Case 1 To 33
                        pbpString$ = targetWRName$ + " TAKES IT OVER THE MIDDLE"
                    Case 34 To 67
                        pbpString$ = "HE HAS " + targetWRName$
                    Case 68 To 199
                        pbpString$ = "HE COMPLETES IT TO " + targetWRName$
                End Select

            Case 16
                If RN0 <= 50 Then
                    pbpString$ = "HE HAS " + targetWRName$ + " NEAR THE SIDELINE"
                Else
                    pbpString$ = "HE FINDS " + targetWRName$
                End If

            Case 17, 18
                Select Case RN0
                    Case 1 To 50
                        pbpString$ = "HE HITS " + targetWRName$

                    Case 51 To 90
                        pbpString$ = "HE FIRES IT TO " + targetWRName$

                    Case 91 To 100
                        pbpString$ = targetWRName$ + " MAKES A GREAT CATCH"
                End Select

        End Select

        If FF% <> 0 Then pbpString$ = targetWRName$ + " MAKES A GREAT CATCH"

        pbpString$ = pbpString$ + " AT THE" + Str$(Int(ydLineTeam)) + " YARD LINE"

        If IT = 1 Then
            IT = 0
            Call PBP
            Call PBP
        Else
            'Adjust team position
            Call PBP
            Call PBP

            startYds = 100 - ydLine - CP
            endYds = 100 - ydLine - ydsGained

            Call MOVEBALL

        End If

    End If


End Sub


'----------------------------------------
'          Touchback Subroutine
'----------------------------------------
'This subroutine handles a touchback BY
'update the variables for down, YARDLINE
'Also calls Play-by-Play and sets the
'ball position marker.
Sub Touchback

    Call CHANGEPOSS

    '20 yard line
    ydLine = 20
    ydsToScore = 80

    '1st and 10
    currDown = 1
    ydsToFirst = 10

    pbpString$ = "TOUCHBACK"

    Call PBP
    Call MARKER
    Call YARDLINE

End Sub


'----------------------------------------
'      AddPossessionTime Subroutine
'----------------------------------------
'This subroutine updates the team's time
'of possession for the most recent play.
'The time added is between minSeconds
'and maxSeconds, determined randomly.
Sub AddPossessionTime (minSeconds, maxSeconds)

    secsDelta = maxSeconds - minSeconds
    timeElapsed! = Int(Rnd(1) * secsDelta) + minSeconds
    gameClock! = gameClock! - timeElapsed!
    timePoss(P1) = timePoss(P1) + timeElapsed!

End Sub


'----------------------------------------
'          FumblePBP Subroutine
'----------------------------------------
'This subroutine creates the play-by-play
'text for a fumnle
Sub FumblePBP ()

    RN0 = Int(Rnd(1) * 6) + 1

    Select Case RN0

        Case 1: pbpString$ = "*** FUMBLE ON THE PLAY!! ***"
        Case 2: pbpString$ = "*** LOOSE BALL!! ***"
        Case 3: pbpString$ = "*** BALL IS ON THE GROUND!! ***"
        Case 4: pbpString$ = "*** BALL IS KNOCKED LOOSE!! ***"
        Case 5: pbpString$ = "*** BALL IS STRIPPED!! ***"
        Case 6: pbpString$ = "*** FREE BALL !! ***"

    End Select

    Call PBP

End Sub


'----------------------------------------
'          TryForTwoResult Subroutine
'----------------------------------------
'This subroutine makes a generic attempt
'to determine if the attempt at 2PC is
'successfull. It also gives Play-by-Play
'text for the attempt.
Sub TryForTwoResult ()

    If playType = 1 Then K1(nbrScores, 4) = 20
    If playType = 2 Then K1(nbrScores, 4) = 30

    pbpString$ = "TRY FOR TWO IS "
    gameStatsTeam(P1, 35) = gameStatsTeam(P1, 35) + 1

    If ydsGained < 2 And ruleOptType = 1 And ruleOptPro = 3 Then

        pbpString$ = pbpString$ + "NO GOOD"
        Call PBP

    Else

        If ydsGained < 3 And ruleOptType <> 1 And ruleOptPro <> 3 Then
            pbpString$ = pbpString$ + "NO GOOD"
            Call PBP
        Else
            'If ydsGained >= 3 Or ruleOptType = 1 And ruleOptPro = 3 And ydsGained >= 2 Then
            pbpString$ = pbpString$ + "GOOD"
            Call PBP

            score(P1, 0) = score(P1, 0) + 2
            score(P1, quarter) = score(P1, quarter) + 2
            K1(nbrScores, 4) = K1(nbrScores, 4) + 1
            gameStatsTeam(P1, 36) = gameStatsTeam(P1, 36) + 1

            Call DisplayField

        End If

    End If

    'No longer dealing with endzone situation
    endZone = 0

End Sub



'----------------------------------------
'           OPTIONS Subroutine
'----------------------------------------
'This subroutine gives you a small menu
'for changing some of the game options
'(such as pbp delay, coaching mode) in
'the middle of game play.
Sub OPTIONS ()

    Shared playMode$()

    Color 15, 0

    Screen , , 0, 0

    Call INFO

    Do

        Color , 0
        Locate 3, 1
        Color 14: Print "(1) ";
        Color 15: Print playMode$(playerOpt)
        Color 14: Print "(2) ";
        Color 15: Print Using "PLAY-BY-PLAY DELAY: #.# "; pbpDelay!
        Print
        Color 14: Print "<ENTER> ";
        Color 15: Print "RETURN TO GAME PLAY"

        Do
            Color 7, 0
            'Locate 24, 1: Print "HIT ANY KEY TO CONTINUE";
            I$ = GetKeyPress$
            I = Val(I$)
        Loop Until I >= 0 And I <= 2

        Select Case I
            Case 1
                playerOpt = playerOpt + 1
                If playerOpt > 3 Then playerOpt = 0
            Case 2
                Locate 4, 23: Input pbpDelay!
        End Select

        ' Determine computer team
        Select Case playerOpt

            Case 1:
                playerMode = 1
                compTeam = 0

            Case 2:
                playerMode = 1
                compTeam = 1

            Case 3:
                playerMode = 2

        End Select

    Loop Until I$ = Chr$(13)

    Screen , , 0, 0
    'Call DisplayField

End Sub


'----------------------------------------
'           EVENTS Subroutine
'----------------------------------------
'This subroutine works with another to
'look for any events that meet the limits
'for "special". If found, it will create
'a corresponding "special" box score
Sub EVENTS (specialCount, specialStat)

    Dim eventSettings(13)

    If _FileExists("EVENTS.BOX") Then

        Open "EVENTS.BOX" For Input As #1

        For X = 1 To 13
            Input #1, eventSettings(X)
        Next

        Close #1

        checkSpecial% = 0

        If quarter > eventSettings(1) Then
            checkSpecial% = 1
        Else

            If score(1, 0) > eventSettings(2) Or score(0, 0) > eventSettings(2) Or score(1, 0) < eventSettings(3) Or score(0, 0) < eventSettings(3) Then
                checkSpecial% = 1
            Else

                For I = 0 To 1

                    For I1 = 0 To 9

                        '--Combine into multiple Or
                        If gameStatsPlayer(I, I1, 1) > eventSettings(4) Then
                            checkSpecial% = 1
                        Else
                            If gameStatsPlayer(I, I1, 2) > eventSettings(5) Then
                                checkSpecial% = 1
                            Else
                                If gameStatsPlayer(I, I1, 4) > eventSettings(6) Then
                                    checkSpecial% = 1
                                Else
                                    If gameStatsPlayer(I, I1, 5) > eventSettings(7) Then
                                        checkSpecial% = 1
                                    End If
                                End If
                            End If
                        End If

                        If checkSpecial% = 1 Then Exit For

                    Next

                Next

                If checkSpecial% <> 1 Then

                    For I = 0 To 1

                        For I1 = 0 To 5

                            If gameStatsPlayer(I, I1, 7) > eventSettings(6) Then
                                checkSpecial% = 1
                            Else
                                If gameStatsPlayer(I, I1, 8) > eventSettings(7) Then
                                    checkSpecial% = 1
                                End If
                            End If

                            If checkSpecial% = 1 Then Exit For

                        Next

                    Next

                End If

                If checkSpecial% <> 1 Then

                    For I = 0 To 1
                        For I1 = 0 To 3
                            If gameStatsPlayer(I, I1, 12) > eventSettings(8) Then
                                checkSpecial% = 1
                            Else
                                If gameStatsPlayer(I, I1, 13) > eventSettings(9) Then
                                    checkSpecial% = 1
                                End If
                            End If
                        Next
                    Next

                End If

                If checkSpecial% <> 1 Then

                    For I = 0 To 1
                        If gameStatsPlayer(I, 3, 17) > eventSettings(10) Then
                            checkSpecial% = 1
                        Else
                            If gameStatsPlayer(I, 7, 17) > eventSettings(10) Then
                                checkSpecial% = 1
                            End If
                        End If
                    Next

                End If

            End If

        End If

    End If 'Done checking EVENTS.BOX file

    If checkSpecial% = 1 Then
        Call FoundSpecial(specialCount, specialStat)
    End If

End Sub


'----------------------------------------
'        FoundSpecial Subroutine
'----------------------------------------
'This subroutine simply counts how many
'special boxscore files already exist, so
'that it an assign a unique number to the
'boxscore about to be created.
Sub FoundSpecial (specialCount, specialStat)

    'Get count of SPC files to determine
    'number to assign next one
    'Spec$ = diskPaths$(1) + "*.BSS"
    Spec$ = diskPaths$(1) + "*.SPC"
    specialCount = FileCount%(Spec$)
    specialCount = specialCount + 1
    specialStat = 1

End Sub


'----------------------------------------
'          POSTGAME Subroutine
'----------------------------------------
'This subroutine processes everything that
'is necessary after a game is complete.
'It checks for special events, handles a
'prompt for stats, and saving of stats.
'Records in Football are only calculated
'when stats are compiled.
Sub POSTGAME ()

    Shared scheduleFile$

    'Call CheckRecords
    Call EVENTS(specialCount, specialStat)
    endGame = 1

    Do

        Color 15, 0
        Cls

        Color 14: Print "(0) ";
        Color 15: Print "STATS TO SCREEN"
        Print

        Color 14: Print "(1) ";
        Color 15: Print "STATS TO PRINTER"
        Print

        Color 14: Print "(2) ";
        Color 15: Print "STATS TO TEXT FILE"

        If autoPlay = 1 And playerMode = 2 Then
            X = 2
        Else

            Do
                J$ = GetKeyPress$
                X = Val(J$)
            Loop Until X >= 0 And X <= 2

        End If

        Call BOXOPTIONS(X)

        'Reset option for next call
        If X <> 0 Then X = 0

        Color 15, 0
        Cls

        If Not (autoPlay = 1 And playerMode = 2) Then

            Do
                'Using Home Team's color for the box
                Call TEAMBOX(1, 1)
                pbpString$ = "DISPLAY STATS AGAIN? (Y/N)"
                Locate 7, (80 - Len(pbpString$)) / 2
                Print pbpString$
                Color 15, 0
                I$ = GetKeyPress$
            Loop Until UCase$(I$) = "Y" Or UCase$(I$) = "N"

        End If

    Loop Until UCase$(I$) = "N" Or (autoPlay = 1 And playerMode = 2)

    Call TICKER

    'If we had special events
    If specialStat = 1 Then

        boxName$ = "BOX" + Mid$(Str$(specialCount), 2, Len(Str$(specialCount))) + ".SPC"
        'boxName$ = "BOXSCORE" + Mid$(Str$(specialCount), 2, Len(Str$(specialCount))) + ".BSS"

        'Append special events
        'Call BOX2SPC(boxName$)

        Call BOX2FILE(2, boxName$, specialStat)

    End If

    Call SAVESTATS

    endGame = 0
    specialStat = 0

End Sub


'----------------------------------------
'         BOXOPTIONS Subroutine
'----------------------------------------
'This subroutine takes appropriate action
'for the output of the box file, which
'depends on the choice given to it.

' PARAMETER outputChoice
'    represents menu choice
'       0 = Display on screen
'       1 = Send to printer
'       2 = Save to file

'The option to send to printer will first
'create a temporary file, then loop THROUGH
'it to print each line.
Sub BOXOPTIONS (outputChoice)

    Shared scheduleFile$

    Select Case outputChoice

        Case 0
            Call BOX2SCREEN

        Case 1, 2
            If outputChoice = 1 Then
                'Printer = temporary file
                boxName$ = "TEMPBOX"
            Else
                'File = prompt | auto-create
                If autoPlay <> 1 Then
                    Cls
                    Print "ENTER 8-CHARACTER FILENAME FOR BOXSCORE (NO SPACES OR PERIODS)";
                    Input boxName$
                    boxName$ = boxName$ + ".SPC"
                    'boxName$ = boxName$ + ".BSS"
                Else
                    'ScheduleName_GameXXX.BS
                    boxName$ = "BOX" + Mid$(Str$(schedGame(0) + schedGame(2)), 2, Len(Str$(schedGame(0) + schedGame(2))))
                    'boxName$ = Mid$(scheduleFile$, 1, Len(scheduleFile$) - 4) + "_GAME" + Mid$(Str$(schedGame(0) + schedGame(2)), 2, Len(Str$(schedGame(0) + schedGame(2)))) + ".BS"
                End If

            End If

            'Includes a special option to send to printer when done
            Call BOX2FILE(outputChoice, boxName$, 0)

    End Select

End Sub


'----------------------------------------
'          BOX2FILE Subroutine
'----------------------------------------
'This subroutine formats the boxscore for
'an output file.

' PARAMETER printChoice
'    choice to print file vs. saving it

'The option to send to printer will first
'create a temporary file, then loop THROUGH
'it to print each line.
Sub BOX2FILE (printChoice, boxName$, hasSpecial)

    Shared actualAttendance&

    If hasSpecial = 1 Then
        Open (diskPaths$(1) + boxName$) For Append As #1
    Else
        Open (diskPaths$(1) + boxName$) For Output As #1
    End If

    If gameLoc = 2 Then
        Print #1, "SITE: NEUTRAL"
    Else
        Print #1, "SITE: "; gameStadium$
    End If

    Print #1, Using "ATTENDANCE: ###,###"; actualAttendance&
    Print #1,

    Print #1, Tab(20); "Q1 Q2 Q3 Q4 ";
    If quarter > 4 Then
        For I1 = 5 To quarter
            Print #1, "OT ";
        Next
    End If

    Print #1, "- GAME"

    'Score summary
    For I = 0 To 1

        Print #1, gameTeams$(I); Tab(20);
        Print #1, Using "## ## ## ## "; score(I, 1), score(I, 2), score(I, 3), score(I, 4);

        If quarter > 4 Then
            For I1 = 5 To quarter
                Print #1, Using "## "; score(I, I1);
            Next
        End If

        Print #1, "- ";
        Print #1, Using "###"; score(I, 0)

    Next I

    Print #1,

    If nbrScores <> 0 Then

        For I = 1 To nbrScores

            Print #1, gameTeams$(K1(I, 1)); "-";

            TI = scoreTimes(I)
            TI = TI + TI * (TI < 0)
            IR% = Int(TI / 60)
            J = TI - IR% * 60

            Print #1, Tab(17);

            'Minutes
            Print #1, Using "##"; IR%;
            Print #1, ":";

            'Seconds
            If J >= 10 Then
                Print #1, Using "## "; J;
            Else
                Print #1, Using "0#"; J;
            End If

            Print #1, Tab(23);

            If scoreQuarters(I) = 1 Then Print #1, "OF 1ST -";
            If scoreQuarters(I) = 2 Then Print #1, "OF 2ND -";
            If scoreQuarters(I) = 3 Then Print #1, "OF 3RD -";
            If scoreQuarters(I) = 4 Then Print #1, "OF 4TH -";
            If scoreQuarters(I) > 4 Then Print #1, "OF OT "; scoreQuarters(I) - 4; " -";

            '# of yards for score
            If K1(I, 0) <> 7 Then Print #1, Int(K1(I, 5) + .5);

            tryPAT = 1

            Select Case K1(I, 0)

                Case 1
                    Print #1, "RUN BY "; rbacks$(K1(I, 1), K1(I, 2))

                Case 2
                    Print #1, "PASS BY "; qbacks$(K1(I, 1), K1(I, 2));: Print #1, " TO ";
                    If K1(I, 3) > 9 Then Print #1, wdRec$(K1(I, 1), K1(I, 3) - 10)
                    If K1(I, 3) <= 9 Then Print #1, rbacks$(K1(I, 1), K1(I, 3))

                Case 3
                    Print #1, "K.O. RETURN BY "; kickRet$(K1(I, 1), K1(I, 2))

                Case 4
                    Print #1, "PUNT RETURN BY "; pret$(K1(I, 1), K1(I, 2))

                Case 5
                    Print #1, "INTERCEPTION RETURN BY "; defInts$(K1(I, 1), K1(I, 2))

                Case 6
                    Print #1, "BLOCKED KICK RECOVERED IN END ZONE"

                Case 7
                    Print #1, "SAFETY"
                    tryPAT = 0

                Case 8
                    Print #1, "FG BY "; kicker$(K1(I, 1), K1(I, 2))
                    tryPAT = 0

                Case 9
                    Print #1, "RUN BY "; qbacks$(K1(I, 1), K1(I, 2))

                Case 10
                    Print #1, "FUMBLE RETURN "

                Case 11
                    Print #1, "FAKE PUNT", K1(I, 2)

                Case 12
                    Print #1, "2-PT DEFENSE RETURN BY "; defInts$(K1(I, 1), K1(I, 2))
                    tryPAT = 0

            End Select

            If tryPAT = 1 Then

                Print #1, Tab(17);
                If K1(I, 4) = 10 Then Print #1, " (KICK FAILED)"
                If K1(I, 4) = 11 Then Print #1, " (KICK BY "; kicker$(K1(I, 1), K1(I, 6));: Print #1, ")"
                If K1(I, 4) = 20 Then Print #1, " (RUN FAILED)"
                If K1(I, 4) = 21 Then Print #1, " (RUN GOOD)"
                If K1(I, 4) = 30 Then Print #1, " (PASS FAILED)"
                If K1(I, 4) = 31 Then Print #1, " (PASS GOOD)"

            End If

        Next I

    End If

    Print #1,
    Print #1,

    Print #1, Tab(27); "TEAM STATISTICS"
    hSpace1 = (15 - Len(gameTeams$(0))) / 2
    hSpace2 = (15 - Len(gameTeams$(1))) / 2

    'gameStatsTeam() = Offense Stats
    Print #1, Tab(10 + hSpace1); gameTeams$(0); Tab(45 + hSpace2); gameTeams$(1)

    'gameStatsTeam(x, 0) = First downs
    Print #1, Tab(17); gameStatsTeam(0, 0); Tab(29); "FIRST DOWNS"; Tab(51); gameStatsTeam(1, 0)

    Print #1, Tab(14);

    'gameStatsTeam(x, 1) = Rush Att
    'gameStatsTeam(x, 2) = Rush Yds
    If ruleOptType = 0 Then
        Print #1, gameStatsTeam(0, 1); "-"; Int(gameStatsTeam(0, 2) + .5); Tab(29); "RUSHING YDS"; Tab(48); gameStatsTeam(1, 1); "-"; Int(gameStatsTeam(1, 2) + .5)
    Else
        Print #1, gameStatsTeam(0, 1) - sackStatsTeam(0, 0); "-"; Int(gameStatsTeam(0, 2) + sackStatsTeam(0, 1) + .5); Tab(29); "RUSHING YDS"; Tab(48); gameStatsTeam(1, 1) - sackStatsTeam(1, 0); "-"; Int(gameStatsTeam(1, 2) + sackStatsTeam(1, 1) + .5)
    End If

    'gameStatsTeam(x, 6) = Pass Yds
    Print #1, Tab(16); Int(gameStatsTeam(0, 6) + .5); Tab(29); "PASSING YDS"; Tab(51); Int(gameStatsTeam(1, 6) + .5)

    Print #1, Tab(16); Int(gameStatsTeam(0, 6) + .5) + Int(gameStatsTeam(0, 2) + .5); Tab(30); "TOTAL YDS"; Tab(51); Int(gameStatsTeam(1, 6) + .5) + Int(gameStatsTeam(1, 2) + .5)

    'gameStatsTeam(x, 3) = Pass Att
    'gameStatsTeam(x, 4) = Pass Comp
    'gameStatsTeam(x, 5) = Pass Int
    Print #1, Tab(13); gameStatsTeam(0, 4); "-"; gameStatsTeam(0, 3); "-"; gameStatsTeam(0, 5); Tab(31); "PASSES"; Tab(47); gameStatsTeam(1, 4); "-"; gameStatsTeam(1, 3); "-"; gameStatsTeam(1, 5)

    'gameStatsTeam(x, 7) = Punts
    'gameStatsTeam(x, 8) = Punt Yds
    If gameStatsTeam(0, 7) <> 0 And gameStatsTeam(1, 7) <> 0 Then
        Print #1, Tab(15); gameStatsTeam(0, 7); "-"; Int((gameStatsTeam(0, 8) / gameStatsTeam(0, 7)) * 10 + .5) / 10; Tab(32); "PUNTS"; Tab(49); gameStatsTeam(1, 7); "-"; Int((gameStatsTeam(1, 8) / gameStatsTeam(1, 7)) * 10 + .5) / 10
    End If

    'gameStatsTeam(x, 9) = Fumbles Total
    'gameStatsTeam(x, 10) = Fumbles Lost
    Print #1, Tab(15); gameStatsTeam(0, 9); "-"; gameStatsTeam(0, 10); Tab(31); "FUM/LOST"; Tab(50); gameStatsTeam(1, 9); "-"; gameStatsTeam(1, 10)

    'gameStatsTeam(x, 11) = Penalties
    'gameStatsTeam(x, 12) = Penalty Yards
    Print #1, Tab(15); gameStatsTeam(0, 11); "-"; Int(gameStatsTeam(0, 12) + .5); Tab(31); "PENALTIES"; Tab(50); gameStatsTeam(1, 11); "-"; Int(gameStatsTeam(1, 12) + .5)

    Print #1, Tab(15); sackStatsTeam(0, 0); "-"; Int(sackStatsTeam(0, 1) + .5); Tab(32); "SACKED"; Tab(50); sackStatsTeam(1, 0); "-"; Int(sackStatsTeam(1, 1) + .5)

    Print #1, Tab(16);
    Print #1, Using "##"; Int(timePoss(0) / 60);
    Print #1, ":";
    Print #1, Using "##"; timePoss(0) - Int(timePoss(0) / 60) * 60;

    Print #1, Tab(26); "TIME OF POSSESSION";

    Print #1, Tab(51);
    Print #1, Using "##"; Int(timePoss(1) / 60);
    Print #1, ":";
    Print #1, Using "##"; timePoss(1) - Int(timePoss(1) / 60) * 60

    Print #1, Tab(15); thirdDownAtt(0) - thirdDownFail(0); "-"; thirdDownAtt(0); Tab(27); "THIRD DOWN EFF"; Tab(49); thirdDownAtt(1) - thirdDownFail(1); "-"; thirdDownAtt(1)
    Print #1,
    Print #1,

    Print #1, Tab(17); "RUSHING STATISTICS"
    Call ADDBACKS

    For I = 0 To 1

        Print #1, gameTeams$(I); Tab(19); "TC   YDS   AVG   LG   TD"

        For I1 = 0 To 13

            If gameStatsPlayer(I, I1, 0) <> 0 Then

                If I1 <= 9 Then
                    Print #1, rbacks$(I, I1);
                Else
                    Print #1, qbacks$(I, I1 - 10);
                End If

                'gameStatsPlayer(I, x, 0) = Rush Att
                'gameStatsPlayer(I, x, 1) = Yds
                'gameStatsPlayer(I, x, 2) = TD
                Print #1, Tab(18); Using "###  #### ###.#  "; gameStatsPlayer(I, I1, 0), Int(gameStatsPlayer(I, I1, 1) + .5), Int(gameStatsPlayer(I, I1, 1) + .5) / gameStatsPlayer(I, I1, 0);

                If I1 <= 9 Then
                    'Long   TD
                    Print #1, Using "###   ##"; runBacks(I, I1), gameStatsPlayer(I, I1, 2)
                Else
                    'TD
                    Print #1, Using "      ##"; gameStatsPlayer(I, I1, 2)
                End If
            End If

        Next I1

        Print #1,

    Next I

    Print #1, Tab(17); "RECEIVING"

    For I = 0 To 1

        Print #1, gameTeams$(I); Tab(19); "TC    YDS    AVG   LG    TD"

        For I1 = 0 To 5

            If gameStatsPlayer(I, I1, 6) <> 0 Then
                Print #1, wdRec$(I, I1); Tab(19);
                'gameStatsPlayer(I, x, 6) = Catches
                'gameStatsPlayer(I, x, 7) = Yds
                'gameStatsPlayer(I, x, 8) = TD
                Print #1, Using "##    ###   ##.#  ###    ##"; gameStatsPlayer(I, I1, 6), Int(gameStatsPlayer(I, I1, 7) + .5), Int(gameStatsPlayer(I, I1, 7) + .5) / gameStatsPlayer(I, I1, 6), WR(I, I1 + 10), gameStatsPlayer(I, I1, 8)
            End If

        Next I1

        For I1 = 0 To 9

            If gameStatsPlayer(I, I1, 3) <> 0 Then
                Print #1, rbacks$(I, I1); Tab(19);
                'gameStatsPlayer(I, x, 3) = Catches
                'gameStatsPlayer(I, x, 4) = Yds
                'gameStatsPlayer(I, x, 5) = TD
                Print #1, Using "##    ###   ##.#  ###    ##"; gameStatsPlayer(I, I1, 3), Int(gameStatsPlayer(I, I1, 4) + .5), Int(gameStatsPlayer(I, I1, 4) + .5) / gameStatsPlayer(I, I1, 3), WR(I, I1), gameStatsPlayer(I, I1, 5)
            End If

        Next I1

        Print #1,

    Next I

    Print #1, Tab(17); "PASSING"

    For I = 0 To 1

        Print #1, gameTeams$(I); Tab(19); "ATT  COM COM %  YDS  Y/A  Y/C  TD  TD%  I    I%  LG"

        For I1 = 0 To 3

            If gameStatsPlayer(I, I1, 9) <> 0 Then

                Print #1, qbacks$(I, I1); Tab(19);
                'gameStatsPlayer(I, x, 9) = Pass Att
                'gameStatsPlayer(I, x, 10) = Completions
                'gameStatsPlayer(I, x, 11) = Interceptions
                'gameStatsPlayer(I, x, 12) = Yards
                'gameStatsPlayer(I, x, 13) = TDs
                Print #1, Using "###  ### ###.#  ### ##.# "; gameStatsPlayer(I, I1, 9), gameStatsPlayer(I, I1, 10), gameStatsPlayer(I, I1, 10) / gameStatsPlayer(I, I1, 9) * 100, Int(gameStatsPlayer(I, I1, 12) + .5), Int(gameStatsPlayer(I, I1, 12) + .5) / gameStatsPlayer(I, I1, 9);

                If gameStatsPlayer(I, I1, 10) <> 0 Then
                    'Y/C, TD
                    Print #1, Using "##.#  ##"; Int(gameStatsPlayer(I, I1, 12) + .5) / gameStatsPlayer(I, I1, 10), gameStatsPlayer(I, I1, 13);
                Else
                    'TDs
                    Print #1, Using "      ##"; gameStatsPlayer(I, I1, 13);
                End If

                'TD%, Ints, Int%, Longest
                Print #1, Using "###.# ## ###.# ###"; gameStatsPlayer(I, I1, 13) / gameStatsPlayer(I, I1, 9) * 100, gameStatsPlayer(I, I1, 11), gameStatsPlayer(I, I1, 11) / gameStatsPlayer(I, I1, 9) * 100, quarterbacks(I, I1)
            End If

        Next I1

        Print #1,

    Next I

    Print #1, Tab(17); "KICKOFF RETURNS"

    For I = 0 To 1

        Print #1, gameTeams$(I); Tab(19); "KR   YDS   AVG   LG  TD"

        For I1 = 0 To 2
            If gameStatsPlayer(I, I1, 14) <> 0 Then
                Print #1, kickRet$(I, I1); Tab(19);
                'gameStatsPlayer(I, x, 14) = # Returns
                'gameStatsPlayer(I, x, 15) = Yards
                'gameStatsPlayer(I, x, 16) = TDs
                Print #1, Using "##  ####  ##.#  ###  ##"; gameStatsPlayer(I, I1, 14), Int(gameStatsPlayer(I, I1, 15) + .5), Int(gameStatsPlayer(I, I1, 15) + .5) / gameStatsPlayer(I, I1, 14), kickReturners(I, I1), gameStatsPlayer(I, I1, 16)
            End If
        Next I1

        Print #1,

    Next I

    Print #1,
    Print #1, Tab(17); "PUNT RETURNS"

    For I = 0 To 1

        Print #1, gameTeams$(I); Tab(19); "PR   YDS   AVG   LG  TD"

        For I1 = 3 To 5
            If gameStatsPlayer(I, I1, 14) <> 0 Then
                Print #1, pret$(I, I1 - 3); Tab(19);
                'gameStatsPlayer(I, x, 14) = # Returns
                'gameStatsPlayer(I, x, 15) = Yards
                'gameStatsPlayer(I, x, 16) = TDs
                Print #1, Using "##  ####  ##.#  ###  ##"; gameStatsPlayer(I, I1, 14), Int(gameStatsPlayer(I, I1, 15) + .5), Int(gameStatsPlayer(I, I1, 15) + .5) / gameStatsPlayer(I, I1, 14), puntReturners(I, I1 - 3), gameStatsPlayer(I, I1, 16)
            End If
        Next I1

        Print #1,

    Next I

    Print #1, Tab(17); "PUNTING"

    For I = 0 To 1

        Print #1, gameTeams$(I); Tab(19); "PNT   AVG  LG  TB  BLK  20"

        If gameStatsTeam(I, 7) <> 0 Then
            Print #1, punter$(I, 0); Tab(19);
            'gameStatsTeam(x, 7) = # Punts
            'gameStatsTeam(x, 8) = Yards
            'gameStatsTeam(x, 26) = Longest
            'gameStatsTeam(x, 27) = Touchbacks
            'gameStatsTeam(x, 28) = Blocks
            'gameStatsTeam(x, 29) = 20
            Print #1, Using "###  ##.#  ##  ##   ##  ##  ##  ##.#  ##.#"; gameStatsTeam(I, 7), gameStatsTeam(I, 8) / gameStatsTeam(I, 7), gameStatsTeam(I, 26), gameStatsTeam(I, 27), gameStatsTeam(I, 28), gameStatsTeam(I, 29)
        End If

        Print #1,

    Next I

    Print #1, Tab(17); "KICKING"

    For I = 0 To 1

        Print #1, gameTeams$(I); Tab(19); "EPM EPA   EP% FGM FGA   FG% LG  0-19 20-29 30-39 40-49  50+"

        If gameStatsPlayer(I, 4, 17) <> 0 Or gameStatsPlayer(I, 6, 17) <> 0 Then

            Print #1, kicker$(I, 0); Tab(19);
            'gameStatsPlayer(I, x, 5, 17) = EPM, 1st PK
            'gameStatsPlayer(I, x, 6, 17) = EPA, 1st PK
            If gameStatsPlayer(I, 6, 17) <> 0 Then Print #1, Using "### ### ###.# "; gameStatsPlayer(I, 5, 17), gameStatsPlayer(I, 6, 17), gameStatsPlayer(I, 5, 17) / gameStatsPlayer(I, 6, 17) * 100;

            If gameStatsPlayer(I, 4, 17) = 0 Then
                Print #1,
            Else
                Print #1, Tab(33); Using "### ### ###.# ##  #"; gameStatsPlayer(I, 3, 17), gameStatsPlayer(I, 4, 17), gameStatsPlayer(I, 3, 17) / gameStatsPlayer(I, 4, 17) * 100, gameLongFG(I, 0), FM(I, 0, 0);
                'gameStatsPlayer(I, x, 3, 17) = FGM, 1st PK
                'gameStatsPlayer(I, x, 4, 17) = FGA, 1st PK

                'FG's 0 - 19
                Print #1, "- ";: Print #1, Using "#  #"; FA(I, 0, 0); FM(I, 0, 1);
                'FG's 20 - 29
                Print #1, "- ";: Print #1, Using "#  #"; FA(I, 0, 1); FM(I, 0, 2);
                'FG's 30 - 39
                Print #1, "- ";: Print #1, Using "#  #"; FA(I, 0, 2); FM(I, 0, 3);
                'FG's 40 - 49
                Print #1, "- ";: Print #1, Using "#  #"; FA(I, 0, 3); FM(I, 0, 4);
                'FG's 50+
                Print #1, "- ";: Print #1, Using "#"; FA(I, 0, 4)
            End If

        End If

        If gameStatsPlayer(I, 8, 17) <> 0 Or gameStatsPlayer(I, 10, 17) <> 0 Then

            Print #1, kicker$(I, 1); Tab(19);
            'gameStatsPlayer(I, x, 7, 17) = EPM, 2nd PK
            'gameStatsPlayer(I, x, 8, 17) = EPA, 2nd PK
            If gameStatsPlayer(I, 10, 17) <> 0 Then Print #1, Using "### ### ###.# "; gameStatsPlayer(I, 9, 17), gameStatsPlayer(I, 10, 17), gameStatsPlayer(I, 9, 17) / gameStatsPlayer(I, 10, 17) * 100;

            If gameStatsPlayer(I, 8, 17) = 0 Then
                Print #1,
            Else
                Print #1, Tab(33); Using "### ### ###.# ##  #"; gameStatsPlayer(I, 7, 17), gameStatsPlayer(I, 8, 17), gameStatsPlayer(I, 7, 17) / gameStatsPlayer(I, 8, 17) * 100, gameLongFG(I, 1), FM(I, 1, 0);
                'gameStatsPlayer(I, x, 9, 17) = FGM, 2nd PK
                'gameStatsPlayer(I, x, 10, 17) = FGA, 2nd PK

                'FG's 0 - 19
                Print #1, "- ";: Print #1, Using "#  #"; FA(I, 1, 0); FM(I, 1, 1);
                'FG's 20 - 29
                Print #1, "- ";: Print #1, Using "#  #"; FA(I, 1, 1); FM(I, 1, 2);
                'FG's 30 - 39
                Print #1, "- ";: Print #1, Using "#  #"; FA(I, 1, 2); FM(I, 1, 3);
                'FG's 40 - 49
                Print #1, "- ";: Print #1, Using "#  #"; FA(I, 1, 3); FM(I, 1, 4);
                'FG's 50+
                Print #1, "- ";: Print #1, Using "#"; FA(I, 1, 4)
            End If

        End If

        Print #1,

    Next I

    Print #1,
    Print #1, "INTERCEPTIONS"

    For I = 0 To 1:

        Print #1, gameTeams$(I); Tab(16);
        Print #1, "IR   YDS   AVG   LG  TD"

        For I1 = 0 To 9
            If gameInts(I, I1, 0) <> 0 Then
                Print #1, defInts$(I, I1); Tab(16);
                Print #1, Using "##  ####  ##.#  ###  ##"; gameInts(I, I1, 0), Int(gameInts(I, I1, 1) + .5), Int(gameInts(I, I1, 1) + .5) / gameInts(I, I1, 0), IR(I, I1), gameInts(I, I1, 2)
            End If
        Next I1

        Print #1,

    Next I

    Print #1, "SACKS"

    For I = 0 To 1:
        Print #1, gameTeams$(I); Tab(19);
        Print #1, "NO.   YDS LOST"
        For I1 = 0 To 14
            If totalSacks(I, I1, 0) <> 0 Then
                Print #1, defSacks$(I, I1); Tab(19);
                Print #1, Using " ##       ##"; totalSacks(I, I1, 0), Int(totalSacks(I, I1, 1) + .5)
            End If
        Next I1
        Locate , 1: Print #1,
    Next I

    If printChoice = 1 Then Print #1, Chr$(12)

    Close #1

    If printChoice = 1 Then

        Open diskPaths$(1) + boxName$ For Input As #2

        Do
            Line Input #2, X$
            LPrint X$
        Loop While Left$(X$, 1) <> Chr$(12)

        Close #2

        Kill "TEMPBOX"

    End If


End Sub


'----------------------------------------
'           BOX2SPC Subroutine
'----------------------------------------
'This subroutine adds any special event
'information to a boxscore file
Sub BOX2SPC (boxName$)

    Open diskPaths$(1) + boxName$ For Output As #1

    Print #1,

    'Output any special events
    Print #1,

    Close #1

End Sub


'----------------------------------------
'         BOX2SCREEN Subroutine
'----------------------------------------
'This subroutine formats the boxscore for
'output to the end user display.
Sub BOX2SCREEN ()

    Shared actualAttendance&

    Color 15, 0
    Cls

    Color 14
    Locate 2, 1

    If gameLoc = 2 Then
        Print "SITE: ";
        Color 15
        Print "NEUTRAL"
    Else
        Print "SITE: ";
        Color 15
        Print gameStadium$
    End If

    Color 14
    Print "ATTENDANCE: ";
    Color 15
    Print Using "###,###"; actualAttendance&
    Print

    Call ScoreSummary

    If Not (autoPlay = 1 And playerMode = 2) Then
        Locate 24, 27
        Color 14
        Print "PRESS ANY KEY TO CONTINUE";
        I$ = GetKeyPress$
    End If

    Call STATS2SCREEN

End Sub


'----------------------------------------
'        ScoreSummary Subroutine
'----------------------------------------
'This subroutine prints a score by period
'for each team.
Sub ScoreSummary

    Locate 5, 20
    Color 14

    'quarter has been increased prior to this call
    'So quarter = 3 represents halftime

    If quarter = 3 Then
        Print "Q1 Q2 ";
    Else
        Print "Q1 Q2 Q3 Q4 ";

        If quarter = 6 Then
            Print "OT ";
        Else
            If quarter > 6 Then
                For X = 5 To quarter
                    'OT1, OT2, OT3
                    Print Using "OT# "; (X - 4);
                Next X
            End If
        End If

    End If

    Print "- GAME"

    For I = 0 To 1:

        Color teamRat_GAME(I, 2), teamRat_GAME(I, 1)
        Print gameTeams$(I);
        Color 15, 0
        Locate , 20

        If quarter = 3 Then
            Print Using "## ## "; score(I, 1), score(I, 2);
        Else

            Print Using "## ## ## ## "; score(I, 1), score(I, 2), score(I, 3), score(I, 4);

            If quarter > 5 Then
                For I1 = 5 To quarter
                    Print Using "## "; score(I, I1);
                Next
            End If

        End If

        Print "- ";

        Print Using "###"; score(I, 0)

    Next I

    Print
    Color 15, 0

    scoreLines = 0
    maxScoreLines = 13 'for page 1

    If nbrScores <> 0 Then

        For I = 1 To nbrScores

            Locate , 1
            Print gameTeams$(K1(I, 1)); "-";

            TI = scoreTimes(I)
            TI = TI + TI * (TI < 0)
            IR% = Int(TI / 60)
            J = TI - IR% * 60

            Locate , 17
            Print Using "##.##"; IR% + J / 100;
            Locate , 19: Print ":";

            Locate , 23

            If scoreQuarters(I) = 1 Then Print "OF 1ST -";
            If scoreQuarters(I) = 2 Then Print "OF 2ND -";
            If scoreQuarters(I) = 3 Then Print "OF 3RD -";
            If scoreQuarters(I) = 4 Then Print "OF 4TH -";
            If scoreQuarters(I) = 5 Then Print "OF OVERTIME -";

            If K1(I, 0) <> 7 Then Print Int(K1(I, 5) + .5);

            tryPAT = 1

            Select Case K1(I, 0)

                Case 1
                    Print "RUN BY "; rbacks$(K1(I, 1), K1(I, 2))

                Case 2
                    Print "PASS BY "; qbacks$(K1(I, 1), K1(I, 2)); " TO ";: If K1(I, 3) > 9 Then Print wdRec$(K1(I, 1), K1(I, 3) - 10)
                    If K1(I, 3) <= 9 Then Print rbacks$(K1(I, 1), K1(I, 3))

                Case 3
                    Print "K.O. RETURN BY "; kickRet$(K1(I, 1), K1(I, 2))

                Case 4
                    Print "PUNT RETURN BY "; pret$(K1(I, 1), K1(I, 2))

                Case 5
                    Print "INTERCEPTION RETURN BY "; defInts$(K1(I, 1), K1(I, 2))

                Case 6
                    Print "BLOCKED KICK RECOVERED IN END ZONE"

                Case 7
                    Print "SAFETY"
                    tryPAT = 0

                Case 8
                    Print "FG BY "; kicker$(K1(I, 1), K1(I, 2))
                    tryPAT = 0

                Case 9
                    Print "RUN BY "; qbacks$(K1(I, 1), K1(I, 2))

                Case 10
                    Print "FUMBLE RETURN "

                Case 11
                    Print "FAKE PUNT ", K1(I, 2)

                Case 12
                    Print "2-PT DEFENSE RETURN BY "; defInts$(K1(I, 1), K1(I, 2))
                    tryPAT = 0

            End Select

            scoreLines = scoreLines + 1

            If tryPAT = 1 Then

                Locate , 17
                If K1(I, 4) = 10 Then Print " (KICK FAILED)"
                If K1(I, 4) = 11 Then Print " (KICK BY "; kicker$(K1(I, 1), K1(I, 6));: Print ")"
                If K1(I, 4) = 20 Then Print " (RUN FAILED)"
                If K1(I, 4) = 21 Then Print " (RUN GOOD)"
                If K1(I, 4) = 30 Then Print " (PASS FAILED)"
                If K1(I, 4) = 31 Then Print " (PASS GOOD)"
                scoreLines = scoreLines + 1

            End If

            If Not (autoPlay = 1 And playerMode = 2) And scoreLines >= maxScoreLines Then
                Locate 24, 20
                Color 14
                Print "PRESS ANY KEY FOR MORE SCORE INFORMATION";
                Call GetKeyWhenNoDelay

                Cls
                Locate 2, 27
                Print "SCORE SUMMARY (CONTINUED)"
                Color 15
                Locate 4, 1

                'We can show more lines of scores on pages 2/3
                scoreLines = 0
                maxScoreLines = 18
            End If

        Next I

    End If

End Sub


'----------------------------------------
'        STATS2SCREEN Subroutine
'----------------------------------------
'This subroutine formats the game stats
'for output to the end user display
Sub STATS2SCREEN ()

    Color , 1
    Cls

    Call OPTIONBOX(15)
    Locate 24, 27: Print "PRESS ANY KEY TO CONTINUE";

    Locate 10, 32

    Print "TEAM STATISTICS"

    Locate , 15
    Color teamRat_GAME(0, 2), teamRat_GAME(0, 1)
    Print gameTeams$(0);

    Color 0, 7
    Locate , 50
    Color teamRat_GAME(1, 2), teamRat_GAME(1, 1)
    Print gameTeams$(1)

    Color 0, 7
    Locate , 20: Print gameStatsTeam(0, 0);
    Locate , 34: Print "FIRST DOWNS";
    Locate , 55: Print gameStatsTeam(1, 0)

    If ruleOptType = 0 Then
        Locate , 18: Print gameStatsTeam(0, 1); "-"; Int(gameStatsTeam(0, 2) + .5);
        Locate , 34: Print "RUSHING YDS";
        Locate , 53: Print gameStatsTeam(1, 1); "-"; Int(gameStatsTeam(1, 2) + .5)
    Else
        Locate , 18: Print gameStatsTeam(0, 1) - sackStatsTeam(0, 0); "-"; Int(gameStatsTeam(0, 2) + sackStatsTeam(0, 1) + .5);
        Locate , 34: Print "RUSHING YDS";
        Locate , 53: Print gameStatsTeam(1, 1) - sackStatsTeam(1, 0); "-"; Int(gameStatsTeam(1, 2) + sackStatsTeam(1, 1) + .5)
    End If

    Locate , 20: Print Int(gameStatsTeam(0, 6) + .5);
    Locate , 34: Print "PASSING YDS";
    Locate , 55: Print Int(gameStatsTeam(1, 6) + .5)

    Locate , 35: Print "TOTAL YDS";
    Locate , 20: Print Int(gameStatsTeam(0, 6) + .5) + Int(gameStatsTeam(0, 2) + .5);
    Locate , 55: Print Int(gameStatsTeam(1, 6) + .5) + Int(gameStatsTeam(1, 2) + .5)

    Locate , 37: Print "PASSES";
    Locate , 16: Print gameStatsTeam(0, 4); "-"; gameStatsTeam(0, 3); "-"; gameStatsTeam(0, 5);
    Locate , 51: Print gameStatsTeam(1, 4); "-"; gameStatsTeam(1, 3); "-"; gameStatsTeam(1, 5)

    If gameStatsTeam(0, 7) <> 0 And gameStatsTeam(1, 7) <> 0 Then
        Locate , 38: Print "PUNTS";
        Locate , 15: Print gameStatsTeam(0, 7); "-"; Int((gameStatsTeam(0, 8) / gameStatsTeam(0, 7)) * 10 + .5) / 10;
        Locate , 50: Print gameStatsTeam(1, 7); "-"; Int((gameStatsTeam(1, 8) / gameStatsTeam(1, 7)) * 10 + .5) / 10
    End If

    Locate , 36: Print "FUM/LOST";
    Locate , 18: Print gameStatsTeam(0, 9); "-"; gameStatsTeam(0, 10);
    Locate , 53: Print gameStatsTeam(1, 9); "-"; gameStatsTeam(1, 10)

    Locate , 35: Print "PENALTIES";
    Locate , 18: Print gameStatsTeam(0, 11); "-"; Int(gameStatsTeam(0, 12) + .5);
    Locate , 53: Print gameStatsTeam(1, 11); "-"; Int(gameStatsTeam(1, 12) + .5)

    Locate , 37: Print "SACKED";
    Locate , 18: Print sackStatsTeam(0, 0); "-"; Int(sackStatsTeam(0, 1) + .5);
    Locate , 53: Print sackStatsTeam(1, 0); "-"; Int(sackStatsTeam(1, 1) + .5)

    Locate , 18: Print Using "##"; Int(timePoss(0) / 60);
    Print ":";
    Print Using "##"; timePoss(0) - Int(timePoss(0) / 60) * 60;
    Locate , 31: Print "TIME OF POSSESSION";
    Locate , 53: Print Using "##"; Int(timePoss(1) / 60);
    Print ":";
    Print Using "##"; timePoss(1) - Int(timePoss(1) / 60) * 60

    Locate , 32: Print "THIRD DOWN EFF";
    Locate , 16: Print thirdDownAtt(0) - thirdDownFail(0); "-"; thirdDownAtt(0);
    Locate , 51: Print thirdDownAtt(1) - thirdDownFail(1); "-"; thirdDownAtt(1)

    Call GetKeyWhenNoDelay

    Call FRAMESTATS(22)
    Locate 24, 27
    Print "PRESS ANY KEY TO CONTINUE";

    Locate 2, 32
    Print "RUSHING STATISTICS"
    Print
    Call ADDBACKS

    For I = 0 To 1

        Locate , 20
        Color teamRat_GAME(I, 2), teamRat_GAME(I, 1): Print gameTeams$(I);
        Color 0, 7
        Locate , 36
        Print "TC   YDS   AVG   LG   TD"

        For I1 = 0 To 13

            If gameStatsPlayer(I, I1, 0) <> 0 Then

                Locate , 20
                If I1 <= 9 Then
                    Print rbacks$(I, I1);
                Else
                    Print qbacks$(I, I1 - 10);
                End If

                Locate , 35: Print Using "###  #### ###.#  "; gameStatsPlayer(I, I1, 0), Int(gameStatsPlayer(I, I1, 1) + .5), Int(gameStatsPlayer(I, I1, 1) + .5) / gameStatsPlayer(I, I1, 0);

                If I1 <= 9 Then
                    Print Using "###   ##"; runBacks(I, I1), gameStatsPlayer(I, I1, 2)
                Else
                    Print Using "      ##"; gameStatsPlayer(I, I1, 2)
                End If

            End If

        Next I1

        Print

    Next I

    Call GetKeyWhenNoDelay

    Call FRAMESTATS(22)
    Locate 24, 27
    Print "PRESS ANY KEY TO CONTINUE";

    Locate 2, 32
    Print "RECEIVING STATISTICS"
    Print
    nbrPlayers = 0

    For I = 0 To 1

        Locate , 18
        Color teamRat_GAME(I, 2), teamRat_GAME(I, 1): Print gameTeams$(I);: Color 0, 7
        Locate , 36: Print "TC    YDS    AVG   LG    TD"

        For I1 = 0 To 5

            If gameStatsPlayer(I, I1, 6) <> 0 Then
                Locate , 18
                Print wdRec$(I, I1);
                Locate , 36: Print Using "##    ###   ##.#  ###    ##"; gameStatsPlayer(I, I1, 6), Int(gameStatsPlayer(I, I1, 7) + .5), Int(gameStatsPlayer(I, I1, 7) + .5) / gameStatsPlayer(I, I1, 6), WR(I, I1 + 10), gameStatsPlayer(I, I1, 8)
                nbrPlayers = nbrPlayers + 1
            End If

            If (nbrPlayers > 0 And nbrPlayers Mod 16 = 0) Then
                'Create a 2nd page if there are enough players
                'to spam out past the page
                Call GetKeyWhenNoDelay

                Call FRAMESTATS(22)
                Locate 24, 27
                Print "PRESS ANY KEY TO CONTINUE";

                Locate 2, 32
                Print "RECEIVING STATISTICS"
                Print

                Locate , 18
                Color teamRat_GAME(I, 2), teamRat_GAME(I, 1): Print gameTeams$(I);
                Color 0, 7
                Locate , 36: Print "TC    YDS    AVG   LG    TD"

            End If

        Next I1

        For I1 = 0 To 9

            If gameStatsPlayer(I, I1, 3) <> 0 Then
                Locate , 18
                Print rbacks$(I, I1);
                Locate , 36: Print Using "##    ###   ##.#  ###    ##"; gameStatsPlayer(I, I1, 3), Int(gameStatsPlayer(I, I1, 4) + .5), Int(gameStatsPlayer(I, I1, 4) + .5) / gameStatsPlayer(I, I1, 3), WR(I, I1), gameStatsPlayer(I, I1, 5)
                nbrPlayers = nbrPlayers + 1
            End If

            If (nbrPlayers > 0 And nbrPlayers Mod 16 = 0) Then
                'Create a 2nd page if there are enough players
                'to spam out past the page
                Call GetKeyWhenNoDelay

                Call FRAMESTATS(22)
                Locate 24, 27: Print "PRESS ANY KEY TO CONTINUE";

                Locate 2, 32: Print "RECEIVING STATISTICS"
                Print

                Locate , 18
                Color teamRat_GAME(I, 2), teamRat_GAME(I, 1): Print gameTeams$(I);
                Color 0, 7
                Locate , 36: Print "TC    YDS    AVG   LG    TD"

            End If

        Next I1

        Print

    Next I

    Call GetKeyWhenNoDelay

    Call FRAMESTATS(22)
    Locate 24, 27
    Print "PRESS ANY KEY TO CONTINUE";

    Locate 2, 32
    Print "PASSING STATISTICS"
    Print

    For I = 0 To 1
        Locate , 6
        Color teamRat_GAME(I, 2), teamRat_GAME(I, 1): Print gameTeams$(I);
        Color 0, 7
        Locate , 22: Print "ATT  COM COM %  YDS  Y/A  Y/C  TD  TD%  I    I%  LG"

        For I1 = 0 To 3

            If gameStatsPlayer(I, I1, 9) <> 0 Then

                Locate , 6
                Print qbacks$(I, I1);
                Locate , 22: Print Using "###  ### ###.#  ### ##.# "; gameStatsPlayer(I, I1, 9), gameStatsPlayer(I, I1, 10), gameStatsPlayer(I, I1, 10) / gameStatsPlayer(I, I1, 9) * 100, Int(gameStatsPlayer(I, I1, 12) + .5), Int(gameStatsPlayer(I, I1, 12) + .5) / gameStatsPlayer(I, I1, 9);

                If gameStatsPlayer(I, I1, 10) <> 0 Then
                    'Y/C, TD
                    Print Using "##.#  ##"; Int(gameStatsPlayer(I, I1, 12) + .5) / gameStatsPlayer(I, I1, 10), gameStatsPlayer(I, I1, 13);
                Else
                    Print Using "      ##"; gameStatsPlayer(I, I1, 13);
                End If

                '             TD%, I, I%,   LG
                Print Using "###.# ## ###.# ###"; gameStatsPlayer(I, I1, 13) / gameStatsPlayer(I, I1, 9) * 100, gameStatsPlayer(I, I1, 11), gameStatsPlayer(I, I1, 11) / gameStatsPlayer(I, I1, 9) * 100, quarterbacks(I, I1)

            End If

        Next I1

        Print

    Next I

    Call GetKeyWhenNoDelay

    Call FRAMESTATS(22)
    Locate 24, 27
    Print "PRESS ANY KEY TO CONTINUE";

    Locate 2, 25
    Print "KICKOFF RETURN STATISTICS"
    Print

    For I = 0 To 1
        Locate , 20
        Color teamRat_GAME(I, 2), teamRat_GAME(I, 1): Print gameTeams$(I);
        Color 0, 7
        Locate , 36: Print "KR   YDS   AVG   LG  TD"

        For I1 = 0 To 2

            If gameStatsPlayer(I, I1, 14) <> 0 Then
                Locate , 20
                Print kickRet$(I, I1);
                Locate , 36
                Print Using "##  ####  ##.#  ###  ##"; gameStatsPlayer(I, I1, 14), Int(gameStatsPlayer(I, I1, 15) + .5), Int(gameStatsPlayer(I, I1, 15) + .5) / gameStatsPlayer(I, I1, 14), kickReturners(I, I1), gameStatsPlayer(I, I1, 16)

            End If

        Next I1

        Print
    Next I

    Print
    Locate , 25
    Print "PUNT RETURN STATISTICS"
    Print

    For I = 0 To 1
        Locate , 20
        Color teamRat_GAME(I, 2), teamRat_GAME(I, 1): Print gameTeams$(I);
        Color 0, 7
        Locate , 36: Print "PR   YDS   AVG   LG  TD"

        For I1 = 3 To 5
            If gameStatsPlayer(I, I1, 14) <> 0 Then
                Locate , 20
                Print pret$(I, I1 - 3);
                Locate , 36
                Print Using "##  ####  ##.#  ###  ##"; gameStatsPlayer(I, I1, 14), Int(gameStatsPlayer(I, I1, 15) + .5), Int(gameStatsPlayer(I, I1, 15) + .5) / gameStatsPlayer(I, I1, 14), puntReturners(I, I1 - 3), gameStatsPlayer(I, I1, 16)
            End If
        Next I1

        Print
    Next I

    Call GetKeyWhenNoDelay

    Call FRAMESTATS(22)
    Locate 24, 27
    Print "PRESS ANY KEY TO CONTINUE";

    Locate 2, 29
    Print "PUNTING STATISTICS"
    Print

    For I = 0 To 1
        Locate , 10
        Color teamRat_GAME(I, 2), teamRat_GAME(I, 1)
        Print gameTeams$(I);: Color 0, 7
        Locate , 26: Print "PNT   AVG  LG  TB  BLK  20"

        If gameStatsTeam(I, 7) <> 0 Then
            Locate , 10
            Print punter$(I, 0);
            Locate , 26
            Print Using "###  ##.#  ##  ##   ##  ##  ##  ##.#  ##.#"; gameStatsTeam(I, 7), gameStatsTeam(I, 8) / gameStatsTeam(I, 7), gameStatsTeam(I, 26), gameStatsTeam(I, 27), gameStatsTeam(I, 28), gameStatsTeam(I, 29)
        End If

        Print
    Next I

    Print
    Locate , 29
    Print "KICKING STATISTICS"
    Print

    For I = 0 To 1

        Locate , 3
        Color teamRat_GAME(I, 2), teamRat_GAME(I, 1): Print gameTeams$(I);: Color 0, 7
        Locate , 18: Print "EPM EPA   EP% FGM FGA   FG% LG  0-19 20-29 30-39 40-49  50+"

        If gameStatsPlayer(I, 4, 17) <> 0 Or gameStatsPlayer(I, 6, 17) <> 0 Then
            Locate , 3
            Print kicker$(I, 0);
            Locate , 18

            If gameStatsPlayer(I, 6, 17) <> 0 Then
                Print Using "### ### ###.# "; gameStatsPlayer(I, 5, 17), gameStatsPlayer(I, 6, 17), gameStatsPlayer(I, 5, 17) / gameStatsPlayer(I, 6, 17) * 100;
            End If

            If gameStatsPlayer(I, 4, 17) = 0 Then
                Print
            Else
                Locate , 32
                Print Using "### ### ###.# ##  #"; gameStatsPlayer(I, 3, 17), gameStatsPlayer(I, 4, 17), gameStatsPlayer(I, 3, 17) / gameStatsPlayer(I, 4, 17) * 100, gameLongFG(I, 0), FM(I, 0, 0);
                Print "- ";: Print Using "#  #"; FA(I, 0, 0); FM(I, 0, 1);
                Print "- ";: Print Using "#  #"; FA(I, 0, 1); FM(I, 0, 2);
                Print "- ";: Print Using "#  #"; FA(I, 0, 2); FM(I, 0, 3);
                Print "- ";: Print Using "#  #"; FA(I, 0, 3); FM(I, 0, 4);
                Print "- ";: Print Using "#"; FA(I, 0, 4)
            End If

        End If

        If gameStatsPlayer(I, 8, 17) <> 0 Or gameStatsPlayer(I, 10, 17) <> 0 Then

            Locate , 3
            Print kicker$(I, 1);
            Locate , 18

            If gameStatsPlayer(I, 10, 17) <> 0 Then
                Print Using "### ### ###.# "; gameStatsPlayer(I, 9, 17), gameStatsPlayer(I, 10, 17), gameStatsPlayer(I, 9, 17) / gameStatsPlayer(I, 10, 17) * 100;
            End If

            If gameStatsPlayer(I, 8, 17) = 0 Then
                Print
            Else

                Locate , 32
                Print Using "### ### ###.# ##  #"; gameStatsPlayer(I, 7, 17), gameStatsPlayer(I, 8, 17), gameStatsPlayer(I, 7, 17) / gameStatsPlayer(I, 8, 17) * 100, gameLongFG(I, 1), FM(I, 1, 0);
                Print "- ";: Print Using "#  #"; FA(I, 1, 0); FM(I, 1, 1);
                Print "- ";: Print Using "#  #"; FA(I, 1, 1); FM(I, 1, 2);
                Print "- ";: Print Using "#  #"; FA(I, 1, 2); FM(I, 1, 3);
                Print "- ";: Print Using "#  #"; FA(I, 1, 3); FM(I, 1, 4);
                Print "- ";: Print Using "#"; FA(I, 1, 4)

            End If

        End If

        Print

    Next I

    Call GetKeyWhenNoDelay

    Call FRAMESTATS(22)
    Locate 24, 27
    Print "PRESS ANY KEY TO CONTINUE";

    Locate 2, 35
    Print "INTERCEPTIONS"
    Print

    For I = 0 To 1:
        Locate , 20
        Color teamRat_GAME(I, 2), teamRat_GAME(I, 1): Print gameTeams$(I);
        Color 0, 7
        Locate , 36
        Print "IR   YDS   AVG   LG  TD"

        For I1 = 0 To 9
            If gameInts(I, I1, 0) <> 0 Then
                Locate , 20: Print defInts$(I, I1);
                Locate , 36: Print Using "##  ####  ##.#  ###  ##"; gameInts(I, I1, 0), Int(gameInts(I, I1, 1) + .5), Int(gameInts(I, I1, 1) + .5) / gameInts(I, I1, 0), IR(I, I1), gameInts(I, I1, 2)
            End If
        Next I1

        Locate , 1: Print

    Next I

    Call GetKeyWhenNoDelay

    Call FRAMESTATS(22)
    Locate 24, 27
    Print "PRESS ANY KEY TO CONTINUE";

    Locate 2, 39
    Print "SACKS"
    Print

    For I = 0 To 1
        Locate , 25
        Color teamRat_GAME(I, 2), teamRat_GAME(I, 1): Print gameTeams$(I);: Color 0, 7

        Locate , 41
        Print "NO.   YDS LOST"

        For I1 = 0 To 14
            If totalSacks(I, I1, 0) <> 0 Then
                Locate , 25: Print defSacks$(I, I1);
                Locate , 41: Print Using " ##       ##"; totalSacks(I, I1, 0), Int(totalSacks(I, I1, 1) + .5)
            End If
        Next I1

        Locate , 1
        Print

    Next I

    Call GetKeyWhenNoDelay

End Sub


'----------------------------------------
'          SAVESTATS Subroutine
'----------------------------------------
'This subroutine prompts the user in ORDER
'to determine which teams to save satts For
'(autoplay will always save both teams).
'It also updates the GAME82 progress file,
'since at this point only can the game be
'considered fully complete.
Sub SAVESTATS ()

    Shared scheduleFile$

    Cls

    If autoPlay <> 1 Then

        Do
            Call TEAMBOX(1, 1)
            pbpString$ = "SAVE STATISTICS? (Y/N)"
            Locate , (80 - Len(pbpString$)) / 2
            Print pbpString$
            Color 15, 0

            I$ = GetKeyPress$
        Loop Until UCase$(I$) = "Y" Or UCase$(I$) = "N"

    End If

    If UCase$(I$) = "Y" Or autoPlay = 1 Then

        PX = 0

        Cls

        ' Update schedule, ticker, etc.
        If autoPlay = 1 Then

            Call TickerSchedule

            If schedGame(1) < schedGame(0) + schedGame(2) Then
                Kill "GAME82"
                endAllGames = 1
            Else

                Open "GAME82" For Output As #1

                Print #1, scheduleFile$

                For I = 0 To 2:
                    Print #1, schedGame(I)
                Next

                Close #1

            End If

        End If

        Cls

        periodNbr = quarter

        For idx = 0 To 1

            skipCompile% = 0

            Color 15, 0
            Cls

            Call FRAMEOPTION(1, idx)

            pbpString$ = "COMPILE STATS FOR " + gameTeams$(idx) + "? (Y/N)"
            Locate , (80 - Len(pbpString$)) / 2
            Print pbpString$
            Color 15, 0

            If autoPlay <> 1 Then

                Do
                    I$ = GetKeyPress$
                Loop Until UCase$(I$) = "Y" Or UCase$(I$) = "N"

                If I$ = "N" Then skipCompile% = 1

            End If

            If skipCompile% <> 1 Then

                Call COMPILESTATFILES(idx)

            End If

        Next idx

    End If

End Sub


'----------------------------------------
'      COMPILESTATFILES Subroutine
'----------------------------------------
'This subroutine updates the various stat
'files for the corresponding team based on
'the stats generated for a completed game.
Sub COMPILESTATFILES (teamIdx)

    Shared actualAttendance&

    Shared oppScore(), powerRating()
    Shared teamScore()

    Shared gameAttendance&()

    Shared gamePK$(), oppName$()
    Shared gameSite$()
    Shared locIndicator$()
    Shared tradeLF$(), tradeLI$(), tradeLK$(), tradeLP$()

    'Determine stat file name
    statFile$ = nameStatFile$(gameTeams$(teamIdx))

    'Read existing stats from the file
    If _FileExists(diskPaths$(1) + statFile$) Then

        Open diskPaths$(1) + statFile$ For Input As #1

        Input #1, gameTeams$(teamIdx)

        For I = 0 To 9:
            Input #1, rbacks$(teamIdx, I)
        Next

        For I = 0 To 5:
            Input #1, wdRec$(teamIdx, I)
        Next

        For I = 0 To 3:
            Input #1, qbacks$(teamIdx, I)
        Next

        For I = 0 To 2:
            Input #1, kickRet$(teamIdx, I)
        Next

        For I = 0 To 2:
            Input #1, pret$(teamIdx, I)
        Next

        Input #1, punter$(teamIdx, 0)

        For I = 0 To 1:
            Input #1, kicker$(teamIdx, I)
        Next

        For J = 0 To 13:
            For K = 0 To 17:
                Input #1, gameZ2(J, K)
            Next
        Next

        For L = 0 To 38:
            Input #1, gameZ(L)
        Next

        For L = 0 To 38:
            Input #1, gameZ1(L)
        Next

        For J = 0 To 9
            Input #1, defInts$(teamIdx, J)
            For J1 = 0 To 2
                Input #1, totalInts(teamIdx, J, J1)
            Next
        Next

        For J = 0 To 14
            Input #1, defSacks$(teamIdx, J)
            For J1 = 0 To 1
                Input #1, totalSacks(teamIdx, J, J1)
            Next
        Next

        For I = 0 To 9:
            Input #1, LR(teamIdx, I)
            Input #1, gameLR$(teamIdx, I, 0)
            Input #1, gameLR$(teamIdx, I, 1)
        Next

        For I = 0 To 15:
            Input #1, LC(teamIdx, I)
            Input #1, gameLC$(teamIdx, I, 0)
            Input #1, gameLC$(teamIdx, I, 1)
        Next

        For I = 0 To 3:
            Input #1, LP(teamIdx, I)
            Input #1, tradeLP$(teamIdx, I, 0)
            Input #1, tradeLP$(teamIdx, I, 1)
        Next

        For I = 0 To 2:
            Input #1, LK(teamIdx, I)
            Input #1, tradeLK$(teamIdx, I, 0)
            Input #1, tradeLK$(teamIdx, I, 1)
        Next

        For I = 0 To 2:
            Input #1, PK(teamIdx, I)
            Input #1, gamePK$(teamIdx, I, 0)
            Input #1, gamePK$(teamIdx, I, 1)
        Next

        For I = 0 To 1:
            Input #1, LF(teamIdx, I)
            Input #1, tradeLF$(teamIdx, I)
            For I1 = 0 To 4:
                Input #1, AM(teamIdx, I, I1)
                Input #1, AF(teamIdx, I, I1)
            Next
        Next

        For I = 0 To 9:
            Input #1, LI(teamIdx, I)
            Input #1, tradeLI$(teamIdx, I, 0)
            Input #1, tradeLI$(teamIdx, I, 1)
        Next

        Close #1

    End If

    For I = 0 To 9
        If runBacks(teamIdx, I) > LR(teamIdx, I) Then
            LR(teamIdx, I) = runBacks(teamIdx, I)
            gameLR$(teamIdx, I, 0) = gameRB$(teamIdx, I)
            gameLR$(teamIdx, I, 1) = gameTeams$(1 - teamIdx)
        End If

        If IR(teamIdx, I) > LI(teamIdx, I) Then
            LI(teamIdx, I) = IR(teamIdx, I)
            tradeLI$(teamIdx, I, 0) = gameIR$(teamIdx, I)
            tradeLI$(teamIdx, I, 1) = gameTeams$(1 - teamIdx)
        End If
    Next

    For I = 0 To 15
        If WR(teamIdx, I) > LC(teamIdx, I) Then
            LC(teamIdx, I) = WR(teamIdx, I)
            gameLC$(teamIdx, I, 0) = gameWR$(teamIdx, I)
            gameLC$(teamIdx, I, 1) = gameTeams$(1 - teamIdx)
        End If
    Next

    For I = 0 To 3
        If quarterbacks(teamIdx, I) > LP(teamIdx, I) Then
            LP(teamIdx, I) = quarterbacks(teamIdx, I)
            tradeLP$(teamIdx, I, 0) = gameQB$(teamIdx, I)
            tradeLP$(teamIdx, I, 1) = gameTeams$(1 - teamIdx)
        End If
    Next

    For I = 0 To 2
        If kickReturners(teamIdx, I) > LK(teamIdx, I) Then
            LK(teamIdx, I) = kickReturners(teamIdx, I)
            tradeLK$(teamIdx, I, 0) = gameKR$(teamIdx, I)
            tradeLK$(teamIdx, I, 1) = gameTeams$(1 - teamIdx)
        End If

        If puntReturners(teamIdx, I) > PK(teamIdx, I) Then
            PK(teamIdx, I) = puntReturners(teamIdx, I)
            gamePK$(teamIdx, I, 0) = gamePR$(teamIdx, I)
            gamePK$(teamIdx, I, 1) = gameTeams$(1 - teamIdx)
        End If
    Next

    If gameStatsTeam(teamIdx, 26) > gameZ(28) Then gameZ(28) = gameStatsTeam(teamIdx, 26)

    For I = 0 To 1

        If gameLongFG(teamIdx, I) > LF(teamIdx, I) Then
            LF(teamIdx, I) = gameLongFG(teamIdx, I)
            tradeLF$(teamIdx, 1) = gameTeams$(1 - teamIdx)
        End If

        For I1 = 0 To 4
            AM(teamIdx, I, I1) = AM(teamIdx, I, I1) + FM(teamIdx, I, I1)
            AF(teamIdx, I, I1) = AF(teamIdx, I, I1) + FA(teamIdx, I, I1)
        Next

    Next

    For J = 0 To 13
        For K = 0 To 17:
            gameZ2(J, K) = gameZ2(J, K) + Int(gameStatsPlayer(teamIdx, J, K) + .5)
        Next
    Next

    gameZ(32) = gameZ(32) + score(teamIdx, 1)
    gameZ(33) = gameZ(33) + score(teamIdx, 2)
    gameZ(34) = gameZ(34) + score(teamIdx, 3)
    gameZ(35) = gameZ(35) + score(teamIdx, 4)

    For K = 5 To periodNbr
        gameZ(36) = gameZ(36) + score(teamIdx, K)
    Next

    gameZ1(32) = gameZ1(32) + score(1 - teamIdx, 1)
    gameZ1(33) = gameZ1(33) + score(1 - teamIdx, 2)
    gameZ1(34) = gameZ1(34) + score(1 - teamIdx, 3)
    gameZ1(35) = gameZ1(35) + score(1 - teamIdx, 4)

    For K = 5 To periodNbr
        gameZ1(36) = gameZ1(36) + score(1 - teamIdx, K)
    Next

    For J = 0 To 3
        gameZ(17) = gameZ(17) + gameStatsPlayer(teamIdx, J, 13)
        gameZ1(17) = gameZ1(17) + gameStatsPlayer(1 - teamIdx, J, 13)
    Next

    For J = 0 To 13
        gameZ(15) = gameZ(15) + gameStatsPlayer(teamIdx, J, 2)
        gameZ1(15) = gameZ1(15) + gameStatsPlayer(1 - teamIdx, J, 2)
    Next

    For J = 0 To 2
        gameZ(18) = gameZ(18) + gameStatsPlayer(teamIdx, J, 16)
        gameZ1(18) = gameZ1(18) + gameStatsPlayer(1 - teamIdx, J, 16)
        gameZ(22) = gameZ(22) + gameStatsPlayer(teamIdx, J, 14)
        gameZ1(22) = gameZ1(22) + gameStatsPlayer(1 - teamIdx, J, 14)
        gameZ(23) = gameZ(23) + gameStatsPlayer(teamIdx, J, 15)
        gameZ1(23) = gameZ1(23) + gameStatsPlayer(1 - teamIdx, J, 15)
        gameZ(19) = gameZ(19) + gameStatsPlayer(teamIdx, J + 3, 16)
        gameZ1(18) = gameZ1(19) + gameStatsPlayer(1 - teamIdx, J + 3, 16)
        gameZ(24) = gameZ(24) + gameStatsPlayer(teamIdx, J + 3, 14)
        gameZ1(24) = gameZ1(24) + gameStatsPlayer(1 - teamIdx, J + 3, 14)
        gameZ(25) = gameZ(25) + gameStatsPlayer(teamIdx, J + 3, 15)
        gameZ1(25) = gameZ1(25) + gameStatsPlayer(1 - teamIdx, J + 3, 15)
    Next

    For J = 0 To 9
        gameZ(20) = gameZ(20) + gameInts(teamIdx, J, 2)
        gameZ1(20) = gameZ1(20) + gameInts(1 - teamIdx, J, 2)
        gameZ(26) = gameZ(26) + gameInts(teamIdx, J, 0)
        gameZ1(26) = gameZ1(26) + gameInts(1 - teamIdx, J, 0)
        gameZ(27) = gameZ(27) + gameInts(teamIdx, J, 1)
        gameZ1(27) = gameZ1(27) + gameInts(1 - teamIdx, J, 1)
    Next

    gameZ(21) = gameZ(21) + gameStatsTeam(teamIdx, 19)
    gameZ1(21) = gameZ1(21) + gameStatsTeam(1 - teamIdx, 19)
    gameZ(37) = gameZ(37) + gameStatsTeam(teamIdx, 35)
    gameZ1(37) = gameZ1(37) + gameStatsTeam(1 - teamIdx, 35)
    gameZ(38) = gameZ(38) + gameStatsTeam(teamIdx, 36)
    gameZ1(38) = gameZ1(38) + gameStatsTeam(1 - teamIdx, 36)
    gameZ(30) = gameZ(30) + gameStatsTeam(teamIdx, 28)
    gameZ1(30) = gameZ1(30) + gameStatsTeam(1 - teamIdx, 28)
    gameZ(29) = gameZ(29) + gameStatsTeam(teamIdx, 27)
    gameZ1(29) = gameZ1(29) + gameStatsTeam(1 - teamIdx, 27)
    gameZ(31) = gameZ(31) + gameStatsTeam(teamIdx, 29)
    gameZ1(31) = gameZ1(31) + gameStatsTeam(1 - teamIdx, 29)

    For L = 0 To 12:
        gameZ(L) = gameZ(L) + gameStatsTeam(teamIdx, L)
    Next

    For L = 0 To 12:
        gameZ1(L) = gameZ1(L) + gameStatsTeam(1 - teamIdx, L)
    Next

    gameZ(13) = gameZ(13) + sackStatsTeam(teamIdx, 0)
    gameZ(14) = gameZ(14) + sackStatsTeam(teamIdx, 1)
    gameZ1(13) = gameZ1(13) + sackStatsTeam(1 - teamIdx, 0)
    gameZ1(14) = gameZ1(14) + sackStatsTeam(1 - teamIdx, 1)

    For J = 0 To 9
        For J1 = 0 To 2
            totalInts(teamIdx, J, J1) = totalInts(teamIdx, J, J1) + gameInts(teamIdx, J, J1)
        Next
    Next

    For J = 0 To 14
        For J1 = 0 To 1
            totalSacks(teamIdx, J, J1) = totalSacks(teamIdx, J, J1) + totalSacks(teamIdx, J, J1)
        Next
    Next

    Cls

    Print ">>>>>"
    Print " THE STATS ARE BEING SAVED UNDER"
    Print "THE FOLLOWING NAME::: ";
    Color teamRat_GAME(teamIdx, 2), teamRat_GAME(teamIdx, 1): Print statFile$
    Color 15, 0

    Open diskPaths$(1) + statFile$ For Output As #2

    Print #2, gameTeams$(teamIdx)

    For I = 0 To 9:
        Print #2, rbacks$(teamIdx, I)
    Next

    For I = 0 To 5:
        Print #2, wdRec$(teamIdx, I)
    Next

    For I = 0 To 3:
        Print #2, qbacks$(teamIdx, I)
    Next

    For I = 0 To 2:
        Print #2, kickRet$(teamIdx, I)
    Next

    For I = 0 To 2:
        Print #2, pret$(teamIdx, I)
    Next

    Print #2, punter$(teamIdx, 0)

    For I = 0 To 1:
        Print #2, kicker$(teamIdx, I)
    Next

    For J = 0 To 13
        For K = 0 To 17:
            Print #2, gameZ2(J, K)
        Next
    Next

    For L = 0 To 38:
        Print #2, gameZ(L)
    Next

    For L = 0 To 38:
        Print #2, gameZ1(L)
    Next

    For J = 0 To 9
        Print #2, defInts$(teamIdx, J)
        For J1 = 0 To 2
            Print #2, totalInts(teamIdx, J, J1)
        Next
    Next

    For J = 0 To 14
        Print #2, defSacks$(teamIdx, J)
        For J1 = 0 To 1
            Print #2, totalSacks(teamIdx, J, J1)
        Next
    Next

    For I = 0 To 9:
        Print #2, LR(teamIdx, I)
        Print #2, gameLR$(teamIdx, I, 0)
        Print #2, gameLR$(teamIdx, I, 1)
    Next

    For I = 0 To 15:
        Print #2, LC(teamIdx, I)
        Print #2, gameLC$(teamIdx, I, 0)
        Print #2, gameLC$(teamIdx, I, 1)
    Next

    For I = 0 To 3:
        Print #2, LP(teamIdx, I)
        Print #2, tradeLP$(teamIdx, I, 0)
        Print #2, tradeLP$(teamIdx, I, 1)
    Next

    For I = 0 To 2:
        Print #2, LK(teamIdx, I)
        Print #2, tradeLK$(teamIdx, I, 0)
        Print #2, tradeLK$(teamIdx, I, 1)
    Next

    For I = 0 To 2:
        Print #2, PK(teamIdx, I)
        Print #2, gamePK$(teamIdx, I, 0)
        Print #2, gamePK$(teamIdx, I, 1)
    Next

    For I = 0 To 1:
        Print #2, LF(teamIdx, I)
        Print #2, tradeLF$(teamIdx, I)
        For I1 = 0 To 4:
            Print #2, AM(teamIdx, I, I1)
            Print #2, AF(teamIdx, I, I1)
        Next
    Next

    For I = 0 To 9:
        Print #2, LI(teamIdx, I)
        Print #2, tradeLI$(teamIdx, I, 0)
        Print #2, tradeLI$(teamIdx, I, 1)
    Next

    Close #2

    Print "<<<<<"
    Print
    Print

    numberGames = 0

    'Read stats in from file
    If _FileExists(diskPaths$(1) + statFile$ + "S") Then

        Open diskPaths$(1) + statFile$ + "S" For Input As #3

        Input #3, numberGames

        For I = 1 To MAX_SCHED_STATS:
            Input #3, teamScore(I)
        Next

        For I = 1 To MAX_SCHED_STATS:
            Input #3, oppName$(I)
        Next

        For I = 1 To MAX_SCHED_STATS:
            Input #3, oppScore(I)
        Next

        For I = 1 To MAX_SCHED_STATS:
            Input #3, locIndicator$(I)
        Next

        For I = 1 To MAX_SCHED_STATS:
            Input #3, powerRating(0, I)
        Next

        For I = 1 To MAX_SCHED_STATS:
            Input #3, powerRating(1, I)
        Next

        For I = 1 To MAX_SCHED_STATS:
            Input #3, gameAttendance&(I)
        Next

        For I = 1 To MAX_SCHED_STATS:
            Input #3, gameSite$(I)
        Next

        Close #3

    End If

    If numberGames < MAX_SCHED_STATS Then numberGames = numberGames + 1
    If gameLoc = teamIdx Then locIndicator$(numberGames) = "H"
    If gameLoc <> teamIdx Then locIndicator$(numberGames) = "A"
    If gameLoc = 2 Then locIndicator$(numberGames) = "X"
    If gameLoc = teamIdx And periodNbr > 4 Then locIndicator$(numberGames) = "H-" + Str$(periodNbr - 4) + " OT"
    If gameLoc <> teamIdx And periodNbr > 4 Then locIndicator$(numberGames) = "A-" + Str$(periodNbr - 4) + " OT"
    If gameLoc = 2 And periodNbr > 4 Then locIndicator$(numberGames) = "X-" + Str$(periodNbr - 4) + " OT"

    teamScore(numberGames) = score(teamIdx, 0)
    oppName$(numberGames) = gameTeams$(1 - teamIdx)
    oppScore(numberGames) = score(1 - teamIdx, 0)
    gameAttendance&(numberGames) = actualAttendance&
    gameSite$(numberGames) = gameStadium$
    powerRating(0, numberGames) = teamRat_GAME(teamIdx, 4)
    powerRating(1, numberGames) = teamRat_GAME(1 - teamIdx, 4)

    'Write stats to new file
    Open diskPaths$(1) + statFile$ + "S" For Output As #4

    Print #4, numberGames

    For I = 1 To MAX_SCHED_STATS:
        Print #4, teamScore(I)
    Next

    For I = 1 To MAX_SCHED_STATS:
        Print #4, oppName$(I)
    Next

    For I = 1 To MAX_SCHED_STATS:
        Print #4, oppScore(I)
    Next

    For I = 1 To MAX_SCHED_STATS:
        Print #4, locIndicator$(I)
    Next

    For I = 1 To MAX_SCHED_STATS:
        Print #4, powerRating(0, I)
    Next

    For I = 1 To MAX_SCHED_STATS:
        Print #4, powerRating(1, I)
    Next

    For I = 1 To MAX_SCHED_STATS:
        Print #4, gameAttendance&(I)
    Next

    For I = 1 To MAX_SCHED_STATS:
        Print #4, gameSite$(I)
    Next

    Close #4

    Erase oppScore, powerRating
    Erase teamScore
    Erase gameAttendance&
    Erase locIndicator$, oppName$, gameSite$
    Erase gameZ, gameZ1, gameZ2

End Sub


'----------------------------------------
'            Custom Function
'----------------------------------------
'It's not clear at this time what this
'function does
Function FNP (X)
    FNP = 11 + Int((X + Int(X / 5) - 1) / 2)
End Function


'----------------------------------------
'          GetTeamYear Function
'----------------------------------------
'This subroutine determines the 4-digit
'team year from the team loaded by the
'file. It uses the 2 digit year to make
'assumptions. (00 to 24 are year 2000.)
'The earliest years of pro football began
'in 1925, so years 00 to 24 are safe to
'be considered as 2000's
Function GetTeamYear% (yearFromName$)

    teamYear% = Val(yearFromName$)

    If teamYear% >= 0 And teamYear% < 25 Then
        GetTeamYear% = 2000 + teamYear%
    Else
        GetTeamYear% = 1900 + teamYear%
    End If

End Function


'----------------------------------------
'            pbplog Function
'----------------------------------------
'This subroutine is designed to help me
'print information about where the game
'is at during the code. The general idea
'was originally to be called everywhere
'there is PBP. This helps me debug where
'issues are happening in a game.
Sub pbplog (lineNumber)


    ydsToScore = 100 - ydLine
    ydLineTeam = ydsToScore - (ydsToScore > 50) * (100 - ydsToScore - ydsToScore)

    Print #11, "Called from line "; lineNumber
    Print #11, gameTeams$(0); " vs "; gameTeams$(1)
    Print #11, downDesc$(currDown); " down and"; ydsToFirst; "at the"; ydLineTeam
    Print #11, pbpString$
    Print #11,

End Sub


'----------------------------------------
'       ChooseGameTeam Subroutine
'----------------------------------------
'This subroutine provides interface for
'choosing a team for the game
Sub ChooseGameTeam (yearNumber$, numberTeams, chosenIdx, teamName$, gameIdx)

    Shared previousYear$

    Shared teamIndicator$()

    Do

        Color 15, 0
        Cls

        JJ = 7

        Locate 6, 8
        Color 15, 4

        Print " " + Chr$(214); String$(61, 196); Chr$(183) + " ";

        For II = 1 To JJ
            Locate 6 + II, 8
            Print " " + Chr$(186); String$(61, 32); Chr$(186) + " ";
        Next

        Locate 7 + JJ, 8
        Print " " + Chr$(211); String$(61, 196); Chr$(189) + " ";

        Locate 7, 32

        Locate , 12: Print teamIndicator$(gameIdx)
        Locate , 12: Print "ENTER DISK ID: "
        Print
        Locate , 12: Print "THE DISK ID IS USUALLY THE LAST TWO DIGITS OF THE"
        Locate , 12: Print "SEASON DISK YOU ARE PLAYING FROM (IE. 89, 76, 67)."
        Print
        Locate , 12: Print "HIT <ENTER> TO USE PREVIOUS ID"
        Locate 8,

        I$ = GetKeyPress$

        If I$ <> Chr$(27) Then

            If I$ <> Chr$(13) Then

                Locate 8, 27: Print I$
                Locate 8, 28: Line Input NY$
                NY$ = I$ + NY$
                Color 15, 0

                If Not (NY$ = "" And yearNumber$ <> "") Then

                    yearNumber$ = RTrim$(NY$)
                    Erase teamNames$
                    numberTeams = 0

                    Cls

                    If Not _FileExists(diskPaths$(0) + TEAM_FILE_NAME$ + "." + yearNumber$) Then

                        MsgText$ = "NO TEAM FILES WERE FOUND MATCHING THIS ID. PRESS ANY KEY TO RETURN TO MAIN MENU"
                        BoxWidth = 20
                        BoxHeight = (Len(MsgText$) / BoxWidth) + 3 'top and box lines + 1 extra
                        Locate ((25 - BoxHeight) / 2), (80 - ((BoxWidth + 2) / 2))
                        Call CSMsgBox(MsgText$, BoxWidth, Pass)
                        I$ = GetKeyPress$
                        Call CSMsgBox("", 0, Pass)
                        Exit Sub

                    Else

                        Open diskPaths$(0) + TEAM_FILE_NAME$ + "." + yearNumber$ For Random As #1 Len = TEAM_SIZE_BYTES

                        fileLength& = LOF(1)

                        Field #1, 15 As Q$(0)

                        numberTeams = fileLength& / TEAM_SIZE_BYTES

                        For teamIdx = 1 To numberTeams
                            Get #1, teamIdx
                            'numberTeams = numberTeams + 1
                            teamNames$(teamIdx) = Q$(0)
                            teamIndex%(teamIdx) = teamIdx
                        Next teamIdx

                        Close #1

                        previousYear$ = yearNumber$

                    End If 'Done checking if file exists

                End If 'Done checking for blank year number

            Else

                yearNumber$ = previousYear$

            End If 'Done checking for ENTER key

            'Locate 2, 33
            'Color 31: Print "LOADING..."

            Do

                Call SelectGameTeam(numberTeams, yearNumber$, chosenIdx, teamName$, ExitCode%, gameIdx)

                If ExitCode% <> 27 And numberTeams <> 0 Then

                    Do
                        Locate 2, 33
                        Color teamRat_GAME(gameIdx, 2), teamRat_GAME(gameIdx, 1): Print teamName$

                        Color 15, 4
                        Locate 3, 32: Print "ANY CHANGE? (Y/N)";

                        J$ = GetKeyPress$

                    Loop Until J$ = Chr$(27) Or UCase$(J$) = "Y" Or UCase$(J$) = "N"

                End If

            Loop Until ExitCode% = 27 Or J$ = Chr$(27) Or UCase$(J$) = "N"

        End If 'Done checking for ESC

    Loop Until UCase$(J$) = "N" Or I$ = Chr$(27) 'Or J$ = Chr$(27)

    If I$ = Chr$(27) Or J$ = Chr$(27) Then chosenIdx = 0

End Sub


'----------------------------------------
'       SelectGameTeam Subroutine
'----------------------------------------
'This subroutine provides an interface for
'selecting a team from the list loaded by
'the disk ID.
Sub SelectGameTeam (numberTeams, yearNumber$, selectedIdx, teamName$, ExitCode%, gameIdx)

    If numberTeams > 0 Then

        SELECTION% = 1
        Count% = numberTeams
        Start% = 1
        FileFGColor% = 15: FileBGColor% = 4
        HiLiteFGColor% = 0: HiLiteBGColor% = 7
        Rows% = 20
        Columns% = 4
        Spacing% = 6
        Row% = 4
        Column% = 1

        Color , 4
        Cls
        Color 14
        Locate , 33: Print "TEAM SELECTION"
        Locate 25, 1
        Color 14: Print "ESC";: Color 15: Print "-SELECT NEW ID  PGDN  PGUP  ";
        Color 14: Print "ENTER";: Color 15: Print "-SELECTS TEAM";

        _MouseShow "DEFAULT"
        MAMenu teamNames$(), SELECTION%, Start%, Count%, ExitCode%, FileFGColor%, FileBGColor%, HiLiteFGColor%, HiLiteBGColor%, Rows%, Columns%, Spacing%, Row%, Column%
        _MouseHide

        If ExitCode% <> 27 Then

            selectedIdx = teamIndex%(SELECTION%)

            'Locate 2, 33
            'Color 31: Print "LOADING..."

            Call ReadGameTeam(yearNumber$, teamName$, selectedIdx, gameIdx)

        End If

    End If

End Sub


'----------------------------------------
'        ReadGameTeam Subroutine
'----------------------------------------
'This subroutine reads in the data for the
'selected tema into each of the data arrays
'needed by the game.
Sub ReadGameTeam (teamYear$, targetTeam$, teamIdx, gameIdx)

    Open diskPaths$(0) + TEAM_FILE_NAME$ + "." + teamYear$ For Random As #1 Len = TEAM_SIZE_BYTES

    Field #1, 15 As Q$(1), 15 As Q$(2), 2 As Q$(3), 2 As Q$(4), 2 As Q$(5), 2 As Q$(6), 2 As Q$(7), 2 As Q$(8), 2 As Q$(9), 2 As Q$(10), 2 As Q$(11), 2 As Q$(12), 2 As Q$(13), 2 As Q$(14), 2 As Q$(15), 200 As Q$(0), 200 As Q$(199), 200 As Q$, 200 As Q$, 78 As Q$(0)

    Field #1, 56 As Q$(0), 12 As Q$(16), 2 As Q$(17), 2 As Q$(18), 2 As Q$(19), 2 As Q$(20), 12 As Q$(21), 2 As Q$(22), 2 As Q$(23), 2 As Q$(24), 2 As Q$(25), 200 As Q$, 200 As Q$(200), 238 As Q$(201), 200 As Q$

    Field #1, 96 As Q$(0), 12 As Q$(26), 2 As Q$(27), 2 As Q$(28), 2 As Q$(29), 2 As Q$(30), 12 As Q$(31), 2 As Q$(32), 2 As Q$(33), 2 As Q$(34), 2 As Q$(35), 200 As Q$(199), 200 As Q$(200), 198 As Q$(201), 200 As Q$

    Field #1, 136 As Q$(0), 12 As Q$(36), 2 As Q$(37), 2 As Q$(38), 2 As Q$(39), 2 As Q$(40), 12 As Q$(41), 2 As Q$(42), 2 As Q$(43), 2 As Q$(44), 2 As Q$(45), 200 As Q$(199), 200 As Q$(200), 158 As Q$(201), 200 As Q$

    Field #1, 176 As Q$(0), 12 As Q$(46), 2 As Q$(47), 2 As Q$(48), 2 As Q$(49), 2 As Q$(50), 12 As Q$(51), 2 As Q$(52), 2 As Q$(53), 2 As Q$(54), 2 As Q$(55), 200 As Q$(199), 200 As Q$(200), 118 As Q$(201), 200 As Q$

    Field #1, 216 As Q$(0), 12 As Q$(56), 2 As Q$(57), 2 As Q$(58), 2 As Q$(59), 2 As Q$(60), 12 As Q$(61), 2 As Q$(62), 2 As Q$(63), 2 As Q$(64), 2 As Q$(65), 200 As Q$(199), 200 As Q$(200), 78 As Q$(201), 200 As Q$

    Field #1, 200 As Q$(0), 56 As Q$, 12 As Q$(66), 2 As Q$(67), 2 As Q$(68), 12 As Q$(69), 2 As Q$(70), 2 As Q$(71), 12 As Q$(72), 2 As Q$(73), 2 As Q$(74), 200 As Q$(99), 200 As Q$(200), 200 As Q$, 30 As Q$

    Field #1, 200 As Q$(0), 100 As Q$, 4 As Q$(201), 12 As Q$(75), 2 As Q$(76), 2 As Q$(77), 12 As Q$(78), 2 As Q$(79), 2 As Q$(80), 12 As Q$(81), 2 As Q$(82), 2 As Q$(83), 200 As Q$(199), 182 As Q$(200), 200 As Q$

    Field #1, 200 As Q$(0), 152 As Q$(201), 12 As Q$(84), 2 As Q$(85), 2 As Q$(86), 2 As Q$(87), 2 As Q$(88), 12 As Q$(89), 2 As Q$(90), 2 As Q$(91), 2 As Q$(92), 2 As Q$(93), 200 As Q$(199), 200 As Q$(200), 142 As Q$(202)

    Field #1, 200 As Q$(0), 192 As Q$(201), 12 As Q$(94), 2 As Q$(95), 2 As Q$(96), 2 As Q$(97), 2 As Q$(98), 12 As Q$(99), 2 As Q$(100), 2 As Q$(101), 2 As Q$(102), 2 As Q$(103), 200 As Q$(199), 200 As Q$(200), 102 As Q$(202)

    Field #1, 200 As Q$(0), 200 As Q$(199), 32 As Q$(200), 12 As Q$(104), 2 As Q$(105), 2 As Q$(106), 12 As Q$(107), 2 As Q$(108), 2 As Q$(109), 12 As Q$(110), 2 As Q$(111), 2 As Q$(112), 200 As Q$(201), 200 As Q$(202), 54 As Q$(203)

    Field #1, 200 As Q$(0), 200 As Q$(199), 80 As Q$(200), 12 As Q$(113), 2 As Q$(114), 2 As Q$(115), 12 As Q$(116), 2 As Q$(117), 2 As Q$(118), 12 As Q$(119), 2 As Q$(120), 2 As Q$(121), 200 As Q$(201), 200 As Q$(202), 6 As Q$(203)

    Field #1, 200 As Q$, 200 As Q$, 128 As Q$, 12 As Q$(122), 2 As Q$(123), 12 As Q$(124), 2 As Q$(125), 2 As Q$(126), 2 As Q$(127), 2 As Q$(128), 12 As Q$(129), 2 As Q$(130), 2 As Q$(131), 2 As Q$(132), 2 As Q$(133), 2 As Q$(134), 200 As Q$, 150 As Q$

    Field #1, 200 As Q$(0), 200 As Q$(199), 184 As Q$(200), 12 As Q$(135), 2 As Q$(136), 12 As Q$(137), 2 As Q$(138), 12 As Q$(139), 2 As Q$(140), 12 As Q$(141), 2 As Q$(142), 12 As Q$(143), 2 As Q$(144), 200 As Q$(201), 80 As Q$(202)

    Field #1, 200 As Q$(0), 100 As Q$, 200 As Q$(199), 154 As Q$(200), 12 As Q$(145), 2 As Q$(146), 12 As Q$(147), 2 As Q$(148), 12 As Q$(149), 2 As Q$(150), 12 As Q$(151), 2 As Q$(152), 12 As Q$(153), 2 As Q$(154), 200 As Q$(201), 10 As Q$(202)

    Field #1, 200 As Q$(0), 100 As Q$, 200 As Q$(199), 224 As Q$(200), 12 As Q$(155), 2 As Q$(156), 12 As Q$(157), 2 As Q$(158), 12 As Q$(159), 2 As Q$(160), 12 As Q$(161), 2 As Q$(162), 12 As Q$(163), 2 As Q$(164), 140 As Q$(201)

    Field #1, 200 As Q$(0), 200 As Q$, 200 As Q$(199), 194 As Q$(200), 12 As Q$(165), 2 As Q$(166), 12 As Q$(167), 2 As Q$(168), 12 As Q$(169), 2 As Q$(170), 12 As Q$(171), 2 As Q$(172), 12 As Q$(173), 2 As Q$(174), 70 As Q$(201)

    Field #1, 232 As Q$(0), 200 As Q$, 200 As Q$(199), 232 As Q$(200), 12 As Q$(175), 2 As Q$(176), 12 As Q$(177), 2 As Q$(178), 12 As Q$(179), 2 As Q$(180), 12 As Q$(181), 2 As Q$(182), 12 As Q$(183), 2 As Q$(184)

    Get #1, teamIdx

    targetTeam$ = RTrim$(Q$(1))
    gameMascots$(gameIdx) = RTrim$(Q$(2))

    For I1 = 0 To 7
        leagRat_GAME(gameIdx, I1) = CVI(Q$(I1 + 3))
    Next I1

    For I1 = 0 To 4
        teamRat_GAME(gameIdx, I1) = CVI(Q$(I1 + 11))
    Next I1

    For I1 = 0 To 9
        rbacks$(gameIdx, I1) = RTrim$(Q$(I1 * 5 + 16))
        rbRushContrib(gameIdx, I1) = CVI(Q$(I1 * 5 + 17))
        rbRushAvg(gameIdx, I1) = CVI(Q$(I1 * 5 + 18))
        rbNumRec(gameIdx, I1) = CVI(Q$(I1 * 5 + 19))
        rbYdsPerC(gameIdx, I1) = CVI(Q$(I1 * 5 + 20))
    Next I1

    For I1 = 0 To 5
        wdRec$(gameIdx, I1) = RTrim$(Q$(I1 * 3 + 66))
        wrNumRec(gameIdx, I1) = CVI(Q$(I1 * 3 + 67))
        wrYdsPerC(gameIdx, I1) = CVI(Q$(I1 * 3 + 68))
    Next I1

    For I1 = 0 To 3
        qbacks$(gameIdx, I1) = RTrim$(Q$(I1 * 5 + 84))
        qbNumAtt(gameIdx, I1) = CVI(Q$(I1 * 5 + 85))
        qbCompPct(gameIdx, I1) = CVI(Q$(I1 * 5 + 86))
        qbPctInt(gameIdx, I1) = CVI(Q$(I1 * 5 + 87))
        qbArmRat(gameIdx, I1) = CVI(Q$(I1 * 5 + 88))
    Next I1

    For I1 = 0 To 2
        kickRet$(gameIdx, I1) = RTrim$(Q$(I1 * 3 + 104))
        krNumRet(gameIdx, I1) = CVI(Q$(I1 * 3 + 105))
        krYdsPerRet(gameIdx, I1) = CVI(Q$(I1 * 3 + 106))
    Next I1

    For I1 = 0 To 2
        pret$(gameIdx, I1) = RTrim$(Q$(I1 * 3 + 113))
        prNumRet(gameIdx, I1) = CVI(Q$(I1 * 3 + 114))
        prYdsPerRet(gameIdx, I1) = CVI(Q$(I1 * 3 + 115))
    Next I1

    punter$(gameIdx, 0) = RTrim$(Q$(122))
    ydsPerPunt(gameIdx, 0) = CVI(Q$(123))

    kicker$(gameIdx, 0) = RTrim$(Q$(124))
    kickerFGA(gameIdx, 0) = CVI(Q$(125))
    kickerFGPct(gameIdx, 0) = CVI(Q$(126))
    kickerIdx(gameIdx, 0) = CVI(Q$(127))
    kickerPATPct(gameIdx, 0) = CVI(Q$(128))

    kicker$(gameIdx, 1) = Q$(129)
    kickerFGA(gameIdx, 1) = CVI(Q$(130))
    kickerFGPct(gameIdx, 1) = CVI(Q$(131))
    kickerIdx(gameIdx, 1) = CVI(Q$(132))
    kickerPATPct(gameIdx, 1) = CVI(Q$(133))

    ydsPerComp(gameIdx) = CVI(Q$(134))

    For I1 = 0 To 14
        defSacks$(gameIdx, I1) = Q$(I1 * 2 + 135)
        playerSacks(gameIdx, I1) = CVI(Q$(I1 * 2 + 136))
    Next I1

    For I1 = 0 To 9
        defInts$(gameIdx, I1) = Q$(I1 * 2 + 165)
        playerInts(gameIdx, I1) = CVI(Q$(I1 * 2 + 166))
    Next I1

    Close #1

    If Left$(gameMascots$(gameIdx), 1) = " " Or gameMascots$(gameIdx) = "" Then gameMascots$(gameIdx) = gameTeams$(gameIdx)

    indRushPct(gameIdx, 0) = rbRushContrib(gameIdx, 0)

    For currBack = 9 To 1 Step -1
        If rbRushContrib(gameIdx, currBack) <> 0 Then
            indRushPct(gameIdx, currBack) = rbRushContrib(gameIdx, currBack) - rbRushContrib(gameIdx, currBack - 1)
        End If
    Next

    teamSacks(gameIdx) = 0
    teamInts(gameIdx) = 0

    For I1 = 0 To 14
        teamSacks(gameIdx) = teamSacks(gameIdx) + playerSacks(gameIdx, I1)
        If playerSacks(gameIdx, 0) > 0 Then defSacks(gameIdx) = 1
    Next I1

    For I1 = 0 To 9
        teamInts(gameIdx) = teamInts(gameIdx) + playerInts(gameIdx, I1)
        If playerInts(gameIdx, 0) > 0 Then defInts(gameIdx) = 1
    Next I1

End Sub


'----------------------------------------
'      GetKeyWhenNoDelay Subroutine
'----------------------------------------
' ...explanation...
Sub GetKeyWhenNoDelay ()

    'If we're not in autoplay
    If Not (autoPlay = 1 And playerMode = 2) Then
        I$ = GetKeyPress$
    End If

End Sub


'----------------------------------------
'          EndOfGame Subroutine
'----------------------------------------
'This subroutine serves as the final portion
'of a post game routine. At this point,
'stats have been saved and we are simply
'looking to determine whether a new game
'should be started.
Sub EndOfGame

    Call POSTGAME

    If autoPlay <> 1 Then

        Cls

        'Using Home Team's color for the box
        Call TEAMBOX(1, 1)

        Do
            pbpString$ = "PLAY ANOTHER GAME? (Y/N)"
            Locate 7, (80 - Len(pbpString$)) / 2
            Print pbpString$

            Color 15, 0
            I$ = GetKeyPress$
        Loop Until UCase$(I$) = "Y" Or UCase$(I$) = "N"

    End If

    Close #11

    If UCase$(I$) = "Y" Or (autoPlay = 1 And endAllGames <> 1) Then
        Cls
        Print "PLEASE WAIT ..."
        Call FOOTBALL
    End If

End Sub
